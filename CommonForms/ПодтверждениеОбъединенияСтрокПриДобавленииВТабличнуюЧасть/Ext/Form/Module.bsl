#Область ОписаниеПеременных

&НаКлиенте
Перем ЗакрытьССохранением; // Сохранить значение при закрытии формы

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ОбновитьТекстДекорации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗакрытьССохранением = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НЕ ЗавершениеРаботы И ЗакрытьССохранением И ЗапомнитьВыбранноеЗначение Тогда
		СохранитьНастройку();
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗапомнитьВыбранноеЗначениеПриИзменении(Элемент)
	
	Элементы.ДекорацияПодсказка.Видимость = ЗапомнитьВыбранноеЗначение;
	
КонецПроцедуры

&НаКлиенте
Процедура СбрасыватьКоличествоПриИзменении(Элемент)
	
	Если НастройкаОбъединенияСтрокПриДобавлении = 0 Тогда
		ОбъединятьСтрокиПриДобавлении = Ложь;
	Иначе
		ОбъединятьСтрокиПриДобавлении = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	ЗакрытьССохранением = Истина;
	ЗаписатьИЗакрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ОбновитьТекстДекорации()

	НастройкиКоличества = ДокументыУНФКлиентСервер.ПараметрыСбросаКоличества();
	ДокументыУНФ.ЗаполнитьНастройкиКоличества(НастройкиКоличества);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиКоличества);
	
	Если НЕ ОбъединятьСтрокиПриДобавлении Тогда
		НастройкаОбъединенияСтрокПриДобавлении = 0;
	Иначе
		НастройкаОбъединенияСтрокПриДобавлении = 1;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СохранитьНастройку()
	
	НазваниеНастройки = "ПриДобавленииСтрокВТабличнуюЧасть";
	Если НастройкаОбъединенияСтрокПриДобавлении = 0 Тогда
		ЗначениеНастройки = Перечисления.ВариантыДобавленияСтрокВТабличнуюЧасть.ДобавлятьНовую;
	Иначе
		ЗначениеНастройки = Перечисления.ВариантыДобавленияСтрокВТабличнуюЧасть.Объединять;
	КонецЕсли;
	
	РегистрыСведений.НастройкиПользователей.Установить(ЗначениеНастройки, НазваниеНастройки);
	//ХранилищеСистемныхНастроек.Сохранить("ОбщиеНастройкиФорм", "ЗапрашиватьОбъединениеСтрок", НЕ ЗапомнитьВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть()
	
	НастройкаКоличества = Новый Структура();
	
	НастройкаКоличества.Вставить("ОбъединятьСтрокиПриДобавлении", ОбъединятьСтрокиПриДобавлении);
	НастройкаКоличества.Вставить("ДобавлятьНовуюСтроку", Не ОбъединятьСтрокиПриДобавлении);
	НастройкаКоличества.Вставить("ЗапрашиватьОбъединениеСтрок", НЕ ЗапомнитьВыбранноеЗначение);
	
	Закрыть(НастройкаКоличества);

КонецПроцедуры

#КонецОбласти
