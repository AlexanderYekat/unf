#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиОбновления

Процедура ЗаполнитьГоловнуюОрганизацию() Экспорт
	
	ОбработатьСправочникОрганизации();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыПоЕдиномуНалоговомуСчету.Регистратор КАК Регистратор,
	|	РасчетыПоЕдиномуНалоговомуСчету.Организация КАК Организация,
	|	РасчетыПоЕдиномуНалоговомуСчету.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|ИЗ
	|	РегистрНакопления.РасчетыПоЕдиномуНалоговомуСчету КАК РасчетыПоЕдиномуНалоговомуСчету
	|ГДЕ
	|	РасчетыПоЕдиномуНалоговомуСчету.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоЕдиномуНалоговомуСчету.Регистратор,
	|	РасчетыПоЕдиномуНалоговомуСчету.Организация,
	|	РасчетыПоЕдиномуНалоговомуСчету.Организация.ГоловнаяОрганизация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НачатьТранзакцию();
		НаборЗаписейРегистра = РегистрыНакопления.РасчетыПоЕдиномуНалоговомуСчету.СоздатьНаборЗаписей();
		ОтборПоРегистратору = НаборЗаписейРегистра.Отбор.Регистратор;
		ОтборПоРегистратору.Установить(Выборка.Регистратор);
		НаборЗаписейРегистра.Прочитать();
		ТаблицаЗаписей = НаборЗаписейРегистра.Выгрузить();
		ТаблицаЗаписей.ЗаполнитьЗначения(Выборка.ГоловнаяОрганизация, "ГоловнаяОрганизация");
		НаборЗаписейРегистра.Загрузить(ТаблицаЗаписей);
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписейРегистра);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ШаблонСообщения = НСтр("ru = 'Не выполнено обновление записей регистра накопления ""Расчеты по единому налоговому счету""
			|%1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыНакопления.РасчетыПоЕдиномуНалоговомуСчету,,
				ТекстСообщения);
			Продолжить;
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьСправочникОрганизации() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 500
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И Организации.ПометкаУдаления = ЛОЖЬ";
	
	НачатьТранзакцию();
	Попытка
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		ТекущаяСсылка = Неопределено;
		
		Пока Выборка.Следующий() Цикл
			
			ТекущаяСсылка = Выборка.Ссылка;
			ТекущийОбъект = ТекущаяСсылка.ПолучитьОбъект();
			ТекущийОбъект.Заблокировать();
			
			ТекущийОбъект.ГоловнаяОрганизация = ТекущаяСсылка;
			
			ТекущийОбъект.ОбменДанными.Загрузка = Истина;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ТекущийОбъект);
			ТекущийОбъект.Разблокировать();
		
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ИмяСобытия = НСтр("ru = 'Выполнение обработчика ОбработатьСправочникОрганизации'",
		ОбщегоНазначения.КодОсновногоЯзыка());
		
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , ТекущаяСсылка,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
	КонецПопытки;
	
КонецПроцедуры 

#КонецОбласти

#КонецОбласти

#КонецЕсли
