
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.ВалютаПоУмолчанию) Тогда
		
		Объект.ВалютаПоУмолчанию = Константы.НациональнаяВалюта.Получить();
		
	КонецЕсли;
	
	ЗаполнитьОстаткиПоКассе();
	НастроитьВидимостьРеквизитовВалюты();
	
	ОтчетыУНФ.ПриСозданииНаСервереФормыСвязанногоОбъекта(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменилисьСчетаКассы" Тогда
		Объект.СчетУчета = Параметр.СчетУчета;
		Модифицированность = Истина;
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_НаборКонстант" Тогда // Не локализуется
		Если Источник = "ФункциональнаяУчетВалютныхОпераций" Тогда // Не локализуется
			НастроитьВидимостьРеквизитовВалюты();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВалютаДляВключенияОпцииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасчетыРаботаСФормамиКлиент.ОткрытьДиалогВключенияНесколькихВалют();
	
КонецПроцедуры

&НаКлиенте
Процедура ОстатокДенежныхСредствНажатие(Элемент)
	
	РаботаСФормойДокументаКлиент.ОткрытьОтчетДенежныеСредства(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОстатокДенежныхСредствОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСФормойДокументаКлиент.ОткрытьОтчетДенежныеСредства(Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервере
Процедура НастроитьВидимостьРеквизитовВалюты()
	
	Если ВозможностиПриложения.ЭтоРозница()
		ИЛИ ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций") Тогда
		Элементы.ВалютаДляВключенияОпции.Видимость = Ложь;
	Иначе
		Элементы.ВалютаДляВключенияОпции.Видимость = Истина;
		ВалютаДляВключенияОпции = Константы.НациональнаяВалюта.Получить();
		Элементы.ВалютаДляВключенияОпции.СписокВыбора.Добавить(Строка(ВалютаДляВключенияОпции));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОстаткиПоКассе()
	
	Если НЕ Пользователи.РолиДоступны("ЧтениеОстатковДенежныхСредств") ИЛИ НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Элементы.ОстатокДенежныхСредств.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	КрупныйШрифт =  ШрифтыСтиля.ШрифтВспомогательнойНадписи11; // Новый Шрифт(,11);
	МелкийШрифт  = ШрифтыСтиля.МелкийШрифтТекста; // Новый Шрифт(,8);
	
	КомпонентыФС = Новый Массив;
	КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Остаток'") + " ", КрупныйШрифт));
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДенежныеСредстваОстатки.Валюта,
		|	ДенежныеСредстваОстатки.Валюта.СимвольноеПредставление КАК СимвольноеПредставление,
		|	ДенежныеСредстваОстатки.Валюта.Представление КАК ПредставлениеВалюты,
		|	ДенежныеСредстваОстатки.СуммаВалОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.ДенежныеСредства.Остатки(, БанковскийСчетКасса = &Касса) КАК ДенежныеСредстваОстатки";
	
	Запрос.УстановитьПараметр("Касса", Объект.Ссылка);
	
	ВыборкаОстатков = Запрос.Выполнить().Выбрать();
	
	НомерОстатка = 0;
	КомпонентыЧисла = Новый Массив;
	Пока ВыборкаОстатков.Следующий() Цикл
		
		НомерОстатка = НомерОстатка + 1;
		
		СуммаСтрокой = Формат(ВыборкаОстатков.Сумма, "ЧДЦ=2; ЧРД=,; ЧРГ=' '; ЧН=0,00");
		ПозицияРазделителя = СтрНайти(СуммаСтрокой, ",");
		
		КомпонентыЧисла.Добавить(Новый ФорматированнаяСтрока(Лев(СуммаСтрокой, ПозицияРазделителя), КрупныйШрифт));
		КомпонентыЧисла.Добавить(Новый ФорматированнаяСтрока(Сред(СуммаСтрокой, ПозицияРазделителя+1), МелкийШрифт));
		
		СимвольноеПредставление = ?(СокрЛП(ВыборкаОстатков.СимвольноеПредставление) = "", ВыборкаОстатков.ПредставлениеВалюты, ВыборкаОстатков.СимвольноеПредставление);
		КомпонентыЧисла.Добавить(Новый ФорматированнаяСтрока(" " + СимвольноеПредставление, КрупныйШрифт));
		Если ВыборкаОстатков.Количество() > НомерОстатка Тогда
			КомпонентыЧисла.Добавить(Новый ФорматированнаяСтрока(" • ", КрупныйШрифт));
		КонецЕсли;
		
	КонецЦикла;
	
	КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(КомпонентыЧисла, , , , "Остаток"));
	
	СтрокаОстатков = Новый ФорматированнаяСтрока(КомпонентыФС, , ЦветаСтиля.ТекстВторостепеннойНадписи);
	
	Элементы.ОстатокДенежныхСредств.Заголовок = СтрокаОстатков;
	
КонецПроцедуры

#КонецОбласти
