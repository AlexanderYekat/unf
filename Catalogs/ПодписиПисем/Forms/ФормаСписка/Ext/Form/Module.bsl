
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастроитьФормуПоПараметрам(Параметры);
	ИзменитьФормуПоРолиПользователя();
	ИзменитьФормуДляМобильногоКлиента();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ОбработкаЗаписиПодписи(ИмяСобытия, Параметр);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если Элементы.Предпросмотр.Видимость Тогда
		ПодключитьОбработчикОжидания("ОбработчикСписокПриАктивизацииСтроки", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Не ЭтоФормаСписка Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Элемент.ТекущиеДанные.Ссылка);
	
	ОткрытьФорму(
	"Справочник.ПодписиПисем.Форма.ФормаЭлемента",
	ПараметрыФормы,
	ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПредпросмотрДокументСформирован(Элемент)
	
	ГипертекстКлиент.ПрименитьНастройкиОтображения(Элемент.Документ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредпросмотрПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	ГипертекстКлиент.ДокументHTMLПриНажатии(ДанныеСобытия, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Настройки(Команда)
	
	ОткрытьФорму("Справочник.ПодписиПисем.Форма.Настройки", , ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаСобытийФормы

#Область ОбработкаОповещений

&НаКлиенте
Процедура ОбработкаЗаписиПодписи(ИмяСобытия, Параметр)
	
	Если ИмяСобытия <> "Запись_ПодписьПисем" Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаЗаписиПодписиНаСервере(Параметр);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаЗаписиПодписиНаСервере(Знач Ссылка)
	
	СодержаниеПодписи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Содержание");
	
	Если СтрНайти(СодержаниеПодписи, "<html") Тогда
		Содержание = СодержаниеПодписи;
	Иначе
		Содержание = ГипертекстВызовСервера.HTMLИзПростогоТекста(СодержаниеПодписи);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ВидФормы

&НаСервере
Процедура НастроитьФормуПоПараметрам(Параметры)
	
	Параметры.Свойство("ЭтоФормаСписка", ЭтоФормаСписка);
	Элементы.Список.РежимВыбора = Не ЭтоФормаСписка;
	
	ТолькоСписок = Неопределено;
	Параметры.Свойство("ТолькоСписок", ТолькоСписок);
	Если ТолькоСписок = Истина Тогда
		ОставитьНаФормеТолькоСписок();
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоФормаСписка Тогда
		УстановитьОтборПоВладельцу();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОставитьНаФормеТолькоСписок()
	
	УстановитьОтборПоВладельцу();
	
	Элементы.Предпросмотр.Видимость = Ложь;
	Элементы.Настройки.Видимость = Ложь;
	
	АвтоЗаголовок = Ложь;
	Заголовок = НСтр("ru = 'Подписи писем'");
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьФормуПоРолиПользователя()
	
	Если Пользователи.ЭтоПолноправныйПользователь(Пользователи.ТекущийПользователь()) Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.ВладелецПодписи.Видимость Тогда
		УстановитьОтборПоВладельцу();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоВладельцу()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
	"ВладелецПодписи", Пользователи.ТекущийПользователь(), , , Истина);
	
	Элементы.ВладелецПодписи.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьФормуДляМобильногоКлиента()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Переместить(Элементы.Настройки, ЭтотОбъект);
	Элементы.ГруппаПредпросмотр.Видимость = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область Содержание

&НаКлиенте
Процедура ОбработчикСписокПриАктивизацииСтроки()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(ТекущиеДанные.Содержание, "<html") Тогда
		Содержание = ТекущиеДанные.Содержание;
	Иначе
		Содержание = ГипертекстВызовСервера.HTMLИзПростогоТекста(ТекущиеДанные.Содержание);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
