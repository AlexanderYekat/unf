
#Область ОписаниеПеременных

&НаКлиенте
Перем ОкноHTMLДокумента; // см СодержаниеДокументСформирован

&НаКлиенте
Перем ДокументHTML; // см СодержаниеДокументСформирован

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		
		РаботаССодержаниемДляНовогоОбъекта();
		
	Иначе
		
		РаботаССодержаниемДляСуществующегоОбъекта();
		
	КонецЕсли;
	
	ИзначальноеЗаполнениеРеквизитов();
	УстановитьНачальныйВидФормы();
	ИзменитьФормуДляМобильногоКлиента();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаполнениеРеквизитовСуществующегоОбъекта();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ТипСодержанияПисьма = ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.HTML") Тогда
		
		Если ПустаяСтрока(ДокументHTML.body.style.lineHeight) Тогда
			Результат = СтрШаблон("<html><body>%1</body></html>", ДокументHTML.body.innerHTML);
		Иначе
			Результат = СтрШаблон("<html><body style=""line-height:%1;"">%2</body></html>",
			ДокументHTML.body.style.lineHeight,
			ДокументHTML.body.innerHTML);
		КонецЕсли;
		
		Объект.Содержание = Результат;
		
	Иначе
		Объект.Содержание = Содержание;
	КонецЕсли;
	
	Если Не ПараметрыЗаписи.Свойство("НеНужноЗадаватьВопрос") Тогда
		
		Если НужноЗадатьВопросОбУстановкеОсновной(Объект.Ссылка, Объект.ВладелецПодписи) Тогда
			ВопросОбУстановкеОсновнойПодписи();
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ПодписьПисем", Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПредыдущийВладелецПодписи <> Справочники.ПодписиПисем.ПустаяСсылка()
		И ПредыдущийВладелецПодписи <> Объект.ВладелецПодписи Тогда
		
		Справочники.ПодписиПисем.ОтменитьИспользованиеПодписиКакОсновной(Объект.Ссылка, ПредыдущийВладелецПодписи);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СодержаниеДокументСформирован(Элемент)
	
	ГипертекстКлиент.ДокументHTMLСформирован(
	Элемент,
	ДокументHTML,
	ОкноHTMLДокумента,
	ТекущийЭлемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СодержаниеПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	ЭлементыКомандВставкиТаблиц = Новый Массив;
	ЭлементыКомандВставкиТаблиц.Добавить(Элементы.КомандаВставитьТаблицу);
	
	ЭлементыКомандИзмененияТаблиц = Новый Массив;
	ЭлементыКомандИзмененияТаблиц.Добавить(Элементы.КомандаИзменитьТаблицу);
	ЭлементыКомандИзмененияТаблиц.Добавить(Элементы.КомандаИзменитьТаблицу1);
	
	ГипертекстКлиент.РедактируемоеПолеHTMLДокументаПриНажатии(
	ДанныеСобытия,
	СтандартнаяОбработка,
	Элементы,
	Элементы.КомандыФорматированияСодержания.Имя,
	ЭлементыКомандВставкиТаблиц,
	ЭлементыКомандИзмененияТаблиц);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область КомандыHTMLПоля

&НаКлиенте
Процедура ОбработатьHTMLКоманду(Команда)
	
	НазваниеКоманды = Сред(Команда.Имя, 8, СтрДлина(Команда.Имя));
	ГипертекстКлиент.ВыполнитьHTMLКоманду(НазваниеКоманды, ПараметрыДляПередачиВМодульГипертекст());
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьШрифт(Команда)
	
	ГипертекстКлиент.ИзменитьШрифт(ПараметрыДляПередачиВМодульГипертекст());
	
КонецПроцедуры

&НаКлиенте
Процедура УвеличитьШрифт(Команда)
	
	ГипертекстКлиент.ИзменитьРазмерШрифта(ПараметрыДляПередачиВМодульГипертекст(), 1);
	
КонецПроцедуры

&НаКлиенте
Процедура УменьшитьШрифт(Команда)
	
	ГипертекстКлиент.ИзменитьРазмерШрифта(ПараметрыДляПередачиВМодульГипертекст(), -1);
	
КонецПроцедуры

&НаКлиенте
Процедура ЦветТекста(Команда)
	
	ГипертекстКлиент.ИзменитьЦветТекста(ПараметрыДляПередачиВМодульГипертекст(), "ВыбранныйЦветТекстаHTML");
	
КонецПроцедуры

&НаКлиенте
Процедура ЦветФона(Команда)
	
	ГипертекстКлиент.ИзменитьЦветФонаСТаблицей(ПараметрыДляПередачиВМодульГипертекст(), "ВыбранныйЦветФонаHTML");
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьГиперссылку(Команда)
	
	ГипертекстКлиент.ВставитьГиперссылку(ПараметрыДляПередачиВМодульГипертекст());
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСимвол(Команда)
	
	ГипертекстКлиент.ВставитьСимвол(ПараметрыДляПередачиВМодульГипертекст());
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьИзменитьТаблицу(Команда)
	
	ГипертекстКлиент.ВставитьИзменитьТаблицу(ПараметрыДляПередачиВМодульГипертекст());
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТаблицу(Команда)
	
	ГипертекстКлиент.ИзменитьТаблицу(ПараметрыДляПередачиВМодульГипертекст());
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВставить(Команда)
	
	ГипертекстКлиент.КомандаВставить(ПараметрыДляПередачиВМодульГипертекст());
	
КонецПроцедуры

&НаКлиенте
Процедура Цитирование(Команда)
	
	ГипертекстКлиент.Цитировать(ПараметрыДляПередачиВМодульГипертекст());
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьИзображение(Команда)
	
	ГипертекстКлиент.ВставитьИзображение(ПараметрыДляПередачиВМодульГипертекст());
	
КонецПроцедуры

&НаКлиенте
Процедура МеждустрочныйИнтервал(Команда)
	
	ГипертекстКлиент.ИзменитьМеждустрочныйИнтервал(ПараметрыДляПередачиВМодульГипертекст());
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВернуть(Команда)
	ОкноHTMLДокумента.redo();
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтменить(Команда)
	ОкноHTMLДокумента.undo();
КонецПроцедуры

&НаКлиенте
Процедура ПереключательHTML(Команда)
	
	ПараметрыДляПередачи = Новый Структура;
	ПараметрыДляПередачи.Вставить("Форма", ЭтотОбъект);
	ПараметрыДляПередачи.Вставить("НазваниеРеквизитаСодержания", "Содержание");
	ПараметрыДляПередачи.Вставить("НазваниеРеквизитаТипаСодержания", "ТипСодержанияПисьма");
	ПараметрыДляПередачи.Вставить("Страницы", Элементы.СтраницыТипыСодержаний);
	ПараметрыДляПередачи.Вставить("СтраницаHTML", Элементы.СтраницаТекстHTML);
	ПараметрыДляПередачи.Вставить("СтраницаПростойТекст", Элементы.СтраницаПростойТекст);
	ПараметрыДляПередачи.Вставить("ГруппаСКомандамиПереключения", Элементы.АктивныйВидПисьма);
	ПараметрыДляПередачи.Вставить("КомандаHTML", Элементы.ФормаПереключательHTML);
	ПараметрыДляПередачи.Вставить("КомандаПростойТекст", Элементы.ФормаПереключательПростойТекст);
	ПараметрыДляПередачи.Вставить("ДокументHTML", ДокументHTML);
	ПараметрыДляПередачи.Вставить("ПолеHTML", Элементы.Содержание);
	
	ГипертекстКлиент.ПереключательHTML(ПараметрыДляПередачи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательПростойТекст(Команда)
	
	ПараметрыДляПередачи = Новый Структура;
	ПараметрыДляПередачи.Вставить("Форма", ЭтотОбъект);
	ПараметрыДляПередачи.Вставить("НазваниеРеквизитаСодержания", "Содержание");
	ПараметрыДляПередачи.Вставить("НазваниеРеквизитаТипаСодержания", "ТипСодержанияПисьма");
	ПараметрыДляПередачи.Вставить("Страницы", Элементы.СтраницыТипыСодержаний);
	ПараметрыДляПередачи.Вставить("СтраницаHTML", Элементы.СтраницаТекстHTML);
	ПараметрыДляПередачи.Вставить("СтраницаПростойТекст", Элементы.СтраницаПростойТекст);
	ПараметрыДляПередачи.Вставить("ГруппаСКомандамиПереключения", Элементы.АктивныйВидПисьма);
	ПараметрыДляПередачи.Вставить("КомандаHTML", Элементы.ФормаПереключательHTML);
	ПараметрыДляПередачи.Вставить("КомандаПростойТекст", Элементы.ФормаПереключательПростойТекст);
	ПараметрыДляПередачи.Вставить("ДокументHTML", ДокументHTML);
	
	ГипертекстКлиент.ПереключательПростойТекст(ПараметрыДляПередачи);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если НужноЗадатьВопросОбУстановкеОсновной(Объект.Ссылка, Объект.ВладелецПодписи) Тогда
		ВопросОбУстановкеОсновнойПодписи(Истина);
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("НеНужноЗадаватьВопрос", Истина);
	
	Записать(ПараметрыЗаписи);
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаСобытийФормы

&НаСервере
Процедура РаботаССодержаниемДляНовогоОбъекта()
	
	Если Не ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		
		Содержание = "<html><body contenteditable>
		|<div><br></div>
		|<div>------------------------------</div>
		|<div><br></div>
		|</body></html>";
		Гипертекст.ДобавитьJSКод(Содержание);
		
		ТипСодержанияПисьма = Перечисления.ТипыТекстовЭлектронныхПисем.HTML;
		
	Иначе
		
		Если СтрНайти(Объект.Содержание, "<html") Тогда
			
			Содержание = СтрЗаменить(Объект.Содержание, "<body", "<body contenteditable");
			Гипертекст.ДобавитьJSКод(Содержание);
			
			ТипСодержанияПисьма = Перечисления.ТипыТекстовЭлектронныхПисем.HTML;
			
		Иначе
			
			Содержание = Объект.Содержание;
			ТипСодержанияПисьма = Перечисления.ТипыТекстовЭлектронныхПисем.ПростойТекст;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РаботаССодержаниемДляСуществующегоОбъекта()
	
	Если СтрНайти(Объект.Содержание, "<html") Тогда
		
		Содержание = СтрЗаменить(Объект.Содержание, "<body", "<body contenteditable");
		Гипертекст.ДобавитьJSКод(Содержание);
		
		ТипСодержанияПисьма = Перечисления.ТипыТекстовЭлектронныхПисем.HTML;
		
	Иначе
		
		Содержание = Объект.Содержание;
		ТипСодержанияПисьма = Перечисления.ТипыТекстовЭлектронныхПисем.ПростойТекст;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзначальноеЗаполнениеРеквизитов()
	
	ЭтоНовыйОбъект = Объект.Ссылка.Пустая();
	ОбъектСозданКопированием = ЗначениеЗаполнено(Параметры.ЗначениеКопирования);
	
	Если ЭтоНовыйОбъект И Не ОбъектСозданКопированием Тогда
		Объект.ВладелецПодписи = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнениеРеквизитовСуществующегоОбъекта()
	
	ПредыдущийВладелецПодписи = Объект.ВладелецПодписи;
	
КонецПроцедуры

#КонецОбласти

#Область ОсновныеПодписи

&НаСервереБезКонтекста
Функция НужноЗадатьВопросОбУстановкеОсновной(Знач Ссылка, Знач ВладелецПодписи)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Если ВладелецПодписи <> ТекущийПользователь Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Ссылка <> Справочники.ПодписиПисем.ПустаяСсылка() Тогда
		
		ПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления");
		Если Не ПометкаУдаления Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Справочники.ПодписиПисем.ЕстьТолькоЭтаПодпись(Ссылка, ТекущийПользователь)
	И Не Справочники.ПодписиПисем.ЭтоОсновнаяПодпись(Ссылка, ТекущийПользователь);
	
КонецФункции

&НаКлиенте
Асинх Процедура ВопросОбУстановкеОсновнойПодписи(ЗакрытьОкно = Ложь)
	
	Ответ = Ждать ВопросАсинх(
	ТекстВопросаОВставкеПодписей(),
	РежимДиалогаВопрос.ДаНет);
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("НеНужноЗадаватьВопрос", Истина);
	Записать(ПараметрыЗаписи);
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ВопросОбУстановкеОсновнойПодписиНаСервере(Объект.Ссылка);
	КонецЕсли;
	
	Если ЗакрытьОкно Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ТекстВопросаОВставкеПодписей()
	
	КомпонентыФС = Новый Массив;
	
	Компонент1 = Новый ФорматированнаяСтрока(
	НСтр("ru = 'Установить подпись в качестве основной?'"), Новый Шрифт(, , Истина));
	Компонент2 = НСтр("ru = 'Основная подпись вставляется в текст письма автоматически.'");
	Компонент3 = НСтр("ru = 'Изменить настройки подписей можно в их списке по кнопке'");
	Компонент4 = НСтр("ru = '""Настройки основных подписей для учетных записей"".'");
	
	КомпонентыФС.Добавить(Компонент1);
	КомпонентыФС.Добавить(Символы.ПС + Символы.ПС);
	КомпонентыФС.Добавить(Компонент2);
	КомпонентыФС.Добавить(Символы.ПС);
	КомпонентыФС.Добавить(Компонент3);
	КомпонентыФС.Добавить(Символы.ПС);
	КомпонентыФС.Добавить(Компонент4);
	КомпонентыФС.Добавить(".");
	
	Возврат Новый ФорматированнаяСтрока(КомпонентыФС);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ВопросОбУстановкеОсновнойПодписиНаСервере(Знач Ссылка)
	
	Справочники.ПодписиПисем.УстановитьПодписьОсновнойДляВсехУчетныхЗаписей(Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ВидФормы

&НаСервере
Процедура УстановитьНачальныйВидФормы()
	
	ИзменитьФормуПоАктивномуТипуСодержания();
	
	УстановитьВидФормыПоПравамПользователя();
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьФормуПоАктивномуТипуСодержания()
	
	ЭтоФорматHTML = (ТипСодержанияПисьма = Перечисления.ТипыТекстовЭлектронныхПисем.HTML);
	
	Элементы.СтраницыТипыСодержаний.ТекущаяСтраница =
	?(ЭтоФорматHTML, Элементы.СтраницаТекстHTML, Элементы.СтраницаПростойТекст);
	Элементы.АктивныйВидПисьма.Заголовок = ?(ЭтоФорматHTML, "HTML", НСтр("ru = 'Простой текст'"));
	
	Элементы.ФормаПереключательHTML.Пометка = ЭтоФорматHTML;
	Элементы.ФормаПереключательПростойТекст.Пометка = Не ЭтоФорматHTML;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидФормыПоПравамПользователя()
	
	Если Пользователи.ЭтоПолноправныйПользователь(Пользователи.ТекущийПользователь()) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ВладелецПодписи.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьФормуДляМобильногоКлиента()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.КомандыФорматированияСодержания.Видимость = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСHTML

&НаКлиенте
Функция ПараметрыДляПередачиВМодульГипертекст()
	
	Возврат ГипертекстКлиент.ПараметрыДляПередачи(
	ЭтотОбъект,
	ДокументHTML,
	Элементы.Содержание.Имя,
	Элементы.КомандыФорматированияСодержания.Имя);
	
КонецФункции

#КонецОбласти

#КонецОбласти
