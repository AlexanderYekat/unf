
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	ИспользоватьМассовыеРассылкиИнтеграция = ПолучитьФункциональнуюОпцию("ИспользоватьМассовыеРассылкиИнтеграция")
		И ЗначениеЗаполнено(МассовыеРассылкиИнтеграция.ПодключенныйСервис());
	
	Элементы.Список.МножественныйВыбор = ?(Параметры.ЗакрыватьПриВыборе = Неопределено, Ложь, Не Параметры.ЗакрыватьПриВыборе);
	КлючНазначенияИспользования = КлючНазначенияИспользования + "ВыборПодбор";
	РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		
	СервисМассовойРассылки = МассовыеРассылкиИнтеграция.ПодключенныйСервис();
	НастроитьКнопкиСоздать();
	
	Если Параметры.Свойство("СпособОтправки") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,"СпособОтправки", Параметры.СпособОтправки);
	КонецЕсли;
	Если Параметры.Свойство("Состояние") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,"Состояние", Параметры.Состояние);
	КонецЕсли;
	Если Параметры.Свойство("СервисРассылкиИдентификаторАдреснойКниги") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,"СервисРассылкиИдентификаторАдреснойКниги", Параметры.СервисРассылкиИдентификаторАдреснойКниги);
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийКомандФормы

&НаКлиенте
Процедура СоздатьРассылкуСервиса(Команда)
	
	ЗначенияЗаполнения = Новый Структура("СервисМассовойРассылки", СервисМассовойРассылки);
	ОткрытьФорму("Документ.МассоваяРассылка.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьКнопкиСоздать()
	
	Элементы.КомандыСоздатьПодменю.Видимость = ИспользоватьМассовыеРассылкиИнтеграция;
	Элементы.ФормаСоздать.Видимость = Не ИспользоватьМассовыеРассылкиИнтеграция;
	
	Если Не ИспользоватьМассовыеРассылкиИнтеграция Тогда
		Возврат;
	КонецЕсли;
	
	ПредставлениеСервиса = МассовыеРассылкиИнтеграция.ПредставлениеСервиса(СервисМассовойРассылки);
	Элементы.СоздатьРассылкуСервиса.Заголовок = СтрШаблон(НСтр("ru='Рассылка %1'"), ПредставлениеСервиса);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
