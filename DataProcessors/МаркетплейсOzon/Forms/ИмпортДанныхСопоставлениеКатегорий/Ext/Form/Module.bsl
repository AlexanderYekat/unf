&НаКлиенте
Перем ДеревоКатегорийКэш, ИдентификаторКэшаКатегорий;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.Параметры.Свойство( "УчетнаяЗапись", ЭтаФорма.УчетнаяЗаписьМаркетплейса );
	ЭтаФорма.Параметры.Свойство( "ИсточникКатегории", ЭтаФорма.ИсточникКатегории );
	ЭтаФорма.Параметры.Свойство( "ГруппаДляСозданияНовыхТоваров", ЭтаФорма.ГруппаДляСозданияНовыхТоваров );
	
	ПараметрСопоставлениеКатегорий = Неопределено;
	ЭтаФорма.Параметры.Свойство( "СопоставлениеКатегорий", ПараметрСопоставлениеКатегорий );
	Если ТипЗнч( ПараметрСопоставлениеКатегорий ) = Тип( "Соответствие" ) Тогда
		
		Для Каждого ПараметрСопоставлениеКатегорийЭлемент Из ПараметрСопоставлениеКатегорий Цикл
			
			СопоставлениеКатегорийСтрока = СопоставлениеКатегорий.Добавить();
			
			Значение = ПараметрСопоставлениеКатегорийЭлемент.Значение;
			
			СопоставлениеКатегорийСтрока.ИдентификаторКатегорииМаркетплейса = Значение.ИдентификаторКатегорииМаркетплейса;
			СопоставлениеКатегорийСтрока.НаименованиеКатегорииМаркетплейса = Значение.НаименованиеКатегорииМаркетплейса;
			СопоставлениеКатегорийСтрока.Категория = Значение.Категория; // Категория 1С для заполнения номенклатуры
			СопоставлениеКатегорийСтрока.КатегорияИлиНоменклатураДляСопоставленияКатегорииOzon = Значение.КатегорияИлиНоменклатураДляСопоставленияКатегорииOzon;
			
		КонецЦикла;
		
		Если СопоставлениеКатегорий.Количество() > 0 Тогда
			Категории1СПоНаименованиюКатегорииOzonНайтиИЗаполнить();
		КонецЕсли;
		
	КонецЕсли;

	Если ЭтаФорма.ИсточникКатегории = ПредопределенноеЗначение( "Перечисление.ИсточникиКатегорийДляМаркетплейса.ВидНоменклатуры" ) Тогда
		
		Элементы.КнопкаСоздатьГруппыНоменклатурыПоКатегориямПоOzon.Видимость = Ложь;

		Элементы.СопоставлениеКатегорийКатегорияИлиНоменклатураДляСопоставленияКатегорииOzon.Заголовок = "Категория для сопоставления с категорией Ozon";
		Элементы.СопоставлениеКатегорийКатегорияИлиНоменклатураДляСопоставленияКатегорииOzon.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.КатегорииНоменклатуры");
		
	ИначеЕсли ЭтаФорма.ИсточникКатегории = ПредопределенноеЗначение( "Перечисление.ИсточникиКатегорийДляМаркетплейса.ИерархияНоменклатуры" ) Тогда
		
		Элементы.СопоставлениеКатегорийКатегорияИлиНоменклатураДляСопоставленияКатегорииOzon.Заголовок = "Номенклатура или группа для сопоставления с категорией Ozon";
		Элементы.СопоставлениеКатегорийКатегорияИлиНоменклатураДляСопоставленияКатегорииOzon.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
		
	КонецЕсли;
	
	ЭтаФорма.СопоставлениеКатегорий.Сортировать( "НаименованиеКатегорииМаркетплейса" );
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭтаФорма.ПодключитьОбработчикОжидания( "ЗаполнитьДеревоКатегорий", 0.1, Истина );
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКатегорийВСоответсвиеЗаполнитьРекурсивно( ДеревоКатегорий, ДеревоКатегорийСоответсвие )
	
	Для Каждого ДеревоКатегорийЭлемент Из ДеревоКатегорий.ПолучитьЭлементы() Цикл
		
		Если ЗначениеЗаполнено( ДеревоКатегорийЭлемент.ИдентификаторКатегорииМаркетплейса ) Тогда
			ДеревоКатегорийСоответсвие.Вставить( ДеревоКатегорийЭлемент.ИдентификаторКатегорииМаркетплейса, ДеревоКатегорийЭлемент );
		КонецЕсли;
		
		ДеревоКатегорийВСоответсвиеЗаполнитьРекурсивно( ДеревоКатегорийЭлемент, ДеревоКатегорийСоответсвие );
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеКатегорийПолноеНаименованиеКатегорииOzonИИерархиюЗаполнить( ДеревоКатегорий )
	
	ДеревоКатегорийСоответсвие = Новый Соответствие;
	ДеревоКатегорийВСоответсвиеЗаполнитьРекурсивно( ДеревоКатегорий, ДеревоКатегорийСоответсвие );
	
	Для Каждого СопоставлениеКатегорийСтрока Из СопоставлениеКатегорий Цикл
	
		ДеревоКатегорийСтрока = ДеревоКатегорийСоответсвие.Получить( СопоставлениеКатегорийСтрока.ИдентификаторКатегорииМаркетплейса );
		Если ДеревоКатегорийСтрока = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СопоставлениеКатегорийСтрока.КатегорияИерархия.Очистить();

		СопоставлениеКатегорийСтрока.ПолноеНаименованиеКатегорииМаркетплейса = ДеревоКатегорийСтрока.НаименованиеКатегорииМаркетплейса;
		
		Родитель = ДеревоКатегорийСтрока;
		Пока Истина Цикл
			Родитель = Родитель.ПолучитьРодителя();
			Если Родитель = Неопределено Тогда
				Прервать;
			КонецЕсли;
			
			КатегорияИерархияСтрока = СопоставлениеКатегорийСтрока.КатегорияИерархия.Вставить( 0 );
			КатегорияИерархияСтрока.Код = Родитель.ИдентификаторКатегорииМаркетплейса;
			КатегорияИерархияСтрока.Наименование = Родитель.НаименованиеКатегорииМаркетплейса;
			
			СопоставлениеКатегорийСтрока.ПолноеНаименованиеКатегорииМаркетплейса = 
			Родитель.НаименованиеКатегорииМаркетплейса + " - " + 
			СопоставлениеКатегорийСтрока.ПолноеНаименованиеКатегорииМаркетплейса;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция Категории1СПоНаименованиюКатегорииOzonНайтиИЗаполнить()
    
    ПараметрыФункции = Новый Структура;
    ПараметрыФункции.Вставить( "СопоставлениеКатегорий", ЭтаФорма.СопоставлениеКатегорий );
    ПараметрыФункции.Вставить( "ИсточникКатегории", ЭтаФорма.ИсточникКатегории );
    Результат = ИнтеграцияСМаркетплейсомOzonСервер.Категории1СПоНаименованиюКатегорииOzonНайтиИЗаполнить( ПараметрыФункции );
    
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура Категории1СПоКатегориямOzonСоздать( ДеревоКатегорийКэш )
    
	ПараметрыФункции = ИнтеграцияСМаркетплейсомOzonСервер.Категории1СПоКатегориямOzonПараметрыНовый();;
	ПараметрыФункции.СопоставлениеКатегорий = СопоставлениеКатегорий;
	ПараметрыФункции.ИсточникКатегории = ИсточникКатегории;
	ПараметрыФункции.СоздатьИерархию = КатегорииСоздатьИерархию;
	ПараметрыФункции.УчетнаяЗапись = УчетнаяЗаписьМаркетплейса;
	ПараметрыФункции.КэшКатегорийДерево = ДеревоКатегорийКэш;
    
    ИнтеграцияСМаркетплейсомOzonСервер.Категории1СПоКатегориямOzonСоздать( ПараметрыФункции );
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура КнопкаСоздатьКатегорииПоOzon(Команда)
    Перем МенюДляВыбора;
    
	КоличествоСоздать = 0;
	Для Каждого СопоставлениеКатегорийСтрока Из ЭтаФорма.СопоставлениеКатегорий Цикл
		Если ЗначениеЗаполнено( СопоставлениеКатегорийСтрока.Категория ) Тогда
			Продолжить;
		КонецЕсли;
		КоличествоСоздать  = КоличествоСоздать + 1;
	КонецЦикла;
	
	Если КоличествоСоздать = 0 Тогда
		ПоказатьПредупреждение( Новый ОписаниеОповещения, "Отсутствуют строки с незаполненной категорией номенклатуры." );
		Возврат;
	КонецЕсли;
	
	МенюДляВыбора = Новый СписокЗначений;
	МенюДляВыбора.Добавить( Ложь, "без иерархии" );
	МенюДляВыбора.Добавить( Истина, "с иерархией" );
	
	ОтветОбещание = ВыбратьИзМенюАсинх( МенюДляВыбора, Элементы.КнопкаСоздатьКатегорииПоOzon );
    Ответ = Ждать ОтветОбещание;
    Если Ответ = Неопределено Тогда
    	Возврат;
    КонецЕсли;
    
    ЭтаФорма.КатегорииСоздатьИерархию = Ответ.Значение;
    СоздатьИерархиюПредставление = Ответ.Представление;
	
    ВопросТекст = 
    "Категории номенклатуры будут созданы для тех строк, в которых колонка ""Категория 1С"" не заполнена.
    |Будет создано категорий номенклатуры: " + КоличествоСоздать + "
    |Продолжить?";
    ОтветОбещание = ВопросАсинх( ВопросТекст, РежимДиалогаВопрос.ДаНетОтмена, , , 
    "Создание категорий номенклатуры " + Символы.ПС + СоздатьИерархиюПредставление );
    Ответ = Ждать ОтветОбещание;
    Если Ответ <> КодВозвратаДиалога.Да Тогда
    	Возврат;
    КонецЕсли;

	Категории1СПоКатегориямOzonСоздать( ДеревоКатегорийКэш );
	
КонецПроцедуры

&НаСервере
Процедура ГруппыНоменклатурыПоКатегориямOzonСоздать( ДеревоКатегорийКэш )
    
	ПараметрыФункции = ИнтеграцияСМаркетплейсомOzonСервер.Категории1СПоКатегориямOzonПараметрыНовый();;
	ПараметрыФункции.СопоставлениеКатегорий = СопоставлениеКатегорий;
	ПараметрыФункции.ИсточникКатегории = ИсточникКатегории;
	ПараметрыФункции.СоздатьИерархию = НоменклатураСоздатьИерархию;
	ПараметрыФункции.УчетнаяЗапись = УчетнаяЗаписьМаркетплейса;
	ПараметрыФункции.КэшКатегорийДерево = ДеревоКатегорийКэш;
	
	ПараметрыФункции.Вставить( "Родитель", ГруппаДляСозданияНовыхТоваров );
    
    ИнтеграцияСМаркетплейсомOzonСервер.ГруппыНоменклатурыПоКатегориямOzonСоздать( ПараметрыФункции );
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура КнопкаСоздатьГруппыНоменклатурыПоКатегориямOzon(Команда)
    Перем МенюДляВыбора;
    
	КоличествоСоздать = 0;
	Для Каждого СопоставлениеКатегорийСтрока Из ЭтаФорма.СопоставлениеКатегорий Цикл
		Если ЗначениеЗаполнено( СопоставлениеКатегорийСтрока.КатегорияИлиНоменклатураДляСопоставленияКатегорииOzon ) Тогда
			Продолжить;
		КонецЕсли;
		КоличествоСоздать  = КоличествоСоздать + 1;
	КонецЦикла;
	
	Если КоличествоСоздать = 0 Тогда
		ПоказатьПредупреждение( Новый ОписаниеОповещения, "Отсутствуют строки с незаполненной номенклатурой." );
		Возврат;
	КонецЕсли;
	
	МенюДляВыбора = Новый СписокЗначений;
	МенюДляВыбора.Добавить( Ложь, "без иерархии" );
	МенюДляВыбора.Добавить( Истина, "с иерархией" );
	
	ОтветОбещание = ВыбратьИзМенюАсинх( МенюДляВыбора, Элементы.КнопкаСоздатьГруппыНоменклатурыПоКатегориямПоOzon );
    Ответ = Ждать ОтветОбещание;
    Если Ответ = Неопределено Тогда
    	Возврат;
    КонецЕсли;
    
    ЭтаФорма.НоменклатураСоздатьИерархию = Ответ.Значение;
    СоздатьИерархиюПредставление = Ответ.Представление;
	
    ВопросТекст = 
    "Будет создано групп номенклатуры: " + КоличествоСоздать + "
    |Продолжить?";
    ОтветОбещание = ВопросАсинх( ВопросТекст, РежимДиалогаВопрос.ДаНетОтмена, , , 
    "Создание категорий номенклатуры " + Символы.ПС + СоздатьИерархиюПредставление );
    Ответ = Ждать ОтветОбещание;
    Если Ответ <> КодВозвратаДиалога.Да Тогда
    	Возврат;
    КонецЕсли;

	ГруппыНоменклатурыПоКатегориямOzonСоздать( ДеревоКатегорийКэш );
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьСоответствияКатегорийНаСервере()

	Если Не ЗначениеЗаполнено( УчетнаяЗаписьМаркетплейса ) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПараметрыФункции = Новый Структура;
	ПараметрыФункции.Вставить( "УчетнаяЗаписьМаркетплейса", ЭтаФорма.УчетнаяЗаписьМаркетплейса );
	ПараметрыФункции.Вставить( "ИсточникКатегории", ЭтаФорма.ИсточникКатегории );
	ПараметрыФункции.Вставить( "СопоставлениеКатегорий", ЭтаФорма.СопоставлениеКатегорий );
	
	Результат = ИнтеграцияСМаркетплейсомOzonСервер.ЗаписатьСоопоставленияКатегорийOzon( ПараметрыФункции );
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура КнопкаЗаписать(Команда)
	Выполнено = ЗаписатьСоответствияКатегорийНаСервере();
	Если Выполнено = Истина Тогда
		Оповестить( "OzonЗаписаныСопоставленияКатегорий", ЭтаФорма.СопоставлениеКатегорий );
	КонецЕсли;
	ПоказатьОповещениеПользователя( "Сопоставления категорий записаны", , , БиблиотекаКартинок.ЛоготипOzon2 );
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДеревоКатегорий()

	ИдентификаторКэшаКатегорий = "ДеревоКатегорийДерево";
	ДанныеКэша = ИнтеграцияСМаркетплейсомOzonКлиент.ПолучитьДанныеИзКэшаКатегорий( ИдентификаторКэшаКатегорий );
	Если ТипЗнч( ДанныеКэша ) = Тип( "ДанныеФормыДерево" ) Тогда

		ДеревоКатегорийКэш = ДанныеКэша;
		
		СопоставлениеКатегорийПолноеНаименованиеКатегорииOzonИИерархиюЗаполнить( ДеревоКатегорийКэш );
		
		Возврат;
	КонецЕсли;
    
	ДлительнаяОперация = ПолучитьКатегорииМаркетплейсаНаСервере( ДанныеКэша );

	ОповещениеОЗавершении = Новый ОписаниеОповещения( "ЗаполнитьДеревоКатегорийМаркетплейсаЗавершение", ЭтотОбъект );
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания( ЭтотОбъект );
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ТекстСообщения = "Получение списка категорий из Ozon.";
	ДлительныеОперацииКлиент.ОжидатьЗавершение( ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания );

КонецПроцедуры

&НаСервере
Функция ПолучитьКатегорииМаркетплейсаНаСервере(ДанныеКэша)

	ИмяМетода = "ИнтеграцияСМаркетплейсомOzonСервер.ЗаполнитьДеревоКатегорийИТиповТоваров";
	НаименованиеФоновогоЗадания = НСтр("ru = 'Ozon. Получение категорий маркетплейса'");

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне( ЭтаФорма.УникальныйИдентификатор );
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания;

	ДеревоКатегорийМаркетплейсаФормы = РеквизитФормыВЗначение( "ДеревоКатегорийМаркетплейса" );
	ДеревоКатегорийМаркетплейсаФормы.Строки.Очистить();  

	Возврат ДлительныеОперации.ВыполнитьФункцию( ПараметрыВыполнения, ИмяМетода,
			УчетнаяЗаписьМаркетплейса, ДеревоКатегорийМаркетплейсаФормы, ДанныеКэша );

КонецФункции

&НаКлиенте
Процедура ЗаполнитьДеревоКатегорийМаркетплейсаЗавершение( Результат, ДополнительныеПараметры ) Экспорт

	ОчиститьСообщения();

	Если Результат <> Неопределено И Результат.Статус = "Выполнено" Тогда
	
		ДанныеКэша = Неопределено;
		ЗаполнитьДеревоКатегорийМаркетплейсаНаСервере( Результат.АдресРезультата, ДанныеКэша );

		Если ЗначениеЗаполнено( ДанныеКэша ) Тогда
		
			ДеревоКатегорийКэш = ЭтаФорма.ДеревоКатегорийМаркетплейса;
			ИнтеграцияСМаркетплейсомOzonКлиент.СохранитьДанныеВКэшКатегорий( ДеревоКатегорийМаркетплейса, ИдентификаторКэшаКатегорий );
			ДанныеКэша = Неопределено;
			
			Если ТипЗнч( ДеревоКатегорийКэш ) = Тип( "ДанныеФормыДерево" ) Тогда
				СопоставлениеКатегорийПолноеНаименованиеКатегорииOzonИИерархиюЗаполнить( ДеревоКатегорийКэш );
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоКатегорийМаркетплейсаНаСервере( АдресХранилища, ДанныеКэша )

	Результат = ПолучитьИзВременногоХранилища( АдресХранилища );
	УдалитьИзВременногоХранилища(АдресХранилища);

	ДеревоКатегорий = Результат.ДеревоКатегорий;
	ДанныеКэша = Результат.ДанныеКэша;

	Если ДеревоКатегорий = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ЗначениеВРеквизитФормы( ДеревоКатегорий, "ДеревоКатегорийМаркетплейса");

КонецПроцедуры

