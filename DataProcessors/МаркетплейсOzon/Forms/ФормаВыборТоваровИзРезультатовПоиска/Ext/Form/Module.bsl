&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЭтаФорма.Параметры.Свойство( "ЗаголовокФормы" ) = Истина Тогда
		ЭтаФорма.АвтоЗаголовок = Ложь;
		ЭтаФорма.Заголовок = Параметры.ЗаголовокФормы;
	КонецЕсли;
	
	Если ЭтаФорма.Параметры.Свойство( "ИспользоватьПометки" ) = Истина Тогда
		ЭтаФорма.ИспользоватьПометки = Параметры.ИспользоватьПометки;
	КонецЕсли;
	ЭтаФорма.Элементы.ТоварыПометка.Видимость = ЭтаФорма.ИспользоватьПометки;
	
	Если ЭтаФорма.Параметры.Свойство( "Товары" ) = Истина Тогда
		
		Товары.Очистить();
		
		ЕстьХарактеристики = Ложь;
		Для Каждого ПараметрТоварыСтрока Из Параметры.Товары Цикл
			ТоварыСтрока = Товары.Добавить();
			ТоварыСтрока.Номенклатура = ПараметрТоварыСтрока.Номенклатура;
			ТоварыСтрока.Характеристика = ПараметрТоварыСтрока.Характеристика;
			ТоварыСтрока.МетодПоиска = ПараметрТоварыСтрока.МетодПоиска;
			ТоварыСтрока.ВероятностьСовпадения = ПараметрТоварыСтрока.ВероятностьСовпадения;
			ТоварыСтрока.ВесПоиска = ПараметрТоварыСтрока.ВесПоиска;
			ТоварыСтрока.Описание = ПараметрТоварыСтрока.Описание;
			
			Если ЗначениеЗаполнено( ТоварыСтрока.Характеристика ) Тогда
				ЕстьХарактеристики = Истина;
			КонецЕсли;
		КонецЦикла;
		ЭтаФорма.Элементы.ТоварыХарактеристика.Видимость = ЕстьХарактеристики;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВыбор()

	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ЭтаФорма.ИспользоватьПометки Тогда
		РезультатВыбора = ЭтаФорма.Товары.НайтиСтроки( Новый Структура( "Пометка", Истина ) );
	Иначе
		РезультатВыбора = Новый Структура;
		РезультатВыбора.Вставить( "Номенклатура", ТекущиеДанные.Номенклатура );
		РезультатВыбора.Вставить( "Характеристика", ТекущиеДанные.Характеристика );
		РезультатВыбора.Вставить( "ВероятностьСовпадения", ТекущиеДанные.ВероятностьСовпадения );
		РезультатВыбора.Вставить( "ВесПоиска", ТекущиеДанные.ВесПоиска );
		РезультатВыбора.Вставить( "МетодПоиска", ТекущиеДанные.МетодПоиска );
		РезультатВыбора.Вставить( "Описание", ТекущиеДанные.Описание );
	КонецЕсли;
	
	ЭтаФорма.Закрыть( РезультатВыбора );
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаВыбрать(Команда)
	ЗавершитьВыбор();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ЗавершитьВыбор();
КонецПроцедуры

&НаСервере
Процедура ТоварыПриАктивизацииСтрокиКартинкаАдресПолучитьНаСервере()

	ИдентификаторСтроки = Элементы.Товары.ТекущаяСтрока;
	Если ИдентификаторСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = ЭтаФорма.Товары.НайтиПоИдентификатору( ИдентификаторСтроки );
	
	Если ЗначениеЗаполнено( ТекущиеДанные.Характеристика ) Тогда
		ФайлКартинки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта( ТекущиеДанные.Характеристика, "ФайлКартинки" );
		Если НЕ ЗначениеЗаполнено( ФайлКартинки ) Тогда
			ФайлКартинки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта( ТекущиеДанные.Номенклатура, "ФайлКартинки" );
		КонецЕсли;
	Иначе
		ФайлКартинки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта( ТекущиеДанные.Номенклатура, "ФайлКартинки" );
	КонецЕсли;
	
	Если ЗначениеЗаполнено( ФайлКартинки ) Тогда
		ТекущиеДанные.КартинкаАдрес = ИнтеграцияСМаркетплейсамиСервер.ПрисоединенныйФайлВоВременноеХранилище( ФайлКартинки, ЭтаФорма.УникальныйИдентификатор );
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриАктивизацииСтрокиКартинкаАдресПолучить()
	ТоварыПриАктивизацииСтрокиКартинкаАдресПолучитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока( ТекущиеДанные.КартинкаАдрес ) Тогда
		ЭтаФорма.ПодключитьОбработчикОжидания( "ТоварыПриАктивизацииСтрокиКартинкаАдресПолучить", 0.1, Истина );
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаОткрыть(Команда)

	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТоварыТекущаяКолонка = Элементы.Товары.ТекущийЭлемент;
	Если ТоварыТекущаяКолонка.Имя = "ТоварыНоменклатура" Тогда
		Ссылка = ТекущиеДанные.Номенклатура;
	ИначеЕсли ТоварыТекущаяКолонка.Имя = "ТоварыХарактеристика" Тогда
		Ссылка = ТекущиеДанные.Характеристика;
	Иначе
		Если ЗначениеЗаполнено( ТекущиеДанные.Характеристика ) Тогда
			Ссылка = ТекущиеДанные.Характеристика;
		Иначе
			Ссылка = ТекущиеДанные.Номенклатура;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено( Ссылка ) Тогда
		ПоказатьЗначение( , Ссылка );
	КонецЕсли;
	
КонецПроцедуры

