
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтправлятьСлепыеКопииПисем = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПараметрыОтправкиСкрытыхКопий", "ОтправлятьСлепыеКопииПисем", Ложь);
	АдресПолучателяСкрытойКопии = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПараметрыОтправкиСкрытыхКопий", "АдресПолучателяСкрытойКопии", "");
	ИмяОтправителяSMS = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиSMS", "ИмяОтправителяSMS", "");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтправлятьСлепыеКопииПисемПриИзменении(Элемент)
	
	СохранитьНастройкуОтправлятьСлепыеКопииПисем();
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресПолучателяСкрытойКопииПриИзменении(Элемент)
	
	ТекстОшибки = СобытияУНФКлиентСервер.ПроверитьКорректностьАдресаЭлектроннойПочты(АдресПолучателяСкрытойКопии);
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , Элементы.АдресПолучателяСкрытойКопии.Имя);
		Возврат;
	КонецЕсли;
	
	СохранитьНастройкуАдресПолучателяСкрытойКопии();
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяОтправителяSMSПриИзменении(Элемент)
	
	СохранитьНастройкуИмяОтправителя();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастройкаПодписиПисем(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ЭтоФормаСписка", Истина);
	ОткрытьФорму("Справочник.ПодписиПисем.Форма.ФормаСписка", ПараметрыОткрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНастройкуИмяОтправителя()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НастройкиSMS", "ИмяОтправителяSMS", ИмяОтправителяSMS);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкуОтправлятьСлепыеКопииПисем()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ПараметрыОтправкиСкрытыхКопий",
		"ОтправлятьСлепыеКопииПисем",
		ОтправлятьСлепыеКопииПисем);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкуАдресПолучателяСкрытойКопии()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ПараметрыОтправкиСкрытыхКопий",
		"АдресПолучателяСкрытойКопии",
		АдресПолучателяСкрытойКопии);
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормой()
	
	Элементы.АдресПолучателяСкрытойКопии.Доступность = ОтправлятьСлепыеКопииПисем;
	
КонецПроцедуры

#КонецОбласти
