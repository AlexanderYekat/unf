
#Область ОписаниеПеременных

&НаКлиенте
Перем ИзмененПризнакВРаботе;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ИсходныйДокументОтбор = Параметры.ИсходныйДокумент;
	ОтображатьОдинДокумент = Параметры.ОтображатьОдинДокумент;
	
	Задача = ЗадачаПоРегулярнымПлатежам();
	ПустойСправочникЗадач = Задача.ПараметрыРегулярныхПлатежей.Количество() = 0;
	РазрешеноИзменятьЗадачи = Обработки.АссистентУправления.РазрешеноИзменятьЗадачи() Или ОтображатьОдинДокумент;
	
	Элементы.ДобавитьДействие.Видимость = РазрешеноИзменятьЗадачи;
	Элементы.ДействияАссистента.ТолькоПросмотр = НЕ РазрешеноИзменятьЗадачи;
	Элементы.ВРаботе.ТолькоПросмотр = НЕ РазрешеноИзменятьЗадачи;
	ЭтотОбъект.ТолькоПросмотр = НЕ РазрешеноИзменятьЗадачи;
	
	ЗаполнитьСпособыОповещения();
	ОбновитьБлокиНастроекАссистента();
	ОбновитьЭлементыДействийАссистента();
	ЗаполнитьСписокВыбораЭлементовФормы();
	
	ИнформационнаяБазаЗарегистрирована = СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована();
	КлючСохраненияПоложенияОкна = Новый УникальныйИдентификатор();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ИзмененПризнакВРаботе = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ИзмененныеЗадачи = НастройкиЗадач.НайтиСтроки(Новый Структура("Модифицированность", Истина));
	ЗадачиИзменены = ИзмененныеЗадачи.Количество() <> 0;
	
	Если НЕ ЗадачиИзменены Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ РазрешеноИзменятьЗадачи Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ВРаботе Тогда
		
		СоздатьИзменитьЗадачиАссистента();
		ЗакрытьФормуЗадач();
		Возврат;
		
	КонецЕсли;
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
		
	СоздатьИзменитьЗадачиАссистента();
	ЗакрытьФормуЗадач();

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ЕстьОшибки = Ложь;
	Для Каждого СтрокаНастроек Из НастройкиЗадач Цикл
		
		Если СтрокаНастроек.Удалена Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.ТипОповещения) Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если СтрокаНастроек.ТипОповещения = "Обсуждение" 
			И НЕ ЗначениеЗаполнено(СтрокаНастроек.ТипОбсуждения) Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если СтрокаНастроек.ТипОповещения = "Обсуждение" И СтрокаНастроек.ТипОбсуждения <> "Групповое"
			И НЕ ЗначениеЗаполнено(СтрокаНастроек.СпособОповещения) Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если СтрокаНастроек.ТипОповещения <> "Обсуждение" 
			И НЕ ЗначениеЗаполнено(СтрокаНастроек.ПользовательДляПисем) Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если СтрокаНастроек.ТипОповещения = "Обсуждение" И СтрокаНастроек.ТипОбсуждения = "Групповое"
			И НЕ ЗначениеЗаполнено(СтрокаНастроек.Обсуждение) Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если СтрокаНастроек.ТипОповещения = "Email" 
			И НЕ ЗначениеЗаполнено(СтрокаНастроек.УчетнаяЗапись) Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.ИсходныйДокумент) Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.НачалоВыполнения) Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.ОкончаниеВыполнения) Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаНастроек.Периодичность) Тогда
			ЕстьОшибки = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьОшибки Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не заполнены обязательные данные'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область ОбработчикиЭлементовНаФорме

&НаКлиенте
Процедура Декорация_0УдалитьНажатие(Элемент)
	УдалитьБлокНастроек(Элемент.Имя);
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ВРаботеПриИзменении(Элемент)
	
	ИзмененПризнакВРаботе = Истина;
	УстановитьМодифицированностьЗадач();
	
	Если НЕ ВРаботе Тогда
		Возврат;
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстОшибкиОбсуждениеНажатие(Элемент)
	НачатьПодключениеОбсуждений();
КонецПроцедуры

&НаКлиенте
Процедура ВыполняетсяПоОдномуДокументуПриИзменении(Элемент)
	СтрокаНастроек = НастройкиЗадач.Получить(0);
	СтрокаНастроек.Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьДействие(Команда)
	
	ДобавитьДействиеСервер();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПодключаемыеОбработчикиСобытий

&НаКлиенте
Процедура Подключаемый_УдалитьБлокНастроек(Элемент)
	УдалитьБлокНастроек(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПользовательДляПисемПриИзменении(Элемент)
	УстановитьМодифицированностьПриИзмененииПоля(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ТипОбсужденияПриИзменении(Элемент)
	УстановитьМодифицированностьПриИзмененииПоля(Элемент.Имя);
	Индекс = ИндексБлока(Элемент.Имя);
	ТипОповещенияИзменениеНаСервере(Индекс);
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СпособОповещенияПриИзменении(Элемент)
	
	УстановитьМодифицированностьПриИзмененииПоля(Элемент.Имя);
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбсуждениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Индекс = ИндексБлока(Элемент.Имя);
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыборОбсуждения", ЭтотОбъект,Индекс);
	ОткрытьФорму("Обработка.АссистентУправления.Форма.ВыборОбсуждения",,,,,, ОповещениеОЗакрытии);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбсуждениеПриИзменении(Элемент)
	УстановитьМодифицированностьПриИзмененииПоля(Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СпособОповещенияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Индекс = ИндексБлока(Элемент.Имя);
	ЗаполнитьДанныеВыбора(ДанныеВыбора, Индекс);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СпособОповещенияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	УстановитьМодифицированностьПриИзмененииПоля(Элемент.Имя);
	Индекс = ИндексБлока(Элемент.Имя);
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Пользователи") Тогда
		
		Элементы["СпособОповещения_" + Индекс].ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
		НастройкиЗадач[Число(Индекс)].СпособОповещения = ВыбранноеЗначение;
		НастройкиЗадач[Число(Индекс)].ПользовательДляОповещения = ВыбранноеЗначение;
		
		Возврат;
		
	КонецЕсли;
	
	НастройкиЗадач[Число(Индекс)].ПользовательДляОповещения = Неопределено;
	ЗначениеСпособаОповещения = СпособыОповещения.НайтиСтроки(Новый Структура("Представление", ВыбранноеЗначение));

	Если ЗначениеСпособаОповещения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеСпособаОповещения[0].Значение <> ПредопределенноеЗначение("Перечисление.СпособОповещенияАссистентаУправления.СообщениеКонтекстногоОбсужденияПользователю")
		И ЗначениеСпособаОповещения[0].Значение <> ПредопределенноеЗначение("Перечисление.СпособОповещенияАссистентаУправления.СообщениеЛичногоОбсужденияПользователю") Тогда
	
		Элементы["СпособОповещения_" + Индекс].ОграничениеТипа = Новый ОписаниеТипов("Строка");
		НастройкиЗадач[Число(Индекс)].СпособОповещения = ВыбранноеЗначение;
		НастройкиЗадач[Число(Индекс)].ПользовательДляОповещения = Неопределено;
		Возврат;
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Индекс", Индекс);
	
	Оповещение = Новый ОписаниеОповещения("ВыборПользователя", ЭтотОбъект, ДополнительныеПараметры);
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора",Истина);
	ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыФормы,,,,, Оповещение,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СпособОповещенияАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
	Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Индекс = ИндексБлока(Элемент.Имя);
	ЗаполнитьДанныеВыбора(ДанныеВыбора, Индекс);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ТипОповещенияПриИзменении(Элемент)
	
	УстановитьМодифицированностьПриИзмененииПоля(Элемент.Имя);
	Индекс = ИндексБлока(Элемент.Имя);
	ТипОповещенияИзменениеНаСервере(Индекс);
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УчетнаяЗаписьПриИзменении(Элемент)
	УстановитьМодифицированностьПриИзмененииПоля(Элемент.Имя);
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура УдалитьБлокНастроек(ИмяЭлемента)
	
	Индекс = ИндексБлока(ИмяЭлемента);
	СтрокаНастроек = НастройкиЗадач.Получить(Индекс);
	СтрокаНастроек.Модифицированность = Истина;
	СтрокаНастроек.Удалена = Истина;
	
	ОбновитьЭлементыДействийИЗаполнитьСписокВыбора();

КонецПроцедуры

&НаСервере
Процедура ТипОповещенияИзменениеНаСервере(Индекс)
	
	СтрокаНастроек = НастройкиЗадач.Получить(Индекс);
	Если СтрокаНастроек.ТипОповещения <> "Обсуждение" Тогда
		СтрокаНастроек.Обсуждение = "";
		СтрокаНастроек.ОбсуждениеИдентификатор = "";
		СтрокаНастроек.СпособОповещения = Перечисления.СпособОповещенияАссистентаУправления.ПустаяСсылка();
		Возврат;
	КонецЕсли;
	
	СтрокаНастроек.Обсуждение = "";
	СтрокаНастроек.ОбсуждениеИдентификатор = "";
	СтрокаНастроек.УчетнаяЗапись = Справочники.УчетныеЗаписиЭлектроннойПочты.ПустаяСсылка();
	
	Личное = Перечисления.СпособОповещенияАссистентаУправления.СообщениеЛичногоОбсужденияПользователю;
	Общее = Перечисления.СпособОповещенияАссистентаУправления.СообщениеОбщегоОбсуждения;
	Если СтрокаНастроек.ТипОповещения = "Личное" Тогда
		ЗаполнитьСпособОповещенияПоЗначению(СтрокаНастроек, Справочники.Пользователи.ПустаяСсылка(), Личное);
	Иначе
		ЗаполнитьСпособОповещенияПоЗначению(СтрокаНастроек, Справочники.Пользователи.ПустаяСсылка(), Общее);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПользователя(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ ДополнительныеПараметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Индекс = ДополнительныеПараметры.Индекс;
	Элементы["СпособОповещения_" + Индекс].ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
	НастройкиЗадач[Число(Индекс)].СпособОповещения = Результат;
	НастройкиЗадач[Число(Индекс)].ПользовательДляОповещения = Результат;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеВыбора(ДанныеВыбора, Индекс)
	
	Если ДанныеВыбора = Неопределено Тогда
		ДанныеВыбора = Новый СписокЗначений;
	КонецЕсли;

	ОповеститьПользователя = Перечисления.СпособОповещенияАссистентаУправления.СообщениеКонтекстногоОбсужденияПользователю;
	
	Индекс = Число(Индекс);
	ДобавитьПунктОповеститьТекущего(Индекс, ДанныеВыбора, ОповеститьПользователя);
	ДобавитьПунктОповеститьВыбранного(Индекс, ДанныеВыбора);
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьПунктОповеститьТекущего(Индекс, ДанныеВыбора, ОповеститьПользователя)
	
	Если НастройкиЗадач[Индекс].ТипОповещения = "Обсуждение" Тогда
		ДанныеВыбора.Добавить(Пользователи.АвторизованныйПользователь(),,, БиблиотекаКартинок.СпособОповещенияОповеститьВыбранного);
	Иначе
		ДанныеВыбора.Добавить(Пользователи.АвторизованныйПользователь());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПунктОповеститьВыбранного(Индекс, ДанныеВыбора)
	
	Если НастройкиЗадач[Индекс].ТипОповещения = "Обсуждение" И НастройкиЗадач[Индекс].ТипОбсуждения = "Личное" Тогда
		ОповеститьПользователя = Перечисления.СпособОповещенияАссистентаУправления.СообщениеЛичногоОбсужденияПользователю;
		СпособОповестиПользователя = СпособыОповещения.НайтиСтроки(Новый Структура("Значение", ОповеститьПользователя));
		Если СпособОповестиПользователя.Количество() > 0 Тогда
			ДанныеВыбора.Добавить(СпособОповестиПользователя[0].Представление,,, БиблиотекаКартинок.СпособОповещенияОповеститьВыбранного);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ОповеститьПользователя = Перечисления.СпособОповещенияАссистентаУправления.СообщениеЛичногоОбсужденияПользователю;
	СпособОповестиПользователя = СпособыОповещения.НайтиСтроки(Новый Структура("Значение", ОповеститьПользователя));
	Если СпособОповестиПользователя.Количество() > 0 Тогда
		ДанныеВыбора.Добавить(СпособОповестиПользователя[0].Представление);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДействийИЗаполнитьСписокВыбора()
	ОбновитьЭлементыДействийАссистента();
	ЗаполнитьСписокВыбораЭлементовФормы();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИндексБлока(ЭлементИмя)
	
	Возврат Прав(ЭлементИмя, СтрДлина(ЭлементИмя) - СтрНайти(ЭлементИмя, "_"));
	
КонецФункции

&НаСервере
Процедура ДобавитьДействиеСервер()
	
	ДобавитьПустыеНастройкиЗадачи();
	ОбновитьЭлементыДействийАссистента();
	ЗаполнитьСписокВыбораЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПодключениеОбсуждений()
	
	Продолжение = Новый ОписаниеОповещения("ЗавершитьПодключениеОбсуждений", ЭтотОбъект);
	ОбсужденияКлиент.ПоказатьПодключение(Продолжение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПодключениеОбсуждений(Результат, ДополнительныеПараметры) Экспорт
	
	УправлениеФормой();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьМодифицированностьЗадач()
	
	Для Каждого СтрокаНастроек Из НастройкиЗадач Цикл
		СтрокаНастроек.Модифицированность = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ИсходныйДокументПриИзменении(Элемент)
	
	УстановитьМодифицированностьПриИзмененииПоля(Элемент.Имя);
	Индекс = ИндексБлока(Элемент.Имя);
	СтрокаНастроек = НастройкиЗадач.Получить(Индекс);
			
	Если ЗначениеЗаполнено(СтрокаНастроек.ИсходныйДокумент) Тогда
		
		Для Каждого СтрокаВсехНастроек Из НастройкиЗадач Цикл
			Если СтрокаНастроек.ИсходныйДокумент = СтрокаВсехНастроек.ИсходныйДокумент
				И СтрокаНастроек <> СтрокаВсехНастроек Тогда
				СтрокаНастроек.ИсходныйДокумент = Неопределено;
				Шаблон = НСтр("ru = 'Документ %1 уже есть в списке'");
				ТекстСообщения = СтрШаблон(Шаблон, СтрокаВсехНастроек.ИсходныйДокумент);
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = ТекстСообщения;
				Сообщение.ПутьКДанным = "Объект";
				Сообщение.Сообщить();
				Возврат;
			КонецЕсли;
		КонецЦикла;
		
		КалендарныеДанные = КалендарныеДанные(СтрокаНастроек.ИсходныйДокумент);
		ЗаполнитьЗначенияСвойств(СтрокаНастроек, КалендарныеДанные);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачалоВыполненияПриИзменении(Элемент)
	
	УстановитьМодифицированностьПриИзмененииПоля(Элемент.Имя);
	Индекс = ИндексБлока(Элемент.Имя);
	СтрокаНастроек = НастройкиЗадач.Получить(Индекс);
	
	МинимальнаяДатаНачала = КонецДня(ТекущаяДатаСеансаСервер()) + 1;
	Если СтрокаНастроек.НачалоВыполнения < МинимальнаяДатаНачала Тогда
		СтрокаНастроек.НачалоВыполнения = МинимальнаяДатаНачала;
		ТекстСообщения = НСтр("ru = 'Дата начала не может быть ранее следующего дня'");
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Поле = Элемент.Имя;
		Сообщение.Сообщить();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОкончаниеВыполненияПриИзменении(Элемент)
	
	УстановитьМодифицированностьПриИзмененииПоля(Элемент.Имя);
	Индекс = ИндексБлока(Элемент.Имя);
	СтрокаНастроек = НастройкиЗадач.Получить(Индекс);
	
	МаксимальнаяДатаОкончания = ДобавитьМесяц(ТекущаяДатаСеансаСервер(), 60);
	
	Если СтрокаНастроек.ОкончаниеВыполнения > МаксимальнаяДатаОкончания Тогда
		СтрокаНастроек.ОкончаниеВыполнения = МаксимальнаяДатаОкончания;
		ТекстСообщения = НСтр("ru = 'Дата окончания не может быть позднее 5 лет от текущей даты'");
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Поле = Элемент.Имя;
		Сообщение.Сообщить();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПериодичностьПриИзменении(Элемент)
	
	УстановитьМодифицированностьПриИзмененииПоля(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеАвтоматическиСоздаватьРегулярныйПлатежПриИзменении(Элемент)
	
	УстановитьМодифицированностьПриИзмененииПоля(Элемент.Имя);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекущаяДатаСеансаСервер()
	
	Возврат ТекущаяДатаСеанса();	
	
КонецФункции

#КонецОбласти

#Область УправлениеФормой

&НаСервере
Процедура ЗаполнитьСписокВыбораЭлементовФормы()

	Для Итератор = 0 По НастройкиЗадач.Количество() - 1 Цикл
		
		Если НастройкиЗадач[Итератор].Удалена Тогда
			Продолжить;
		КонецЕсли;
		
		Элементы["ТипОповещения_" + Итератор].СписокВыбора.Очистить();
		Элементы["ТипОповещения_" + Итератор].СписокВыбора.Добавить("Обсуждение", НСтр("ru='в обсуждение'"));
		Элементы["ТипОповещения_" + Итератор].СписокВыбора.Добавить("Email", НСтр("ru='на почту'"));
		
		Элементы["ТипОбсуждения_" + Итератор].СписокВыбора.Очистить();
		Элементы["ТипОбсуждения_" + Итератор].СписокВыбора.Добавить("Личное", НСтр("ru='личное с пользователем'"));
		Элементы["ТипОбсуждения_" + Итератор].СписокВыбора.Добавить("Групповое", НСтр("ru='групповое с несколькими пользователями'"));
						
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДействийАссистента()
	
	Элементы.Переместить(Элементы.ДобавитьДействие, Элементы.Действие_0);
	УдаляемыеЭлементы = Новый Массив;
	// Группа первой задачи создана в конфигураторе
	Для Каждого ГруппаДействия Из Элементы.ДействияАссистента.ПодчиненныеЭлементы Цикл
		Индекс = ИндексБлока(ГруппаДействия.Имя);
		Если Число(Индекс) = 0 Тогда
			Продолжить;
		КонецЕсли;
		УдаляемыеЭлементы.Добавить(ГруппаДействия);
	КонецЦикла;

	Для Каждого УдаляемыйЭлемент Из УдаляемыеЭлементы Цикл
		Элементы.Удалить(УдаляемыйЭлемент);
	КонецЦикла;
	
	Для Каждого СтрокаНастроек Из НастройкиЗадач Цикл
		
		ИндексНастройки = НастройкиЗадач.Индекс(СтрокаНастроек);
		
		Если НастройкиЗадач[ИндексНастройки].Удалена Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИндексНастройки > 0 Тогда
			
			Если ОтображатьОдинДокумент И СтрокаНастроек.ИсходныйДокумент <> ИсходныйДокументОтбор Тогда
				Продолжить;
			КонецЕсли;
			
			ОбщаяГруппаДействия = Элементы.Добавить("ГруппаДействие_" + ИндексНастройки, Тип("ГруппаФормы"), Элементы.ДействияАссистента);
			УстановитьОформлениеГруппы(ОбщаяГруппаДействия, Элементы.ГруппаДействие_0);
			
			ГруппаДействия = Элементы.Добавить("Действие_" + ИндексНастройки, Тип("ГруппаФормы"), ОбщаяГруппаДействия);
			УстановитьОформлениеГруппы(ГруппаДействия, Элементы.Действие_0);
			
			ГруппаСтроки = Элементы.Добавить("Строки_" + ИндексНастройки, Тип("ГруппаФормы"), ГруппаДействия);
			УстановитьОформлениеГруппы(ГруппаСтроки, Элементы.Строки_0);
			
			ГруппаПервойСтрокиДействия = Элементы.Добавить("ГруппаДействие_" + ИндексНастройки + "_Строка_1", Тип("ГруппаФормы"), ГруппаСтроки);
			УстановитьОформлениеГруппы(ГруппаПервойСтрокиДействия, Элементы.ГруппаДействие_0_Строка_1);		
			
			ПолеТипОповещения = Элементы.Добавить("ТипОповещения_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаПервойСтрокиДействия);
			ПолеТипОповещения.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].ТипОповещения";
			УстановитьОформлениеПоля(ПолеТипОповещения, Элементы.ТипОповещения_0);
			ПолеТипОповещения.УстановитьДействие("ПриИзменении", "Подключаемый_ТипОповещенияПриИзменении");
			
			ПолеТипОбсуждения = Элементы.Добавить("ТипОбсуждения_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаПервойСтрокиДействия);
			ПолеТипОбсуждения.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].ТипОбсуждения";
			УстановитьОформлениеПоля(ПолеТипОбсуждения, Элементы.ТипОбсуждения_0);
			ПолеТипОбсуждения.УстановитьДействие("ПриИзменении", "Подключаемый_ТипОбсужденияПриИзменении");
			
			ПолеСпособОповещения = Элементы.Добавить("СпособОповещения_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаПервойСтрокиДействия);
			ПолеСпособОповещения.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].СпособОповещения";
			УстановитьОформлениеПоля(ПолеСпособОповещения, Элементы.СпособОповещения_0);
			ПолеСпособОповещения.УстановитьДействие("ПриИзменении", "Подключаемый_СпособОповещенияПриИзменении");
			ПолеСпособОповещения.УстановитьДействие("АвтоПодбор", "Подключаемый_СпособОповещенияАвтоПодбор");
			ПолеСпособОповещения.УстановитьДействие("НачалоВыбора", "Подключаемый_СпособОповещенияНачалоВыбора");
			ПолеСпособОповещения.УстановитьДействие("ОбработкаВыбора", "Подключаемый_СпособОповещенияОбработкаВыбора");
			
			ПолеПользовательДляПисем = Элементы.Добавить("ПользовательДляПисем_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаПервойСтрокиДействия);
			ПолеПользовательДляПисем.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].ПользовательДляПисем";
			УстановитьОформлениеПоля(ПолеПользовательДляПисем, Элементы.ПользовательДляПисем_0);
			ПолеПользовательДляПисем.УстановитьДействие("ПриИзменении", "Подключаемый_ПользовательДляПисемПриИзменении");
			
			ПолеОбсуждение = Элементы.Добавить("Обсуждение_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаПервойСтрокиДействия);
			ПолеОбсуждение.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].Обсуждение";
			УстановитьОформлениеПоля(ПолеОбсуждение, Элементы.Обсуждение_0);
			ПолеОбсуждение.УстановитьДействие("ПриИзменении", "Подключаемый_ОбсуждениеПриИзменении");
			ПолеОбсуждение.УстановитьДействие("НачалоВыбора", "Подключаемый_ОбсуждениеНачалоВыбора");
			
			ПолеУчетнаяЗапись = Элементы.Добавить("УчетнаяЗапись_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаПервойСтрокиДействия);
			ПолеУчетнаяЗапись.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].УчетнаяЗапись";
			УстановитьОформлениеПоля(ПолеУчетнаяЗапись, Элементы.УчетнаяЗапись_0);
			ПолеУчетнаяЗапись.УстановитьДействие("ПриИзменении", "Подключаемый_УчетнаяЗаписьПриИзменении");
			
			ДекорацияУдалить = Элементы.Добавить("ДекорацияУдалить_" + ИндексНастройки, Тип("ДекорацияФормы"), ОбщаяГруппаДействия);
			ДекорацияУдалить.Вид = Элементы.ДекорацияУдалить_0.Вид;
			ДекорацияУдалить.Заголовок = Элементы.ДекорацияУдалить_0.Заголовок;
			ДекорацияУдалить.Ширина = Элементы.ДекорацияУдалить_0.Ширина;
			ДекорацияУдалить.Высота = Элементы.ДекорацияУдалить_0.Высота;
			ДекорацияУдалить.Картинка = Элементы.ДекорацияУдалить_0.Картинка;
			ДекорацияУдалить.РазмерКартинки = Элементы.ДекорацияУдалить_0.РазмерКартинки;
			ДекорацияУдалить.Гиперссылка = Элементы.ДекорацияУдалить_0.Гиперссылка;
			ДекорацияУдалить.УстановитьДействие("Нажатие", "Подключаемый_УдалитьБлокНастроек");
			
			ГруппаВторойСтрокиДействия = Элементы.Добавить("ГруппаДействие_" + ИндексНастройки + "_Строка_2",
				Тип("ГруппаФормы"), ГруппаСтроки);
			УстановитьОформлениеГруппы(ГруппаВторойСтрокиДействия, Элементы.ГруппаДействие_0_Строка_2);
			Элементы.Переместить(Элементы.ДобавитьДействие, Элементы["Действие_" + ИндексНастройки]);
			
			ГруппаИсходныйДокумент = Элементы.Добавить("ГруппаИсходныйДокумент_" + ИндексНастройки, Тип("ГруппаФормы"),
				ГруппаВторойСтрокиДействия);
			УстановитьОформлениеГруппы(ГруппаИсходныйДокумент, Элементы.ГруппаИсходныйДокумет_0);
			
			ДекорацияРазделитель_1 = Элементы.Добавить("Разделитель_1_" + ИндексНастройки, Тип("ДекорацияФормы"),
				ГруппаИсходныйДокумент);
			ДекорацияРазделитель_1.Заголовок = Элементы.Разделитель_1_0.Заголовок;
			
			ПолеИсходныйДокумент = Элементы.Добавить("ИсходныйДокумент_" + ИндексНастройки, Тип("ПолеФормы"),
				ГруппаИсходныйДокумент);
			ПолеИсходныйДокумент.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].ИсходныйДокумент";
			УстановитьОформлениеПоля(ПолеИсходныйДокумент, Элементы.ИсходныйДокумент_0);
			ПолеИсходныйДокумент.УстановитьДействие("ПриИзменении", "Подключаемый_ИсходныйДокументПриИзменении");
			
			ГруппаРасписание = Элементы.Добавить("ГруппаРасписание_" + ИндексНастройки, Тип("ГруппаФормы"),
				ГруппаВторойСтрокиДействия);
			УстановитьОформлениеГруппы(ГруппаРасписание, Элементы.ГруппаРасписание_0);
			
			ДекорацияРазделитель_2 = Элементы.Добавить("Разделитель_2_" + ИндексНастройки, Тип("ДекорацияФормы"),
				ГруппаРасписание);
			ДекорацияРазделитель_2.Заголовок = Элементы.Разделитель_2_0.Заголовок;
			
			ПолеНачалоВыполнения = Элементы.Добавить("НачалоВыполнения_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаРасписание);
			ПолеНачалоВыполнения.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].НачалоВыполнения";
			УстановитьОформлениеПоля(ПолеНачалоВыполнения, Элементы.НачалоВыполнения_0);
			ПолеНачалоВыполнения.УстановитьДействие("ПриИзменении", "Подключаемый_НачалоВыполненияПриИзменении");

			ДекорацияРазделитель = Элементы.Добавить("Разделитель_" + ИндексНастройки, Тип("ДекорацияФормы"),
				ГруппаРасписание);
			ДекорацияРазделитель.Заголовок = Элементы.Разделитель_0.Заголовок;
			
			ПолеПериодичность = Элементы.Добавить("Периодичность_" + ИндексНастройки, Тип("ПолеФормы"), ГруппаРасписание);
			ПолеПериодичность.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].Периодичность";
			УстановитьОформлениеПоля(ПолеПериодичность, Элементы.Периодичность_0);
			ПолеПериодичность.УстановитьДействие("ПриИзменении", "Подключаемый_ПериодичностьПриИзменении");
			Для Каждого ЭлементСписка Из Элементы.Периодичность_0.СписокВыбора Цикл
				ПолеПериодичность.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
			КонецЦикла;
			
			ПолеОкончаниеВыполнения = Элементы.Добавить("ОкончаниеВыполнения_" + ИндексНастройки, Тип("ПолеФормы"),
				ГруппаРасписание);
			ПолеОкончаниеВыполнения.ПутьКДанным = "НастройкиЗадач[" + ИндексНастройки + "].ОкончаниеВыполнения";
			УстановитьОформлениеПоля(ПолеОкончаниеВыполнения, Элементы.ОкончаниеВыполнения_0);
			ПолеОкончаниеВыполнения.УстановитьДействие("ПриИзменении", "Подключаемый_ОкончаниеВыполненияПриИзменении");

			ГруппаТретьейСтрокиДействия = Элементы.Добавить("ГруппаДействие_" + ИндексНастройки + "_Строка_3",
				Тип("ГруппаФормы"), ГруппаСтроки);
			УстановитьОформлениеГруппы(ГруппаТретьейСтрокиДействия, Элементы.ГруппаДействие_0_Строка_3);
			Элементы.Переместить(Элементы.ДобавитьДействие, Элементы["Действие_" + ИндексНастройки]);

			ПолеАвтоматическиСоздаватьРегулярныйПлатеж = Элементы.Добавить("АвтоматическиСоздаватьРегулярныйПлатеж_"
				+ ИндексНастройки, Тип("ПолеФормы"), ГруппаТретьейСтрокиДействия);
			ПолеАвтоматическиСоздаватьРегулярныйПлатеж.ПутьКДанным =
				"НастройкиЗадач[" + ИндексНастройки + "].АвтоматическиСоздаватьРегулярныйПлатеж";
			ПолеАвтоматическиСоздаватьРегулярныйПлатеж.Вид = Элементы.АвтоматическиСоздаватьРегулярныйПлатеж_0.Вид;
			ПолеАвтоматическиСоздаватьРегулярныйПлатеж.ПоложениеЗаголовка =
				Элементы.АвтоматическиСоздаватьРегулярныйПлатеж_0.ПоложениеЗаголовка;
			ПолеАвтоматическиСоздаватьРегулярныйПлатеж.УстановитьДействие("ПриИзменении",
				"Подключаемый_ПолеАвтоматическиСоздаватьРегулярныйПлатежПриИзменении");
			
		КонецЕсли;
		
	КонецЦикла;
		
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьМодифицированностьПриИзмененииПоля(ИмяЭлемента)
	Индекс = ИндексБлока(ИмяЭлемента);
	СтрокаНастроек = НастройкиЗадач.Получить(Индекс);
	СтрокаНастроек.Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	ИнформационнаяБазаЗарегистрирована = СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована();
	
	Отбор = Новый Структура;
	Отбор.Вставить("Удалена", Ложь);
	
	ЕстьОшибкиПодключенияБазы = НЕ ИнформационнаяБазаЗарегистрирована;		
	Элементы.ПанельОшибки.Видимость = ЕстьОшибкиПодключенияБазы И ВРаботе;
	Элементы.ОшибкаНеПодключеныОбсуждения.Видимость = ЕстьОшибкиПодключенияБазы И ВРаботе;
	
	ВРаботе = НЕ ЕстьОшибкиПодключенияБазы И ВРаботе;
		
	Для Каждого СтрокаНастроек Из НастройкиЗадач Цикл
		
		ИндексНастройки = НастройкиЗадач.Индекс(СтрокаНастроек);
		Если СтрокаНастроек.Удалена Тогда 
			Продолжить;
		КонецЕсли;
		
		ПолеУчетнаяЗапись			= Элементы["УчетнаяЗапись_" + ИндексНастройки];
		ПолеТипОповещения			= Элементы["ТипОповещения_" + ИндексНастройки];
		ПолеТипОбсуждения			= Элементы["ТипОбсуждения_" + ИндексНастройки];
		ПолеСпособОповещения		= Элементы["СпособОповещения_" + ИндексНастройки];
		ПолеОбсуждение				= Элементы["Обсуждение_" + ИндексНастройки];
		ПолеПользовательДляПисем	= Элементы["ПользовательДляПисем_" + ИндексНастройки];
		
		Если СтрокаНастроек.ТипОповещения = "Обсуждение" Тогда	
			Если СтрокаНастроек.ТипОбсуждения = "Личное" Тогда
				ПолеСпособОповещения.Заголовок = НСтр("ru = '(кому)'");
			Иначе
				ПолеСпособОповещения.Заголовок = НСтр("ru = 'и (кого оповестить)'");
			КонецЕсли;	
		Иначе
			ПолеСпособОповещения.Заголовок = НСтр("ru = '(кому)'");
		КонецЕсли;
		
		ПолеТипОбсуждения.Видимость = СтрокаНастроек.ТипОповещения = "Обсуждение";
		ПолеОбсуждение.Видимость	=  СтрокаНастроек.ТипОповещения = "Обсуждение"
			И СтрокаНастроек.ТипОбсуждения = "Групповое";
		ПолеСпособОповещения.Видимость = НЕ СтрокаНастроек.ТипОбсуждения = "Групповое"
			И НЕ СтрокаНастроек.ТипОповещения = "Email";
			
		ПолеУчетнаяЗапись.Видимость			= СтрокаНастроек.ТипОповещения = "Email";
		ПолеПользовательДляПисем.Видимость	= СтрокаНастроек.ТипОповещения = "Email";
	КонецЦикла;
	
	Если ОтображатьОдинДокумент Тогда
		Элементы.ВыполняетсяПоОдномуДокументу.Видимость = Истина;
		Элементы.Заголовок.Видимость = Ложь;
		Элементы.ДобавитьДействие.Видимость = Ложь;
		Элементы.ИсходныйДокумент_0.Видимость = Ложь;
		Элементы.Разделитель_1_0.Заголовок = НСтр("ru='о том, что пришло время создать платеж по этому документу'");
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#Область ЗадачиАссистента

&НаСервере
Функция ЗадачаПоРегулярнымПлатежам()
	
	Возврат Справочники.ЗадачиАссистентаПоРегулярнымПлатежам.ЗадачаПоРегулярнымПлатежамОсновная;
	
КонецФункции

&НаСервере
Процедура ОбновитьБлокиНастроекАссистента()
	
	Задача = ЗадачаПоРегулярнымПлатежам();
	
	Если ОтображатьОдинДокумент Тогда
		СтрокаТЧ = Задача.ПараметрыРегулярныхПлатежей.Найти(ИсходныйДокументОтбор, "ИсходныйДокумент");
		НастройкаЗадачиПоНовомуДокументу = СтрокаТЧ = Неопределено;
	Иначе	
		НастройкаЗадачиПоНовомуДокументу = Ложь;
	КонецЕсли;

	Если Задача.ПараметрыРегулярныхПлатежей.Количество() = 0 Или НастройкаЗадачиПоНовомуДокументу Тогда
		ДобавитьПустыеНастройкиЗадачи();
		Если НастройкаЗадачиПоНовомуДокументу Тогда
			СтрокаНастроек = НастройкиЗадач[0];
			СтрокаНастроек.ИсходныйДокумент = ИсходныйДокументОтбор;
			КалендарныеДанные = КалендарныеДанные(СтрокаНастроек.ИсходныйДокумент);
			ЗаполнитьЗначенияСвойств(СтрокаНастроек, КалендарныеДанные);
		КонецЕсли;
		Возврат;
	Иначе

		НастройкиЗадач.Очистить();	
		ДанныеЗадачи = Задача.ПолучитьОбъект();
		
		Для Каждого СтрокаТЧ Из ДанныеЗадачи.ПараметрыРегулярныхПлатежей Цикл
			
			Если ОтображатьОдинДокумент Тогда
				Если СтрокаТЧ.ИсходныйДокумент <> ИсходныйДокументОтбор Тогда
					Продолжить;
				КонецЕсли;
				ВыполняетсяПоОдномуДокументу = Истина;
			КонецЕсли;

			СтрокаНастроек = НастройкиЗадач.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаНастроек, СтрокаТЧ);
			СтрокаНастроек.ИсходныйДокументДоИзменения = СтрокаНастроек.ИсходныйДокумент;
			ЗначенияЗадачиПоИдентификатору(СтрокаНастроек, СтрокаТЧ);
			ЗаполнитьСпособОповещенияПоЗначению(
				СтрокаНастроек,
				СтрокаНастроек.ПользовательДляОповещения,
				СтрокаТЧ.СпособОповещения);
		КонецЦикла;
			
		ВРаботе = ДанныеЗадачи.Используется;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КалендарныеДанные(ИсходныйДокумент)

	Результат = Новый Структура("НачалоВыполнения, ОкончаниеВыполнения, Периодичность");
	
	МинимальнаяДатаНачала = КонецДня(ТекущаяДатаСеанса()) + 1;
	ДатаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ИсходныйДокумент, "Дата");
	ДатаНачалаОтДатыДокумента = ДобавитьМесяц(ДатаДокумента, 1);
	Результат.НачалоВыполнения = Макс(МинимальнаяДатаНачала, ДатаНачалаОтДатыДокумента);
	Результат.ОкончаниеВыполнения = ДобавитьМесяц(Результат.НачалоВыполнения, 60);
	Результат.Периодичность = Перечисления.Периодичность.Месяц;
	
	Возврат Результат;

КонецФункции

&НаСервере
Процедура ДобавитьПустыеНастройкиЗадачи()
	
	ЛичноеСообщение = Перечисления.СпособОповещенияАссистентаУправления.СообщениеЛичногоОбсужденияПользователю;
	
	ОбщиеНастройки = Новый Структура;
	ОбщиеНастройки.Вставить("Модифицированность", Истина);

	ОбщиеНастройки.Вставить("ТипОповещения",			 "Обсуждение");
	ОбщиеНастройки.Вставить("ТипОбсуждения",			 "Личное");
	ОбщиеНастройки.Вставить("ПользовательДляОповещения", Пользователи.АвторизованныйПользователь());
	
	НоваяНастройка = НастройкиЗадач.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяНастройка, ОбщиеНастройки);
	
	ЗаполнитьСпособОповещенияПоЗначению(НоваяНастройка, 
		НоваяНастройка.ПользовательДляОповещения,
		ЛичноеСообщение);
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпособОповещенияПоЗначению(СтрокаНастроек, ПользовательДляОповещения, СпособОповещения)
	
	Если НЕ ЗначениеЗаполнено(ПользовательДляОповещения) 
			И СтрокаНастроек.ТипОбсуждения <> "Групповое" Тогда
		
		ПредставлениеСпособаОповещения = СпособыОповещения.НайтиСтроки(Новый Структура("Значение", СпособОповещения));
		Если ПредставлениеСпособаОповещения.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		СтрокаНастроек.СпособОповещения = ПредставлениеСпособаОповещения[0].Представление;
		СтрокаНастроек.ПользовательДляОповещения = ПользовательДляОповещения;
		
		Возврат;
		
	КонецЕсли;
	
	СтрокаНастроек.СпособОповещения 		 = ПользовательДляОповещения;
	СтрокаНастроек.ПользовательДляОповещения = ПользовательДляОповещения;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпособыОповещения()
	
	СпособыОповещения.Очистить();
	
	СпособОповестиПользователя = СпособыОповещения.Добавить();
	СпособОповестиПользователя.Значение = 
		Перечисления.СпособОповещенияАссистентаУправления.СообщениеЛичногоОбсужденияПользователю;
	СпособОповестиПользователя.Представление = НСтр("ru = 'Другому пользователю'");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпособОповещенияПоПредставлению(СтрокаТЧ, СтрокаНастроек)
	
	Таблица = СпособыОповещения;
	
	СтрокаТЧ.УчетнаяЗапись = СтрокаНастроек.УчетнаяЗапись;
	Если СтрокаНастроек.ТипОповещения = "Email" Тогда
		СтрокаТЧ.СпособОповещения = Перечисления.СпособОповещенияАссистентаУправления.ПустаяСсылка();
		Возврат;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(СтрокаНастроек.ПользовательДляОповещения) И СтрокаНастроек.ТипОбсуждения = "Личное" Тогда
		СтрокаТЧ.СпособОповещения = Перечисления.СпособОповещенияАссистентаУправления.СообщениеЛичногоОбсужденияПользователю;
	Иначе
		СтрокаТЧ.СпособОповещения = Перечисления.СпособОповещенияАссистентаУправления.СообщениеОбщегоОбсуждения;
	КонецЕсли;
	
	ЗначениеСпособаОповещения = Таблица.НайтиСтроки(Новый Структура("Представление", СтрокаТЧ.СпособОповещения));
	
	Если ЗначениеСпособаОповещения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТЧ.СпособОповещения = ЗначениеСпособаОповещения[0].Значение;
			
КонецПроцедуры

&НаСервере
Процедура СоздатьИзменитьЗадачиАссистента()
	
	ЗадачиСПриветствием = Новый Массив;
	
	НачатьТранзакцию();
	Попытка
					
		Задача = ЗадачаПоРегулярнымПлатежам();
		ЗадачаОбъект = Задача.ПолучитьОбъект();
		ЗадачаОбъект.Заблокировать();
		Если ПустойСправочникЗадач Тогда
			ЗадачаОбъект.НеобходимоПоприветствовать = Истина;
			АвторИзменений = Пользователи.АвторизованныйПользователь();
			ЕстьЗадачиПользователя = АссистентУправления.ЕстьЗадачиПользователя(АвторИзменений);
		КонецЕсли;
		
		Если ОтображатьОдинДокумент Тогда
			// Форма открыта из документа
			СтрокаНастроек = НастройкиЗадач[0];
		
			Если ВыполняетсяПоОдномуДокументу Тогда
				ЗадачаОбъект.Используется = Истина;
				ЭтотОбъект.ВРаботе = Истина;
			КонецЕсли;

			СтрокаТЧ = ЗадачаОбъект.ПараметрыРегулярныхПлатежей.Найти(ИсходныйДокументОтбор, "ИсходныйДокумент");
			
			Если ВыполняетсяПоОдномуДокументу Тогда
				// Флаг установлен - добавляем или изменяем строку
				
				Если СтрокаТЧ = Неопределено Тогда
					СтрокаТЧ = ЗадачаОбъект.ПараметрыРегулярныхПлатежей.Добавить();
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаНастроек);
				
				СтрокаТЧ.Идентификатор = ИдентификаторПоТипуОповещения(СтрокаНастроек.ТипОповещения);
				
				ЗаполнитьСпособОповещенияПоПредставлению(СтрокаТЧ, СтрокаНастроек);
				ЗаполнитьЗначенияЗадачиПоИдентификатору(СтрокаТЧ, СтрокаНастроек);
				
				Если СтрокаНастроек.Модифицированность Тогда
					РегистрыСведений.РегулярныеПлатежиГрафик.ОбновитьГрафикПоДокументу(СтрокаТЧ);
					СтрокаНастроек.Модифицированность = Ложь;
				КонецЕсли;
			Иначе
				// Флаг снят - удаляем строку
				Если СтрокаТЧ <> Неопределено Тогда
					ЗадачаОбъект.ПараметрыРегулярныхПлатежей.Удалить(СтрокаТЧ);	
					РегистрыСведений.РегулярныеПлатежиГрафик.ОчиститьГрафикПоДокументу(СтрокаНастроек.ИсходныйДокумент);
				КонецЕсли;
				СтрокаНастроек.Модифицированность = Ложь;
			КонецЕсли;
				
		Иначе
			// Форма открыта из Ассистента управления
			ЗадачаОбъект.Используется = ЭтотОбъект.ВРаботе;
			ЗадачаОбъект.ПараметрыРегулярныхПлатежей.Очистить();
			
			Для Каждого СтрокаНастроек Из НастройкиЗадач Цикл

				СохранятьСтроку = Не СтрокаНастроек.Удалена И ВРаботе;
				
				Если Не СохранятьСтроку Тогда
					РегистрыСведений.РегулярныеПлатежиГрафик.ОчиститьГрафикПоДокументу(СтрокаНастроек.ИсходныйДокумент);
					Продолжить;
				КонецЕсли;
				
				СтрокаТЧ = ЗадачаОбъект.ПараметрыРегулярныхПлатежей.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаНастроек);
				
				СтрокаТЧ.Идентификатор = ИдентификаторПоТипуОповещения(СтрокаНастроек.ТипОповещения);
				
				ЗаполнитьСпособОповещенияПоПредставлению(СтрокаТЧ, СтрокаНастроек);
				ЗаполнитьЗначенияЗадачиПоИдентификатору(СтрокаТЧ, СтрокаНастроек);
				
				Если СтрокаНастроек.Модифицированность Тогда
					РегистрыСведений.РегулярныеПлатежиГрафик.ОбновитьГрафикПоДокументу(СтрокаТЧ,
						СтрокаНастроек.ИсходныйДокументДоИзменения);
					СтрокаНастроек.Модифицированность = Ложь;
				КонецЕсли;
				
			КонецЦикла;
		
		КонецЕсли;
		
		Если ЗадачаОбъект.Модифицированность() Тогда
			ЗадачаОбъект.Записать();
		КонецЕсли;
		
		Если ЗадачаОбъект.НеобходимоПоприветствовать Тогда
			ЗадачиСПриветствием.Добавить(ЗадачаОбъект.Ссылка);
		КонецЕсли;
		
		ЗадачаОбъект.Разблокировать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
		
	КонецПопытки;
	
	Если ЗадачиСПриветствием.Количество() <> 0 Тогда
		Справочники.ЗадачиАссистентаПоРегулярнымПлатежам.ПоприветствоватьПользователяПриЗаписиПервыхЗадач(ЗадачиСПриветствием);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначенияЗадачиПоИдентификатору(СтрокаТЧ, СтрокаНастроек)
	
	Если СтрНайти(СтрокаТЧ.Идентификатор, "Обсуждении") > 0 Тогда
		СтрокаТЧ.ПользовательДляОповещения 		= СтрокаНастроек.ПользовательДляОповещения;
		СтрокаТЧ.ИдентификаторОбщегоОбсуждения	= СтрокаНастроек.ОбсуждениеИдентификатор;
	КонецЕсли;
	
	Если СтрНайти(СтрокаТЧ.Идентификатор, "Email") > 0 Тогда
		СтрокаТЧ.ПользовательДляОповещения	= СтрокаНастроек.ПользовательДляПисем;
		СтрокаТЧ.УчетнаяЗапись				= СтрокаНастроек.УчетнаяЗапись;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуЗадач()
	
	Результат = Новый Структура();
	
	Результат.Вставить("ИзмененПризнакВРаботе", ИзмененПризнакВРаботе);
	Результат.Вставить("АвторИзменений", АвторИзменений);
	Результат.Вставить("ГруппаЗадач", "ОповещениеПользователяОПоказателе");
	Результат.Вставить("НужноДобавитьВОбсуждение", НЕ ЕстьЗадачиПользователя);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Функция ИдентификаторПоТипуОповещения(ТипОповещения)
	
	Если ТипОповещения = "Обсуждение" Тогда
		Возврат "ОповеститьСотрудникаОРегулярномПлатежеВОбсуждении";
	КонецЕсли;
	
	Если ТипОповещения = "Email" Тогда
		Возврат "ОповеститьСотрудникаОРегулярномПлатежеПоEmail";
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗначенияЗадачиПоИдентификатору(СтрокаНастроек, ДанныеЗадачи)
	
	Если СтрНайти(ДанныеЗадачи.Идентификатор, "Обсуждении") Тогда
		
		СтрокаНастроек.ТипОповещения = "Обсуждение";
		СообщениеОтветственному = Перечисления.СпособОповещенияАссистентаУправления.СообщениеЛичногоОбсужденияОтветственному;
		СообщениеПользователю = Перечисления.СпособОповещенияАссистентаУправления.СообщениеЛичногоОбсужденияПользователю;
		ОбщееСообщение = Перечисления.СпособОповещенияАссистентаУправления.СообщениеОбщегоОбсуждения;
		
		Если ДанныеЗадачи.СпособОповещения = ОбщееСообщение Тогда
			СтрокаНастроек.ТипОбсуждения = "Групповое";
		Иначе
			СтрокаНастроек.ТипОбсуждения = "Личное";
		КонецЕсли;
		
		СтрокаНастроек.ОбсуждениеИдентификатор = ДанныеЗадачи.ИдентификаторОбщегоОбсуждения;
		
		Если ЗначениеЗаполнено(СтрокаНастроек.ОбсуждениеИдентификатор) Тогда
			ОбщееОбсуждение = ОбсужденияУНФ.ПолучитьОбсуждениеБезопасно(
				Новый ИдентификаторОбсужденияСистемыВзаимодействия(СтрокаНастроек.ОбсуждениеИдентификатор));
			Если ОбщееОбсуждение <> Неопределено И ТекущийПользовательУчаствуетВОбсуждении(ОбщееОбсуждение) Тогда
				СтрокаНастроек.Обсуждение = ОбщееОбсуждение.Заголовок;
			Иначе
				СтрокаНастроек.Обсуждение = НСтр("ru='<Объект не найден>'");
			КонецЕсли;
		КонецЕсли;
	
	КонецЕсли;
			
	Если СтрНайти(ДанныеЗадачи.Идентификатор, "Email") Тогда
		СтрокаНастроек.ТипОповещения		= "Email";
		СтрокаНастроек.УчетнаяЗапись		= ДанныеЗадачи.УчетнаяЗапись;
		СтрокаНастроек.ПользовательДляПисем	= ДанныеЗадачи.ПользовательДляОповещения;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ТекущийПользовательУчаствуетВОбсуждении(Обсуждение)
	ИдентификаторТекущегоПользователя = СистемаВзаимодействия.ИдентификаторТекущегоПользователя();
	Возврат Обсуждение.Участники.Содержит(ИдентификаторТекущегоПользователя);
КонецФункции

&НаСервере
Процедура УстановитьОформлениеПоля(ДобавляемыйЭлемент, ЭлементФормы)
	
	ДобавляемыйЭлемент.Вид = ЭлементФормы.Вид;
	ДобавляемыйЭлемент.ПоложениеЗаголовка = ЭлементФормы.ПоложениеЗаголовка;
	ДобавляемыйЭлемент.Заголовок = ЭлементФормы.Заголовок;
	ДобавляемыйЭлемент.АвтоМаксимальнаяШирина = ЭлементФормы.АвтоМаксимальнаяШирина;
	ДобавляемыйЭлемент.МаксимальнаяШирина = ЭлементФормы.МаксимальнаяШирина;
	ДобавляемыйЭлемент.РежимВыбораИзСписка = ЭлементФормы.РежимВыбораИзСписка;
	ДобавляемыйЭлемент.КнопкаВыпадающегоСписка = ЭлементФормы.КнопкаВыпадающегоСписка;
	ДобавляемыйЭлемент.КнопкаОткрытия = ЭлементФормы.КнопкаОткрытия;
	ДобавляемыйЭлемент.КнопкаВыбора = ЭлементФормы.КнопкаВыбора;
	ДобавляемыйЭлемент.АвтоОтметкаНезаполненного = ЭлементФормы.АвтоОтметкаНезаполненного;
	ДобавляемыйЭлемент.ОтображениеПодсказки = ЭлементФормы.ОтображениеПодсказки;
	ДобавляемыйЭлемент.Подсказка = ЭлементФормы.Подсказка;
	ДобавляемыйЭлемент.ИсторияВыбораПриВводе = ЭлементФормы.ИсторияВыбораПриВводе;
	ДобавляемыйЭлемент.БыстрыйВыбор = ЭлементФормы.БыстрыйВыбор;
	ДобавляемыйЭлемент.КнопкаСоздания = ЭлементФормы.КнопкаСоздания;
	ДобавляемыйЭлемент.КнопкаОткрытия = ЭлементФормы.КнопкаОткрытия;
	ДобавляемыйЭлемент.ПодсказкаВвода = ЭлементФормы.ПодсказкаВвода;
	ДобавляемыйЭлемент.Доступность = ЭлементФормы.Доступность;

КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеГруппы(ДобавляемаяГруппа, ГруппаФормы)
	
	ДобавляемаяГруппа.Вид = ГруппаФормы.Вид;
	ДобавляемаяГруппа.Отображение = ГруппаФормы.Отображение;
	ДобавляемаяГруппа.Группировка = ГруппаФормы.Группировка;
	ДобавляемаяГруппа.ОтображатьЗаголовок = ГруппаФормы.ОтображатьЗаголовок;
	ДобавляемаяГруппа.ЦветФона = ГруппаФормы.ЦветФона;
	ДобавляемаяГруппа.СквозноеВыравнивание = ГруппаФормы.СквозноеВыравнивание;
	ДобавляемаяГруппа.ГоризонтальныйИнтервал = ГруппаФормы.ГоризонтальныйИнтервал;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОбсуждения(Команда)
	ОткрытьФорму("Обработка.АссистентУправления.Форма.ВыборОбсуждения");
КонецПроцедуры

&НаКлиенте
Процедура ВыборОбсуждения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ВыборОбсужденияНаСервере(Результат, ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура ВыборОбсужденияНаСервере(Результат, ДополнительныеПараметры)
	
	Индекс = Число(ДополнительныеПараметры);
	НастройкиЗадач[Индекс].Обсуждение = Результат.Тема;
	НастройкиЗадач[Индекс].ОбсуждениеИдентификатор = Результат.Идентификатор;
	НастройкиЗадач[Индекс].Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Оповестить("ОбновлениеГрафикаРегулярныхПлатежей", ОтображатьОдинДокумент);

КонецПроцедуры

#КонецОбласти
