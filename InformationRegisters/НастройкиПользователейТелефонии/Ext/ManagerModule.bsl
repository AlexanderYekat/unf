#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает данные пользователя телефонии, записанные в регистре сведений НастройкиПользователейТелефонии
// 
// Параметры:
//  ВнутреннийИдентификаторВАТС - Строка - уникальный идентификатор абонента на стороне АТС
// 
// Возвращаемое значение:
//  Структура - Выборка - данные записанные в регистре сведений НастройкиПользователейТелефонии
//
Функция ПолучитьДанныеПользователя(ВнутреннийИдентификаторВАТС) Экспорт

	Запрос = Новый Запрос ("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиПользователейТелефонии.Пользователь КАК Пользователь,
	|	НастройкиПользователейТелефонии.ВнутреннийНомер КАК ВнутреннийНомер,
	|	НастройкиПользователейТелефонии.ИсходящийНомер КАК ИсходящийНомер,
	|	НастройкиПользователейТелефонии.ВнутреннийИдентификаторВАТС КАК ВнутреннийИдентификаторВАТС
	|ИЗ
	|	РегистрСведений.НастройкиПользователейТелефонии КАК НастройкиПользователейТелефонии
	|ГДЕ
	|	НастройкиПользователейТелефонии.ВнутреннийИдентификаторВАТС = &ВнутреннийИдентификаторВАТС");
	
	Запрос.УстановитьПараметр("ВнутреннийИдентификаторВАТС", ВнутреннийИдентификаторВАТС);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Возврат Выборка;

КонецФункции

// Возвращает данные пользователя телефонии, записанные в регистре сведений НастройкиПользователейТелефонии
// 
// Параметры:
//  ВнутреннийНомер - Строка - внутренний номер пользователя телефонии
// 
// Возвращаемое значение:
//  Структура - Выборка - данные записанные в регистре сведений НастройкиПользователейТелефонии
//
Функция НайтиПоВнутреннемуНомеру(ВнутреннийНомер) Экспорт

	Запрос = Новый Запрос ("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиПользователейТелефонии.Пользователь КАК Пользователь,
	|	НастройкиПользователейТелефонии.ВнутреннийНомер КАК ВнутреннийНомер,
	|	НастройкиПользователейТелефонии.ИсходящийНомер КАК ИсходящийНомер,
	|	НастройкиПользователейТелефонии.ВнутреннийИдентификаторВАТС КАК ВнутреннийИдентификаторВАТС
	|ИЗ
	|	РегистрСведений.НастройкиПользователейТелефонии КАК НастройкиПользователейТелефонии
	|ГДЕ
	|	НастройкиПользователейТелефонии.ВнутреннийНомер = &ВнутреннийНомер");
	
	Запрос.УстановитьПараметр("ВнутреннийНомер", ВнутреннийНомер);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.ВнутреннийИдентификаторВАТС;

КонецФункции

#КонецОбласти

#КонецЕсли