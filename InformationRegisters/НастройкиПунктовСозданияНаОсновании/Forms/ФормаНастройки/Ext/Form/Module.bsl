///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)  
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();   
	ПолноеИмяОбъекта =  Параметры.ПолноеИмяОбъекта;           
	ЗагрузитьТекущиеНастройкиПользователя();  
	Если СкрытьПредупреждение Тогда
		Элементы.ГруппаПредупреждение.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СнятьФлажки(Команда)
	Для Каждого СтрокаСписка Из ВыбранныеПункты Цикл
		СтрокаСписка.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	Для Каждого СтрокаСписка Из ВыбранныеПункты Цикл
		СтрокаСписка.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	ЗаписатьВыбранныеПунктыПользователя();
	Оповестить("ВыполненаНастройкаПунктовСозданияНаОсновании");       
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьНастройкиПоУмолчанию(Команда)
	ВосстановитьНастройкиПоУмолчаниюНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЗакрытьПредупреждениеНажатие(Элемент)
	Элементы.ГруппаПредупреждение.Видимость = Ложь;
	СкрытьПредупреждение = Истина;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьВыбранныеПунктыПользователя()
	РегистрыСведений.НастройкиПунктовСозданияНаОсновании.ЗаписатьНастройкиПользователя(ТекущийПользователь, ПолноеИмяОбъекта, ВыбранныеПункты.Выгрузить());
	ОбновитьПовторноИспользуемыеЗначения();
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройкиПоУмолчаниюНаСервере()
	РегистрыСведений.НастройкиПунктовСозданияНаОсновании.УдалитьНастройкиПользователя(ТекущийПользователь, ПолноеИмяОбъекта);
	ОбновитьПовторноИспользуемыеЗначения();                  
	ЗагрузитьТекущиеНастройкиПользователя();
КонецПроцедуры 

&НаСервере
Процедура ЗагрузитьТекущиеНастройкиПользователя()
	НастройкиПользователя = РегистрыСведений.НастройкиПунктовСозданияНаОсновании.НастройкиПользователя(ТекущийПользователь, ПолноеИмяОбъекта); 
	ВыбранныеПункты.Загрузить(НастройкиПользователя);        
КонецПроцедуры


#КонецОбласти
