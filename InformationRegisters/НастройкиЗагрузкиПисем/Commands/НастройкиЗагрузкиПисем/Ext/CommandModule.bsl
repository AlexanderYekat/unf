
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	РезультатПроверкиУЗ = РезультатПроверкиПодключенияУчетнойЗаписи(ПараметрКоманды);
	
	Если РезультатПроверкиУЗ.Подключена Тогда
		
		ПараметрыФормы = Новый Структура("УчетнаяЗапись", ПараметрКоманды);
		ОткрытьФорму("РегистрСведений.НастройкиЗагрузкиПисем.Форма.ФормаНастройки",  ПараметрыФормы,
		ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно,
		ПараметрыВыполненияКоманды.НавигационнаяСсылка);
		
		Возврат;
		
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Ключ", ПараметрКоманды);
	ОткрытьФорму("Справочник.УчетныеЗаписиЭлектроннойПочты.Форма.ФормаЭлемента", ПараметрыФормы);
	
	ПоказатьПредупреждениеОНеНастроеннойУчетнойЗаписи(РезультатПроверкиУЗ.СообщениеОбОшибке);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция РезультатПроверкиПодключенияУчетнойЗаписи(УчетнаяЗапись)
	
	Результат = Новый Структура;
	
	СообщениеОбОшибке = "";
	ДополнительноеСообщение = "";
	
	РаботаСПочтовымиСообщениями.ПроверитьВозможностьОтправкиИПолученияЭлектроннойПочты(
	УчетнаяЗапись, СообщениеОбОшибке, ДополнительноеСообщение);
	
	Результат.Вставить("Подключена", ПустаяСтрока(СообщениеОбОшибке));
	Результат.Вставить("СообщениеОбОшибке", СообщениеОбОшибке);
	Результат.Вставить("ДополнительноеСообщение", ДополнительноеСообщение);
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьПредупреждениеОНеНастроеннойУчетнойЗаписи(СообщениеОбОшибке)
	
	КомпонентФС1 = Новый ФорматированнаяСтрока(НСтр("ru = 'Учетная запись не настроена.
	|Переход к настройкам загрузки писем возможен только после настройки учетной записи.'"),
	Новый Шрифт(, , Истина));
	
	ТекстПредупреждения = Новый ФорматированнаяСтрока(
	КомпонентФС1, Символы.ПС + Символы.ПС, СообщениеОбОшибке);
	
	ПоказатьПредупреждение(,
	ТекстПредупреждения, ,
	НСтр("ru = 'Проверка настроек учетной записи'"));
	
КонецПроцедуры

#КонецОбласти
