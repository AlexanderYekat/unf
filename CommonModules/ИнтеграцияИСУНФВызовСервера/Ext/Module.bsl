#Область СлужебныйПрограммныйИнтерфейс

Функция ВидыПродукцииВЗапасах(Знач Запасы) Экспорт
	
	Возврат ИнтеграцияИСУНФ.ВидыПродукцииВЗапасах(Запасы);
	
КонецФункции

// Заполняет служебные реквизиты в строке таблицы формы.
//
// Параметры:
//  ИсходныеДанные - Структура - источник данных,
//  СлужебныеРеквизиты - Структура - перечень служебных реквизитов для заполнения.
//
Процедура ЗаполнитьСлужебныеРеквизиты(ИсходныеДанные, СлужебныеРеквизиты) Экспорт
	
	Если СлужебныеРеквизиты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	
	Для Каждого Элемент Из ИсходныеДанные Цикл
		Если Элемент.Значение = Неопределено Тогда
			ОписаниеТипа = ОбщегоНазначения.ОписаниеТипаВсеСсылки();
		ИначеЕсли ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Элемент.Значение)) Тогда
			ОписаниеТипа = Новый ОписаниеТипов(СтрЗаменить(Элемент.Значение.Метаданные().ПолноеИмя(), ".", "Ссылка."));
		Иначе
			ОписаниеТипа = Новый ОписаниеТипов(Строка(ТипЗнч(Элемент.Значение)));
		КонецЕсли;
		ТаблицаТоваров.Колонки.Добавить(Элемент.Ключ, ОписаниеТипа);
	КонецЦикла;
	
	Для Каждого СлужебныйРеквизит Из СлужебныеРеквизиты Цикл
		ТаблицаТоваров.Колонки.Добавить(СлужебныйРеквизит.Ключ);
	КонецЦикла;
	
	СтрокаТовары = ТаблицаТоваров.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТовары, СлужебныеРеквизиты);
	ЗаполнитьЗначенияСвойств(СтрокаТовары, ИсходныеДанные);
	
	ИнтеграцияИСУНФ.ЗаполнитьСлужебныеРеквизитыВКоллекции(Неопределено, ТаблицаТоваров, СлужебныеРеквизиты);
	
	ЗаполнитьЗначенияСвойств(СлужебныеРеквизиты, ТаблицаТоваров[0]);
	
КонецПроцедуры

// Возвращает заводской номер ФН
// 
// Параметры:
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - Идентификатор устройства
// 
// Возвращаемое значение:
//  Строка - номер ФН
Функция ЗаводскойНомерФискальногоНакопителя(ИдентификаторУстройства) Экспорт
	
	ЗаводскойНомерФН = "";
	
	Если ОбщегоНазначенияБПО.ИспользуетсяЧекопечатающиеУстройства() Тогда
		МодульОборудованиеЧекопечатающиеУстройства = ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеЧекопечатающиеУстройства");
		ПараметрыРегистрации = МодульОборудованиеЧекопечатающиеУстройства.ПараметрыРегистрацииУстройства(ИдентификаторУстройства);
		Если ПараметрыРегистрации.Свойство("ЗаводскойНомерФН") И ЗначениеЗаполнено(ПараметрыРегистрации.ЗаводскойНомерФН) Тогда
			ЗаводскойНомерФН = ПараметрыРегистрации.ЗаводскойНомерФН;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗаводскойНомерФН;
	
КонецФункции

#КонецОбласти