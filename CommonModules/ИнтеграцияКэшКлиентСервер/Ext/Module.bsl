#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция МаксимальнаяДлинаКлючаПолучить() Экспорт
	Возврат 555;
КонецФункции

Функция КлючРазделительПолучить() Экспорт
	Возврат "#";
КонецФункции

Функция КлючПоПараметрамПолучить( Параметры ) Экспорт

	МаксимальнаяДлинаКлюча = МаксимальнаяДлинаКлючаПолучить();
	КлючРазделитель = КлючРазделительПолучить();
	
	Значения = Новый Массив;
	ПараметрыТип = ТипЗнч( Параметры );
	Если ПараметрыТип = Тип( "Массив" ) Тогда
		Для Каждого Параметр Из Параметры Цикл
			ЗначениеСтрока = ЗначениеВСтрокуДляКлюча( Параметр );
			Значения.Добавить( ЗначениеСтрока );
		КонецЦикла;
	ИначеЕсли ПараметрыТип = Тип( "Структура" ) Тогда
		Для Каждого Параметр Из Параметры Цикл
			Значения.Добавить( Параметр.Ключ );
			
			ЗначениеСтрока = ЗначениеВСтрокуДляКлюча( Параметр.Значение );
			Значения.Добавить( ЗначениеСтрока );
		КонецЦикла;
	ИначеЕсли ПараметрыТип = Тип( "СписокЗначений" ) Тогда
		Для Каждого Параметр Из Параметры Цикл
			ЗначениеСтрока = ЗначениеВСтрокуДляКлюча( Параметр.Значение );			
			Значения.Добавить( ЗначениеСтрока );
		КонецЦикла;
	ИначеЕсли ПараметрыТип = Тип( "Строка" ) Тогда	
		Значения.Добавить( Параметры );
	ИначеЕсли ПараметрыТип = Тип( "Число" )
		Или ПараметрыТип = Тип( "Булево" )
		Или ПараметрыТип = Тип( "Дата" )
	Тогда	
		ЗначениеСтрока = ЗначениеВСтрокуДляКлюча( Параметры );
		Значения.Добавить( ЗначениеСтрока );
	Иначе
		ВызватьИсключение "ИнтеграцияКэшКлиентСервер.КлючПоПараметрамПолучить: Ошибка: Тип параметра задан некорректно: " + ПараметрыТип;
	КонецЕсли;

	Результат = СтрСоединить( Значения, КлючРазделитель );
	
	Если ПустаяСтрока( Результат ) Тогда
		ВызватьИсключение "ИнтеграцияКэшКлиентСервер.КлючПоПараметрамПолучить: Ошибка: По параметрам получен пустой ключ";
	КонецЕсли;
	
	Результат = КлючРазделитель + Результат + КлючРазделитель;
	
	РезультатДлина = СтрДлина( Результат );
	Если РезультатДлина > МаксимальнаяДлинаКлюча Тогда
		ВызватьИсключение "ИнтеграцияКэшКлиентСервер.КлючПоПараметрамПолучить: Ошибка: Длина ключа больше максимальной, "
		 + РезультатДлина + " > " + МаксимальнаяДлинаКлюча + ": " + Результат;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция ЗначениеВСтрокуДляКлюча( Значение )
	
	Если Значение = Неопределено Или Значение = Null Тогда
		Возврат "";
	КонецЕсли;
	
	ЗначениеТип = ТипЗнч( Значение );
	
	Если ЗначениеТип = Тип( "Число" ) Тогда
		ЗначениеСтрока = Формат( Значение, "ЧГ=0" );
	ИначеЕсли ЗначениеТип = Тип( "Дата" ) Тогда
		ЗначениеСтрока = Формат( Значение, "ДФ=yyyy-MM-dd-HH-mm-ss" );
	ИначеЕсли ЗначениеТип = Тип( "Булево" ) Тогда
		ЗначениеСтрока = Формат( Значение, "БЛ=0; БИ=1" );
	Иначе
		ЗначениеСтрока = Строка( Значение );
	КонецЕсли;

	Возврат ЗначениеСтрока;
КонецФункции

#КонецОбласти
