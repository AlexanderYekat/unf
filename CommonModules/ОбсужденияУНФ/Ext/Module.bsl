
////////////////////////////////////////////////////////////////////////////////
// Серверные процедуры работы с обсуждениями
//
// Для новых объектов и при изменении из списка, изменения в СистемаВзаимодействия записываются регламентным заданием (отложенно)
// Для изменений в форме объектов, изменения в СистемаВзаимодействия записываются фоновым заданием (сразу)
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает ответственного сотрудника соответствующего пользователю обсуждений.
//
// Параметры:
//  Пользователь - ИдентификаторПользователяСистемыВзаимодействия - Пользователь системы взаимодействия.
// 
// Возвращаемое значение:
//   СправочникСсылка.Сотрудники
//
Функция СотрудникПользователя(ПользовательСВ) Экспорт
	
	ПользовательСправочника = ПользовательИнформационнойБазы(ПользовательСВ);
	Если Не ЗначениеЗаполнено(ПользовательСправочника) Тогда
		Возврат Справочники.Сотрудники.ПустаяСсылка();
	КонецЕсли;
	
	СотрудникиПользователя = РегистрыСведений.СотрудникиПользователя.ПолучитьСотрудниковПользователя(ПользовательСправочника);
	
	Если СотрудникиПользователя.Количество() <> 0 Тогда
		Возврат СотрудникиПользователя[0];
	Иначе
		Возврат Справочники.Сотрудники.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

Процедура ДобавитьОписаниеИзменений(ОписаниеИзменений, ТекстИзменений, Синоним = "") Экспорт
	
	ОбсужденияУНФИсторияДействийПользователя.ДобавитьОписаниеИзменений(ОписаниеИзменений, ТекстИзменений, Синоним);
	
КонецПроцедуры

Функция ДобавитьСообщениеОбИзмененииРеквизита(СтароеЗначение = Неопределено, НовоеЗначение = Неопределено, Формат = "") Экспорт
	
	Возврат ОбсужденияУНФИсторияДействийПользователя.ДобавитьСообщениеОбИзмененииРеквизита(СтароеЗначение, НовоеЗначение, Формат);
	
КонецФункции

Процедура ДобавитьСистемноеСообщение(ТекстСообщения, Ссылка) Экспорт
	
	ОбсужденияУНФИсторияДействийПользователя.ДобавитьСистемноеСообщение(ТекстСообщения, Ссылка);
	
КонецПроцедуры

// См. РаботаСФайламиПереопределяемый.ПриСозданииФайла
//
Процедура ПриСозданииФайла(Файл) Экспорт
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Если Не ЗначениеЗаполнено(ТекущийПользователь) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Файл) = Тип("СправочникСсылка.СобытиеПрисоединенныеФайлы") Тогда
		ИменаРеквизитов = "ВладелецФайла,ЭтоВложениеЭлектронногоПисьма";
	Иначе
		ИменаРеквизитов = "ВладелецФайла";
	КонецЕсли;
	
	ДанныеФайла = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Файл, ИменаРеквизитов);
	
	Если Не ЗначениеЗаполнено(ДанныеФайла.ВладелецФайла) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеФайла.Свойство("ЭтоВложениеЭлектронногоПисьма") И ДанныеФайла.ЭтоВложениеЭлектронногоПисьма Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = СтрШаблон(НСтр("ru='Присоединен файл %1'"), ПолучитьНавигационнуюСсылку(Файл));
	ОбсужденияУНФ.ДобавитьСистемноеСообщение(ТекстСообщения, ДанныеФайла.ВладелецФайла);
	
КонецПроцедуры

// См. ОбщегоНазначенияПереопределяемый.ПриДобавленииИсключенийПоискаСсылок
//
Процедура ПриДобавленииИсключенийПоискаСсылок(ИсключенияПоискаСсылок) Экспорт
	
	// Справочник неотправленных сообщений не должен мешать удалению объектов (документов или справочников).
	// Проверка существования ссылки будет происходить непосредственно в регламентном задании
	ИсключенияПоискаСсылок.Добавить(Метаданные.Справочники.НеотправленныеСообщенияСистемыВзаимодействия);
	
КонецПроцедуры

#Область СобытияФорм

Процедура ПослеЗаписиНаСервере(ТекущийОбъект) Экспорт
	
	Если Не ЛогироватьИзмененияОбъектовВОбсуждении() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ТекущийОбъект.ДополнительныеСвойства.Свойство("ДляОбсуждений") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоНовый = Неопределено;
	ТекущийОбъект.ДополнительныеСвойства.ДляОбсуждений.Свойство("ЭтоНовый", ЭтоНовый);
	Если ЭтоНовый = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Справочники.НеотправленныеСообщенияСистемыВзаимодействия.ЕстьНеотправленныеСообщенияПоОбъекту(ТекущийОбъект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, "ОбсужденияУНФ.ЗаписатьОтложенныеСообщенияВСистемуВзаимодействия", ТекущийОбъект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область Устаревшие

// Устарела. Будет удалена в следующей версии программы.
// Формирует Массив структур, содержащий имена и описания пользователей для использования в тексте обсуждений.
// 
// Возвращаемое значение:
//  Массив           - по числу пользователей: 
//   * Пользователь  - эл. справочника Пользователи
//   * Представление - Строка - Наименование пользователя,
//   * Имя 			 - Строка - Наименование пользователя сплошной строкой с нижним подчеркиванием, по типу "Фамилия_Имя";
//   * ДляПоиска 	 - Строка - Наименование пользователя сплошной строкой с нижним подчеркиванием, по типу "Фамилия_Имя",
//	 				   в нижнем регистре
Функция МассивОписанийПользователей() Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Пользователь,
	|	Пользователи.Наименование КАК Представление
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	НЕ Пользователи.ПометкаУдаления
	|	И НЕ Пользователи.Недействителен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Представление";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтруктураОписания = Новый Структура;
		СтруктураОписания.Вставить("Пользователь", Выборка.Пользователь);
		СтруктураОписания.Вставить("Представление", Выборка.Представление);
		СтруктураОписания.Вставить("Имя", СтрЗаменить(Выборка.Представление, " ", "_"));
		СтруктураОписания.Вставить("ДляПоиска", НРег(СтруктураОписания.Имя));
		Результат.Добавить(СтруктураОписания);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Устарела. Будет удалена в следующей версии программы.
// Добавляет запись обсуждения в регистр сведений Обсуждения для Ссылки, а также для упомянутых (по навигационным
// ссылкам) в тексте комментария объектов, формирует напоминания пользователям, упомянутым в тексте комментария
// Параметры:
//  Комментарий			   Строка 		- текст сообщения
//  Ссылка				   Любая ссылка - ссылка на объект обсуждения
//  МоментВремени          Дата и время - время сохранения сообщения
//  Системный			   Булево 		- Истина для сохранения изменений объекта, Ложь для сохранения комментариев к объекту
//  Пользователь		   Справочник Пользователи - Пользователь, сохраняющий комментарий
//
Процедура ДобавитьСообщение(
	ТекстСообщения,
	Ссылка,
	Дата = Неопределено,
	Системный = Ложь,
	Автор = Неопределено,
	Действия = Неопределено) Экспорт
	
	Если НЕ УправлениеНебольшойФирмойПовтИсп.ЛогироватьИзмененияОбъектов()
		ИЛИ ПустаяСтрока(ТекстСообщения)
		ИЛИ НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Автор) Тогда
		ПользовательАвторСообщения = Автор;
	Иначе
		ПользовательАвторСообщения = Пользователи.АвторизованныйПользователь();
	КонецЕсли;
	
	ДанныеСообщения = НовыйДанныеСообщения();
	ДанныеСообщения.Объект = Ссылка;
	ДанныеСообщения.Текст = ТекстСообщения;
	ДанныеСообщения.Дата = Дата;
	ДанныеСообщения.Автор = ПользовательАвторСообщения;
	ДанныеСообщения.Системный = Системный;
	Если ТипЗнч(Действия) = Тип("СписокЗначений") Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Действия, ДанныеСообщения.Действия);
	КонецЕсли;
	
	ЗаписатьВОчередьСистемыВзаимодействия(ДанныеСообщения);
	
КонецПроцедуры

// Устарела. Будет удалена в следующей версии программы.
//
Функция ПолучитьСсылкуИзНавигационной(НС) Экспорт
	
	Если СтрНайти(НС, "e1cib/list/") > 0 Тогда
		Возврат ПредопределенноеЗначение(Сред(НС, 12) + ".ПустаяСсылка").Метаданные().ПредставлениеСписка;
	КонецЕсли;
	
	ПерваяТочка = СтрНайти(НС, "e1cib/data/");
	ВтораяТочка = СтрНайти(НС, "?ref=");
	ТретьяТочка = СтрНайти(НС, "'>");
	
	ПредставлениеТипа   = Сред(НС, ПерваяТочка + 11, ВтораяТочка - ПерваяТочка - 11);
	ШаблонЗначения = ЗначениеВСтрокуВнутр(ПредопределенноеЗначение(ПредставлениеТипа + ".ПустаяСсылка"));
	ЗначениеСсылки = СтрЗаменить(ШаблонЗначения, "00000000000000000000000000000000", Сред(НС, ВтораяТочка + 5));
	Если ТретьяТочка> 0 Тогда
		КонецНС = Прав(НС, СтрДлина(НС) - ТретьяТочка+1);
		ЗначениеСсылки = СтрЗаменить(ЗначениеСсылки, КонецНС, "");
	КонецЕсли;
	Попытка
		Возврат ЗначениеИзСтрокиВнутр(ЗначениеСсылки);
	Исключение
		Возврат НСтр("ru = 'Ошибка преобразования: '")+НС;
	КонецПопытки; 
	
КонецФункции

#КонецОбласти

// Значения заполнения для сообщения обсуждений.
// 
// Возвращаемое значение:
//  Структура
//   * Объект    - ЛюбаяСсылка - Объект-владелец контекстного обсуждения.
//              - ИдентификаторОбсужденияСистемыВзаимодействия - Идентификатор обсуждения.
//   * Текст     - Строка - Текст сообщения.
//   * Дата      - Дата - Дата сообщения.
//   * Автор     - СправочникСсылка.Пользователи - Автор сообщения.
//   * Данные    - Произвольный - Данные, содержащиеся в сообщении.
//   * Действия  - СписокЗначений - 
//   * Системный - Булево - Определяет, надо ли оформить текст в виде системного.
//                          Например, "#Изменено: сумма документа".
// 
Функция НовыйДанныеСообщения() Экспорт
	
	ДанныеСообщения = Новый Структура;
	ДанныеСообщения.Вставить("Объект", Неопределено);
	ДанныеСообщения.Вставить("Текст", "");
	ДанныеСообщения.Вставить("Дата", Дата(1, 1, 1, 0, 0, 0));
	ДанныеСообщения.Вставить("Автор", Справочники.Пользователи.ПустаяСсылка());
	ДанныеСообщения.Вставить("Данные", Неопределено);
	ДанныеСообщения.Вставить("Действия", Новый СписокЗначений);
	ДанныеСообщения.Вставить("Получатель", Неопределено); // для совместимости.
	ДанныеСообщения.Вставить("Получатели", Новый Массив);
	ДанныеСообщения.Вставить("Системный", Ложь);
	
	Возврат ДанныеСообщения;
	
КонецФункции

// Производит попытку создать произвольное сообщение обсуждения. Если не удается - добавляет сообщение в очередь сообщений.
//
// Параметры:
//  ДанныеСообщения - Структура - См. функцию ОбсужденияУНФ.НовыйДанныеСообщения()
//
Процедура СоздатьСообщение(ДанныеСообщения) Экспорт
	
	Если ТипЗнч(ДанныеСообщения) <> Тип("Структура") Тогда
		ВызватьИсключение НСтр("ru='Передан некорректный параметр ""ДанныеСообщения"" (ожидаемый тип - Структура).'");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДанныеСообщения, НовыйДанныеСообщения(), Ложь);
	
	Если НЕ ЗначениеЗаполнено(ДанныеСообщения.Текст) Тогда
		ВызватьИсключение НСтр("ru='Сообщение с пустым текстом не может быть записано.'");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеСообщения.Автор) Тогда
		ДанныеСообщения.Автор = Пользователи.АвторизованныйПользователь();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеСообщения.Дата) Тогда
		ДанныеСообщения.Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ПопытатьсяСоздатьСообщениеСистемыВзаимодействия(ДанныеСообщения);
	
КонецПроцедуры

// Добавляет сообщение в очередь сообщений.
//
// Параметры:
//  ДанныеСообщения - Структура - См. функцию ОбсужденияУНФ.НовыйДанныеСообщения()
//
Процедура СоздатьСообщениеОтложенно(ДанныеСообщения) Экспорт
	
	Если ТипЗнч(ДанныеСообщения) <> Тип("Структура") Тогда
		ВызватьИсключение НСтр("ru='Передан некорректный параметр ""ДанныеСообщения"" (ожидаемый тип - Структура).'");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДанныеСообщения, НовыйДанныеСообщения(), Ложь);
	
	Если НЕ ЗначениеЗаполнено(ДанныеСообщения.Текст) Тогда
		ВызватьИсключение НСтр("ru='Сообщение с пустым текстом не может быть записано.'");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеСообщения.Автор) Тогда
		ДанныеСообщения.Автор = Пользователи.АвторизованныйПользователь();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеСообщения.Дата) Тогда
		ДанныеСообщения.Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ТипЗнч(ДанныеСообщения.Получатель) = Тип("СправочникСсылка.Сотрудники") И ЗначениеЗаполнено(ДанныеСообщения.Получатель) Тогда
		ДанныеСообщения.Получатель = РегистрыСведений.СотрудникиПользователя.ПолучитьПользователяПоСотруднику(ДанныеСообщения.Получатель);
	КонецЕсли;
	
	ДобавитьСообщениеВОчередьОтправки(ДанныеСообщения);
	
КонецПроцедуры

// Запускает запись отложенных сообщений из очереди непосредственно в систему взаимодействия.
//
Процедура ЗаписатьОтложенныеСообщенияВСистемуВзаимодействияСейчас() Экспорт
	
	Если Не ЭтоСеансАвторизованногоПользователя() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьОтложенныеСообщенияВСистемуВзаимодействия();
	
КонецПроцедуры

// Получает объект-владелец обсуждения системы взаимодействия по контексту обсуждения.
//
// Параметры:
//  ИдентификаторОбсуждения	 - ИдентификаторОбсужденияСистемыВзаимодействия
// 
// Возвращаемое значение:
//   - ЛюбаяСсылка
//
Функция РазыменоватьКонтекстОбсуждения(ИдентификаторОбсуждения) Экспорт
	
	Обсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(ИдентификаторОбсуждения);
	
	Если Обсуждение = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Обсуждение.КонтекстОбсуждения) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПолучитьСсылкуИзНавигационной(Обсуждение.КонтекстОбсуждения.НавигационнаяСсылка)
	
КонецФункции

Функция ПолучитьСообщение(ОбсуждениеИдентификатор, СообщениеИдентификатор) Экспорт
	
	Возврат ПолучитьСообщениеРекурсивно(ОбсуждениеИдентификатор, СообщениеИдентификатор, Неопределено);
	
КонецФункции

Функция ПолучитьОбсуждение(ОбсуждениеИдентификатор) Экспорт
	
	Если ТипЗнч(ОбсуждениеИдентификатор) = Тип("ИдентификаторОбсужденияСистемыВзаимодействия")
		ИЛИ СтрНайти(ОбсуждениеИдентификатор, "-") = 0 Тогда
		Возврат СистемаВзаимодействия.ПолучитьОбсуждение(ОбсуждениеИдентификатор);
	Иначе
		Возврат СистемаВзаимодействия.ПолучитьОбсуждение(
			Новый ИдентификаторОбсужденияСистемыВзаимодействия(ОбсуждениеИдентификатор));
	КонецЕсли;
	
КонецФункции

Функция ПолучитьОбсуждениеСПользователем(Пользователь1, Пользователь2) Экспорт
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("ОбсужденияУНФ.ПолучитьОбсуждениеСПользователем", "Пользователь1", Пользователь1, Тип("СправочникСсылка.Пользователи"));
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("ОбсужденияУНФ.ПолучитьОбсуждениеСПользователем", "Пользователь2", Пользователь2, Тип("СправочникСсылка.Пользователи"));
	
	ОтборОбсуждений = Новый ОтборОбсужденийСистемыВзаимодействия;
	ОтборОбсуждений.Групповое = Ложь;
	ОтборОбсуждений.Отображаемое = Истина;
	
	НайденноеОбсуждение = Неопределено;
	НайденныеОбсуждения = СистемаВзаимодействия.ПолучитьОбсуждения(ОтборОбсуждений);
	
	ИдентификаторПользователь1 = ИдентификаторПользователя(Пользователь1);
	ИдентификаторПользователь2 = ИдентификаторПользователя(Пользователь2);
	
	Для каждого Обсуждение Из НайденныеОбсуждения Цикл
		Если ЗначениеЗаполнено(Обсуждение.Заголовок)
			ИЛИ Обсуждение.Участники.Количество() <> 2 Тогда
			Продолжить;
		КонецЕсли;
		
		Если Обсуждение.Участники.Содержит(ИдентификаторПользователь1)
			И Обсуждение.Участники.Содержит(ИдентификаторПользователь2) Тогда
			НайденноеОбсуждение = Обсуждение;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НайденноеОбсуждение <> Неопределено Тогда
		Возврат НайденноеОбсуждение;
	КонецЕсли;
	
	НовоеЛичноеОбсуждение = СоздатьЛичноеОбсуждениеПользователей(ИдентификаторПользователь1, ИдентификаторПользователь2);
	
	Возврат НовоеЛичноеОбсуждение;
	
КонецФункции

// Получает контекстное обсуждение системы взаимодействия по навигационной ссылке.
//  В случае если такое обсуждение еще не создано - создает его.
//
// Параметры:
//  НавигационнаяСсылка - Строка - Навигационная ссылка на объект информационной базы.
//  Заголовок           - Строка - Заголовок обсуждения.
// 
// Возвращаемое значение:
//   - ОбсуждениеСистемыВзаимодействия
//
Функция ПолучитьКонтекстноеОбсуждениеПоНавигационнойСсылке(НавигационнаяСсылка, Заголовок = "") Экспорт
	
	Если Заголовок = "" Тогда
		Заголовок = НСтр("ru='История'");
	КонецЕсли;
	
	Обсуждение = Неопределено;
	
	#Если НЕ ВнешнееСоединение Тогда
	КонтекстОбсуждения = Новый КонтекстОбсужденияСистемыВзаимодействия(НавигационнаяСсылка);
	
	ОтборОбсуждений = Новый ОтборОбсужденийСистемыВзаимодействия();
	ОтборОбсуждений.КонтекстноеОбсуждение = Истина;
	ОтборОбсуждений.КонтекстОбсуждения = КонтекстОбсуждения;
	
	ОбсужденияПоОбъекту = СистемаВзаимодействия.ПолучитьОбсуждения(ОтборОбсуждений);
	
	Если ОбсужденияПоОбъекту.Количество() <> 0 Тогда
		Возврат ОбсужденияПоОбъекту[0];
	КонецЕсли;
	
	Обсуждение = СистемаВзаимодействия.СоздатьОбсуждение();
	Обсуждение.Заголовок = Заголовок;
	Обсуждение.КонтекстОбсуждения = КонтекстОбсуждения;
	
	Обсуждение.Записать();
	#КонецЕсли
	
	Возврат Обсуждение;
	
КонецФункции

// Получает идентификатор пользователя системы взаимодействия.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи
//  ВызыватьИсключение - Булево - Если передан идентификатор несуществующего пользователя системы взаимодействия, то будет сгенерировано исключение.
// 
// Возвращаемое значение:
//   - ИдентификаторПользователяСистемыВзаимодействия, Неопределено
//
Функция ИдентификаторПользователя(Пользователь, ВызыватьИсключение = Истина) Экспорт
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("ОбсужденияУНФ.ИдентификаторПользователя", "Пользователь", Пользователь, Тип("СправочникСсылка.Пользователи"));
	
	Если НЕ ЗначениеЗаполнено(Пользователь) Тогда
		ВызватьИсключение НСтр("ru='Не заполнено значение параметра ""Пользователь""'");
	КонецЕсли;
	
	ИдентификаторПользователяСВ = Неопределено;
	
	Попытка
		ПредставлениеПользоватедя = Строка(Пользователь);
		ИдентификаторПользователяИБ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователь, "ИдентификаторПользователяИБ");
		ИдентификаторПользователяСВ = СистемаВзаимодействия.ПолучитьИдентификаторПользователя(ИдентификаторПользователяИБ);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Комментарий = НСтр("ru='Ошибка получения идентификатора пользователя СВ для пользователя ""%1"", идентификатор ИБ ""%2"".'");
		Комментарий = СтрШаблон(Комментарий, ПредставлениеПользоватедя, ИдентификаторПользователяИБ);
		ЗаписьЖурналаРегистрации(ИмяСобытияЖР(), УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
		
		Если ВызыватьИсключение Тогда
			ВызватьИсключение;
		КонецЕсли;
		
		ПользовательСистемыВзаимодействияСуществует = ПользовательСистемыВзаимодействияСуществует(ИнформацияОбОшибке);
		Если ПользовательСистемыВзаимодействияСуществует <> Ложь Тогда
			// Вызвать исключение в любом случае, если произошла ошибка не связанная с отсутствием пользователя системы взаимодействия.
			ВызватьИсключение;
		КонецЕсли;
	КонецПопытки;
	
	Возврат ИдентификаторПользователяСВ;
	
КонецФункции

Функция ПользовательСистемыВзаимодействияСуществует(ИнформацияОбОшибке)
	
	// В текущем АПИ нет возможности определить существование пользователя СВ по идентификатору пользователя ИБ.
	// Если пользователь не существует, вызов метода СистемаВзаимодействия.ПолучитьИдентификаторПользователя возвращает исключение.
	// В некоторых случаях необходимо понимать что пользователь 
	
	КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткоеПредставлениеОшибки = НРег(КраткоеПредставлениеОшибки);
	ПризнакПользовательНеСуществует = НСтр("ru='Объект системы взаимодействия не найден'");
	ПризнакПользовательНеСуществует = НРег(ПризнакПользовательНеСуществует);
	ПризнакПользовательНеСуществуетНайден = СтрНайти(КраткоеПредставлениеОшибки, ПризнакПользовательНеСуществует) <> 0;
	
	Если ПризнакПользовательНеСуществуетНайден Тогда
		Возврат Ложь;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Получает контекстное обсуждение системы взаимодействия по указанному объекту.
// В случае если такое обсуждение еще не создано - создает его.
//
// Параметры:
//  ОбъектСсылка - ЛюбаяСсылка
// 
// Возвращаемое значение:
//   - ОбсуждениеСистемыВзаимодействия
//
Функция ПолучитьКонтекстноеОбсуждение(ОбъектСсылка) Экспорт
	
	Обсуждение = Неопределено;
	
	#Если НЕ ВнешнееСоединение Тогда
	КонтекстОбсуждения = Новый КонтекстОбсужденияСистемыВзаимодействия(ПолучитьНавигационнуюСсылку(ОбъектСсылка));
	
	ОтборОбсуждений = Новый ОтборОбсужденийСистемыВзаимодействия();
	ОтборОбсуждений.КонтекстноеОбсуждение = Истина;
	ОтборОбсуждений.КонтекстОбсуждения = КонтекстОбсуждения;
	
	ОбсужденияПоОбъекту = СистемаВзаимодействия.ПолучитьОбсуждения(ОтборОбсуждений);
	
	Если ОбсужденияПоОбъекту.Количество() <> 0 Тогда
		Возврат ОбсужденияПоОбъекту[0];
	КонецЕсли;
	
	Обсуждение = СистемаВзаимодействия.СоздатьОбсуждение();
	Обсуждение.Заголовок = СтрШаблон(НСтр("ru = 'История %1'"), ОбъектСсылка);
	Обсуждение.КонтекстОбсуждения = КонтекстОбсуждения;
	
	Обсуждение.Записать();
	#КонецЕсли
	
	Возврат Обсуждение;
	
КонецФункции

// Находит последнее сообщение конкретного пользователя в указанном обсуждении.
//
// Параметры:
//  ОбсуждениеИдентификатор	 - ОбсуждениеСистемыВзаимодействия
//  Пользователь			 - ИдентификаторПользователяСистемыВзаимодействия, ПользовательСистемыВзаимодействия, СправочникСсылка.Пользователи
//  После					 - ИдентификаторСообщенияСистемыВзаимодействия - Если параметр передан, возвращаются сообщения после указанного.
// 
// Возвращаемое значение:
//   - СообщениеСистемыВзаимодействия, Неопределено
//
Функция ПолучитьПоследнееСообщение(ОбсуждениеИдентификатор, Пользователь = Неопределено, После = Неопределено) Экспорт
	#Если НЕ ВнешнееСоединение Тогда
	Если ЗначениеЗаполнено(Пользователь) Тогда
		ТипыПользователя = Новый Массив;
		ТипыПользователя.Добавить(Тип("ИдентификаторПользователяСистемыВзаимодействия"));
		ТипыПользователя.Добавить(Тип("ПользовательСистемыВзаимодействия"));
		ТипыПользователя.Добавить(Тип("СправочникСсылка.Пользователи"));
		
		ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("ПолучитьПоследнееСообщение", "Пользователь", Пользователь, ТипыПользователя);
	КонецЕсли;
	
	Отбор = Новый ОтборСообщенийСистемыВзаимодействия;
	Отбор.Обсуждение = ОбсуждениеИдентификатор;
	Отбор.Количество = 1;
	Если После <> Неопределено Тогда
		Отбор.После = После;
	КонецЕсли;
	Отбор.НаправлениеСортировки = НаправлениеСортировки.Убыв;
	
	Сообщения = СистемаВзаимодействия.ПолучитьСообщения(Отбор);
	Если Сообщения.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Пользователь = Неопределено Тогда
		Возврат Сообщения[0];
	КонецЕсли;
	
	Если ТипЗнч(Пользователь) = Тип("СправочникСсылка.Пользователи") Тогда
		Автор = ИдентификаторПользователя(Пользователь);
	ИначеЕсли ТипЗнч(Пользователь) = Тип("ИдентификаторПользователяСистемыВзаимодействия") Тогда
		Автор = Пользователь;
	ИначеЕсли ТипЗнч(Пользователь) = Тип("ПользовательСистемыВзаимодействия") Тогда
		Автор = Пользователь.Идентификатор;
	КонецЕсли;
	
	Если Сообщения[0].Автор = Автор Тогда
		Возврат Сообщения[0];
	КонецЕсли;
	#КонецЕсли
	
	Возврат Неопределено;
	
КонецФункции

Процедура ДобавитьПользователяВОбсуждение(Обсуждение, Пользователь) Экспорт
	
	Если ТипЗнч(Обсуждение) = Тип("ОбсуждениеСистемыВзаимодействия") Тогда
		РедактируемоеОбсуждение = Обсуждение;
	Иначе
		РедактируемоеОбсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(Обсуждение);
	КонецЕсли;
	
	Если ТипЗнч(Пользователь) = Тип("СправочникСсылка.Пользователи") Тогда
		НовыйУчастникОбсуждения = Обсуждения.ПользовательСистемыВзаимодействия(Пользователь).Идентификатор;
	Иначе
		НовыйУчастникОбсуждения = Пользователь;
	КонецЕсли;
	
	Если РедактируемоеОбсуждение.Участники.Содержит(НовыйУчастникОбсуждения) Тогда
		Возврат;
	КонецЕсли;
	
	РедактируемоеОбсуждение.Участники.Добавить(НовыйУчастникОбсуждения);
	РедактируемоеОбсуждение.Записать();
	
КонецПроцедуры

Процедура УдалитьПользователяИзОбсуждения(Обсуждение, Пользователь) Экспорт
	
	Если ТипЗнч(Обсуждение) = Тип("ОбсуждениеСистемыВзаимодействия") Тогда
		РедактируемоеОбсуждение = Обсуждение;
	Иначе
		РедактируемоеОбсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(Обсуждение);
	КонецЕсли;
	
	Если ТипЗнч(Пользователь) = Тип("СправочникСсылка.Пользователи") Тогда
		УдаляемыйУчастникОбсуждения = Обсуждения.ПользовательСистемыВзаимодействия(Пользователь).Идентификатор;
	Иначе
		УдаляемыйУчастникОбсуждения = Пользователь;
	КонецЕсли;
	
	Если НЕ РедактируемоеОбсуждение.Участники.Содержит(УдаляемыйУчастникОбсуждения) Тогда
		Возврат;
	КонецЕсли;
	
	РедактируемоеОбсуждение.Участники.Удалить(УдаляемыйУчастникОбсуждения);
	РедактируемоеОбсуждение.Записать();
	
КонецПроцедуры

// Получает обсуждение по его идентификатору, исключение при неудачной попытке не вызывается.
// Обертка для платформенного метода СистемаВзаимодействия.ПолучитьОбсуждение.
//
// Параметры:
//  ОбсуждениеИдентификатор - ИдентификаторОбсужденияСистемыВзаимодействия, Строка - Идентификатор обсуждения или ключ обсуждения.
// 
// Возвращаемое значение:
//  ОбсуждениеСистемыВзаимодействия, Неопределено
//
Функция ПолучитьОбсуждениеБезопасно(Идентификатор, ВызыватьИсключение = Ложь) Экспорт
	
	Если Не СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОжидаемыеТипы = Новый Массив;
	ОжидаемыеТипы.Добавить(Тип("Строка"));
	ОжидаемыеТипы.Добавить(Тип("ИдентификаторОбсужденияСистемыВзаимодействия"));
	ТипПараметраИдентификатор = Новый ОписаниеТипов(ОжидаемыеТипы);
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр("ПолучитьОбсуждениеБезопасно", "Идентификатор", Идентификатор, ТипПараметраИдентификатор);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Обсуждение = Неопределено;
	
	Если ТипЗнч(Идентификатор) = Тип("Строка") Тогда
		Обсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(Идентификатор);
	КонецЕсли;
	
	Если ТипЗнч(Идентификатор) = Тип("ИдентификаторОбсужденияСистемыВзаимодействия") Тогда
		Попытка
			ПредставлениеИдентификатора = Строка(Идентификатор);
			Обсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(Идентификатор);
		Исключение
			Обсуждение = Неопределено;
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Комментарий = НСтр("ru='Ошибка получения обсуждения СВ по идентификатору ""%1"" по причине:
			|%2'");
			Комментарий = СтрШаблон(Комментарий, ПредставлениеИдентификатора, ТекстОшибки);
			ЗаписьЖурналаРегистрации(ИмяСобытияЖР(), УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
			
			Если ВызыватьИсключение Тогда
				ВызватьИсключение;
			КонецЕсли;
		КонецПопытки;
	КонецЕсли;
	
	Возврат Обсуждение;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ВыполнитьПереходНаЗаданиеЗаписиОтложенныхСообщенийВСистемуВзаимодействия() Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.УдалитьЗаписьСообщенийВСистемуВзаимодействия);
	Отбор.Вставить("Использование", Истина);
	
	СписокЗаданий = РегламентныеЗадания.ПолучитьРегламентныеЗадания(Отбор);
	
	ДобавитьЗадание = Истина;
	Для Каждого ТекущееЗадание Из СписокЗаданий Цикл
		Если ДобавитьЗадание Тогда
			Пользователь = ?(ЗначениеЗаполнено(ТекущееЗадание.ИмяПользователя), Пользователи.НайтиПоИмени(ТекущееЗадание.ИмяПользователя), Неопределено);
			ДобавитьЗаданиеЗаписиОтложенныхСообщений(ТекущееЗадание.Использование, ТекущееЗадание.Расписание, Пользователь);
			ДобавитьЗадание = Ложь;
		КонецЕсли;
		
		ТекущееЗадание.Удалить();
	КонецЦикла;
	
	Отбор.Вставить("Использование", Ложь);
	
	СписокЗаданий = РегламентныеЗадания.ПолучитьРегламентныеЗадания(Отбор);
	
	Для Каждого ТекущееЗадание Из СписокЗаданий Цикл
		Если ДобавитьЗадание Тогда
			Пользователь = ?(ЗначениеЗаполнено(ТекущееЗадание.ИмяПользователя), Пользователи.НайтиПоИмени(ТекущееЗадание.ИмяПользователя), Неопределено);
			ДобавитьЗаданиеЗаписиОтложенныхСообщений(ТекущееЗадание.Использование, ТекущееЗадание.Расписание, Пользователь);
			ДобавитьЗадание = Ложь;
		КонецЕсли;
		
		ТекущееЗадание.Удалить();
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьЗаданияЗаписиОтложенныхСообщенийВСистемуВзаимодействия(ПараметрыОбработчика) Экспорт
	
	Отбор = Новый Структура;
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Отбор.Вставить("ИмяМетода", МетаданныеЗаданияЗаписиОтложенныхСообщений().ИмяМетода);
	Иначе
		Отбор.Вставить("Метаданные", МетаданныеЗаданияЗаписиОтложенныхСообщений());
	КонецЕсли;
	
	СписокЗаданий = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	
	АктивноеЗадание = Неопределено;
	ЗаданияДляУдаления = Новый Массив;
	
	Для Каждого Задание Из СписокЗаданий Цикл
		Если Задание.Использование И АктивноеЗадание = Неопределено Тогда
			АктивноеЗадание = Задание.УникальныйИдентификатор;
		Иначе
			ЗаданияДляУдаления.Добавить(Задание.УникальныйИдентификатор);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ИдентификаторЗадания Из ЗаданияДляУдаления Цикл
		РегламентныеЗаданияСервер.УдалитьЗадание(ИдентификаторЗадания);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(АктивноеЗадание) Тогда
		ПараметрыЗадания = Новый Структура;
		ПараметрыЗадания.Вставить("Расписание", РасписаниеРегламентногоЗаданияПоУмолчанию());
		
		РегламентныеЗаданияСервер.ИзменитьЗадание(АктивноеЗадание, ПараметрыЗадания);
	Иначе
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьНеотправленные
		|ИЗ
		|	Справочник.НеотправленныеСообщенияСистемыВзаимодействия КАК НеотправленныеСообщенияСистемыВзаимодействия");
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			ДобавитьЗаданиеЗаписиОтложенныхСообщений(Истина, РасписаниеРегламентногоЗаданияПоУмолчанию());
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыОбработчика.ОбработкаЗавершена = Истина;
	
КонецПроцедуры

Процедура ПопытатьсяСоздатьСообщениеСистемыВзаимодействия(ДанныеСообщения)
	
	#Если НЕ ВнешнееСоединение Тогда
	
	УстановитьПривилегированныйРежим(Истина);
	
	СообщениеОтправлено = СоздатьИОтправитьСообщениеСистемыВзаимодействия(ДанныеСообщения);
	Если СообщениеОтправлено <> Истина Тогда
		ДобавитьСообщениеВОчередьОтправки(ДанныеСообщения);
	КонецЕсли;
	
	#КонецЕсли

КонецПроцедуры

Процедура ЗаписатьОтложенныеСообщенияВСистемуВзаимодействия(ОбсуждениеКонтекст = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИнформационнаяБазаЗарегистрирована = СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована();
	Если ИнформационнаяБазаЗарегистрирована = Ложь
		Или ИнформационнаяБазаЗарегистрирована = Неопределено Тогда
		ИспользованиеРеглЗаданияЗаписьСообщенийВСистемуВзаимодействия(Ложь);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НеотправленныеСообщенияСистемыВзаимодействия.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НеотправленныеСообщенияСистемыВзаимодействия КАК НеотправленныеСообщенияСистемыВзаимодействия
	|ГДЕ
	|	(&НеВыполнятьОтборПоОбъекту
	|			ИЛИ НеотправленныеСообщенияСистемыВзаимодействия.ОбсуждениеКонтекст = &ОбсуждениеКонтекст)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НеотправленныеСообщенияСистемыВзаимодействия.Дата";
	
	Запрос.УстановитьПараметр("НеВыполнятьОтборПоОбъекту", ОбсуждениеКонтекст = Неопределено);
	Запрос.УстановитьПараметр("ОбсуждениеКонтекст", ОбсуждениеКонтекст);
	
	НеотправленныеСообщения = Запрос.Выполнить().Выбрать();
	Пока НеотправленныеСообщения.Следующий() Цикл
		НеотправленноеСообщениеПолучено = Ложь;
		// АПК:280-выкл обработка исключения не требуется
		Попытка
			НеотправленноеСообщение = НеотправленныеСообщения.Ссылка.ПолучитьОбъект();
			НеотправленноеСообщение.Заблокировать();
			НеотправленноеСообщениеПолучено = Истина;
		Исключение
			// Обработка исключения не требуется.
			// Сообщение очереди уже было отправлено другим сеансом.
		КонецПопытки;
		// АПК:280-вкл
		
		Если Не НеотправленноеСообщениеПолучено Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка
			НужноПопытатьсяОтправитьСообщение = НеотправленноеСообщение.ОбъектыСообщенияСуществуют();
			Если НужноПопытатьсяОтправитьСообщение Тогда
				ДанныеСообщения = ДанныеНеотправленногоСообщения(НеотправленноеСообщение);
				СообщениеОтправлено = СоздатьИОтправитьСообщениеСистемыВзаимодействия(ДанныеСообщения, Ложь);
			КонецЕсли;
			Если СообщениеОтправлено = Неопределено
				Или СообщениеОтправлено = Истина
				Или Не НужноПопытатьсяОтправитьСообщение Тогда
				НеотправленноеСообщение.Удалить();
			КонецЕсли;
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
			ЗаписьЖурналаРегистрации(ИмяСобытияЖР(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Функция ЛогироватьИзмененияОбъектовВОбсуждении() Экспорт
	
	ЗаписьИзмененийОбъектовВОбсуждения = ЗаписьИзмененийОбъектовВОбсуждения();
	
	Если ЗаписьИзмененийОбъектовВОбсуждения = Перечисления.ЗаписьИзмененийОбъектовВОбсуждения.Записывать Тогда
		Возврат Истина;
	ИначеЕсли ЗаписьИзмененийОбъектовВОбсуждения = Перечисления.ЗаписьИзмененийОбъектовВОбсуждения.НеЗаписывать Тогда
		Возврат Ложь;
	Иначе
		ИнформационнаяБазаЗарегистрирована = Неопределено;
		#Если НЕ ВнешнееСоединение Тогда
			ИнформационнаяБазаЗарегистрирована = СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована();
		#КонецЕсли
		Возврат ИнформационнаяБазаЗарегистрирована = Истина;
	КонецЕсли;
	
КонецФункции

Функция ЗаписьИзмененийОбъектовВОбсуждения()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаписьИзмененийОбъектовВОбсуждения.Значение КАК Значение
	|ИЗ
	|	Константа.ЗаписьИзмененийОбъектовВОбсуждения КАК ЗаписьИзмененийОбъектовВОбсуждения";
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Перечисления.ЗаписьИзмененийОбъектовВОбсуждения.Авто;
	КонецЕсли;
	
	ЗаписьИзменений = Результат.Выбрать();
	ЗаписьИзменений.Следующий();
	
	Если ЗначениеЗаполнено(ЗаписьИзменений.Значение) Тогда
		Возврат ЗаписьИзменений.Значение;
	Иначе
		Возврат Перечисления.ЗаписьИзмененийОбъектовВОбсуждения.Авто;
	КонецЕсли;
	
КонецФункции

// Обработчик регламентного задания ЗаписьСообщенийВСистемуВзаимодействия.
//
Процедура ЗаписьСообщенийВСистемуВзаимодействия() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.УдалитьЗаписьСообщенийВСистемуВзаимодействия);
	
	Если Не ЭтоСеансАвторизованногоПользователя() Тогда
		ИспользованиеРеглЗаданияЗаписьСообщенийВСистемуВзаимодействия(Ложь);
		
		Комментарий = НСтр("ru='Задание запущено не от авторизованного пользователя, пропустить выполнение.'");
		ЗаписьЖурналаРегистрации(ИмяСобытияЖР(), УровеньЖурналаРегистрации.Предупреждение,,, Комментарий);
		Возврат;
	КонецЕсли;
	
	АвторизованныйПользователь = Пользователи.АвторизованныйПользователь();
	Если Не ПользовательПодходитДляОтправкиПриемаСообщения(АвторизованныйПользователь) Тогда
		ИспользованиеРеглЗаданияЗаписьСообщенийВСистемуВзаимодействия(Ложь);
		
		Комментарий = НСтр("ru='Пользователь не подходит для регламентной отправки сообщений обсуждений.'");
		ЗаписьЖурналаРегистрации(ИмяСобытияЖР(), УровеньЖурналаРегистрации.Предупреждение,,, Комментарий);
		Возврат;
	КонецЕсли;
	
	ЗаписатьОтложенныеСообщенияВСистемуВзаимодействия();
	
КонецПроцедуры

// Обработчик регламентного задания ЗаписьОтложенныхСообщенийВСистемуВзаимодействия.
//
Процедура ЗаписьОтложенныхСообщенийВСистемуВзаимодействия(ПользовательДляОтправкиПриема = Неопределено) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(МетаданныеЗаданияЗаписиОтложенныхСообщений());
	
	Если Не ПроверитьУстановитьПользователяДляОтправкиПриемаСообщений(ПользовательДляОтправкиПриема) Тогда
		ИспользованиеРеглЗаданияЗаписьСообщенийВСистемуВзаимодействия(Ложь);
		
		Комментарий = НСтр("ru = 'Пользователь не подходит для регламентной отправки сообщений обсуждений.'");
		ЗаписьЖурналаРегистрации(ИмяСобытияЖР(), УровеньЖурналаРегистрации.Предупреждение, , , Комментарий);
		Возврат;
	КонецЕсли;
	
	ЗаписатьОтложенныеСообщенияВСистемуВзаимодействия();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПользовательИнформационнойБазы(ПользовательСистемыВзаимодействия)
	
	ПользовательСВ = СистемаВзаимодействия.ПолучитьПользователя(ПользовательСистемыВзаимодействия);
	Пользователь = Справочники.Пользователи.НайтиПоРеквизиту("ИдентификаторПользователяИБ", ПользовательСВ.ИдентификаторПользователяИнформационнойБазы);
	Возврат Пользователь;
	
КонецФункции

Процедура ОформитьСистемныйТекст(Текст)
	
	СтандартныйНаклонныйШрифт = Новый Шрифт(,,,Истина);
	Текст = СтрШаблон(НСтр("ru='#%1'"), Текст);
	Текст = Новый ФорматированнаяСтрока(Текст, СтандартныйНаклонныйШрифт, ЦветаСтиля.ЦветСистемногоСообщения);
	
КонецПроцедуры

Процедура ДобавитьСообщениеВОчередьОтправки(ДанныеСообщения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДанныеСообщения, НовыйДанныеСообщения(), Ложь);
	
	НеотправленноеСообщение = Справочники.НеотправленныеСообщенияСистемыВзаимодействия.СоздатьЭлемент();
	Если ТипЗнч(ДанныеСообщения.Объект) = Тип("Строка") Тогда
		НеотправленноеСообщение.ОбсуждениеИдентификатор = ДанныеСообщения.Объект;
	ИначеЕсли ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ДанныеСообщения.Объект)) Тогда
		НеотправленноеСообщение.ОбсуждениеКонтекст = ДанныеСообщения.Объект;
	ИначеЕсли ТипЗнч(ДанныеСообщения.Объект) = Тип("ИдентификаторОбсужденияСистемыВзаимодействия") Тогда
		НеотправленноеСообщение.ОбсуждениеИдентификатор = ДанныеСообщения.Объект;
	КонецЕсли;
	НеотправленноеСообщение.Автор = ДанныеСообщения.Автор;
	НеотправленноеСообщение.Текст = ДанныеСообщения.Текст;
	НеотправленноеСообщение.Дата = ?(ЗначениеЗаполнено(ДанныеСообщения.Дата), ДанныеСообщения.Дата, ТекущаяДатаСеанса());
	Если ТипЗнч(ДанныеСообщения.Данные) <> Неопределено Тогда
		НеотправленноеСообщение.Данные = Новый ХранилищеЗначения(ДанныеСообщения.Данные);
	КонецЕсли;
	Если ТипЗнч(ДанныеСообщения.Действия) = Тип("СписокЗначений") И ДанныеСообщения.Действия.Количество() > 0 Тогда
		НеотправленноеСообщение.Действия = Новый ХранилищеЗначения(ДанныеСообщения.Действия);
	КонецЕсли;
	
	Если ДанныеСообщения.Системный Тогда
		НеотправленноеСообщение.Системный = Истина;
	КонецЕсли;
	
	Для каждого Получатель Из ДанныеСообщения.Получатели Цикл
		Если ЗначениеЗаполнено(Получатель) Тогда
			ПолучательСообщения = НеотправленноеСообщение.Получатели.Добавить();
			ПолучательСообщения.Получатель = Получатель;
		КонецЕсли;
	КонецЦикла;
	Если ЗначениеЗаполнено(ДанныеСообщения.Получатель) Тогда
		ПолучательСообщения = НеотправленноеСообщение.Получатели.Добавить();
		ПолучательСообщения.Получатель = ДанныеСообщения.Получатель;
	КонецЕсли;
	
	НеотправленноеСообщение.Записать();
	ИспользованиеРеглЗаданияЗаписьСообщенийВСистемуВзаимодействия(Истина);
	
КонецПроцедуры

Процедура ЗаписатьВОчередьСистемыВзаимодействия(ДанныеСообщения)
	
	Если НЕ ЗначениеЗаполнено(ДанныеСообщения.Автор) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеСообщения.Автор) = Тип("СправочникСсылка.Пользователи")
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеСообщения.Автор, "Служебный") Тогда
		// Нельзя записать сообщение от имени служебного пользователя
		Возврат;
	КонецЕсли;
	
	ДобавитьСообщениеВОчередьОтправки(ДанныеСообщения);
	
КонецПроцедуры

Процедура ИспользованиеРеглЗаданияЗаписьСообщенийВСистемуВзаимодействия(Использование)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Отбор = Новый Структура;
	Отбор.Вставить("Использование", Истина);
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Отбор.Вставить("ИмяМетода", МетаданныеЗаданияЗаписиОтложенныхСообщений().ИмяМетода);
	Иначе
		Отбор.Вставить("Метаданные", МетаданныеЗаданияЗаписиОтложенныхСообщений());
	КонецЕсли;
	
	СписокЗаданий = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	
	АктивноеЗадание = Неопределено;
	ЗаданияДляОтключения = Новый Массив;
	
	Для Каждого Задание Из СписокЗаданий Цикл
		Если АктивноеЗадание = Неопределено Тогда
			АктивноеЗадание = Задание.УникальныйИдентификатор;
		Иначе
			ЗаданияДляОтключения.Добавить(Задание.УникальныйИдентификатор);
		КонецЕсли;
	КонецЦикла;
	
	ЗаданиеИспользуется = ЗначениеЗаполнено(АктивноеЗадание);
	
	Для Каждого ИдентификаторЗадания Из ЗаданияДляОтключения Цикл
		РегламентныеЗаданияСервер.ИзменитьЗадание(ИдентификаторЗадания, Новый Структура("Использование", Ложь));
	КонецЦикла;
	
	Если Использование И ЗаданиеИспользуется Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Использование И Не ЗаданиеИспользуется Тогда
		Возврат;
	КонецЕсли;
	
	Если Использование Тогда
		Отбор.Использование = Ложь;
		СписокЗаданий = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
		
		Если СписокЗаданий.Количество() > 0 Тогда
			Параметры = Новый Структура;
			Параметры.Вставить("Использование", Истина);
			Если ЭтоСеансАвторизованногоПользователя() Тогда
				Параметры.Вставить("Параметры", Новый Массив);
				Параметры.Параметры.Добавить(Пользователи.АвторизованныйПользователь());
			КонецЕсли;
			
			РегламентныеЗаданияСервер.ИзменитьЗадание(СписокЗаданий[0].УникальныйИдентификатор, Параметры);
		Иначе
			Пользователь = ?(ЭтоСеансАвторизованногоПользователя(), Пользователи.АвторизованныйПользователь(), Неопределено);
			ДобавитьЗаданиеЗаписиОтложенныхСообщений(Истина, РасписаниеРегламентногоЗаданияПоУмолчанию(), Пользователь);
		КонецЕсли;
	Иначе
		РегламентныеЗаданияСервер.ИзменитьЗадание(АктивноеЗадание, Новый Структура("Использование", Ложь));
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьЗаданиеЗаписиОтложенныхСообщений(Использование, Расписание, Пользователь = Неопределено)
	
	ПараметрыДобавления = Новый Структура;
	ПараметрыДобавления.Вставить("Метаданные", МетаданныеЗаданияЗаписиОтложенныхСообщений());
	ПараметрыДобавления.Вставить("Использование", Использование);
	ПараметрыДобавления.Вставить("Расписание", Расписание);
	
	Если ЗначениеЗаполнено(Пользователь) Тогда
		ПараметрыДобавления.Вставить("Параметры", Новый Массив);
		ПараметрыДобавления.Параметры.Добавить(Пользователь);
	КонецЕсли;
	
	РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыДобавления);
	
КонецПроцедуры

Функция РасписаниеРегламентногоЗаданияПоУмолчанию()
	
	Месяцы = Новый Массив;
	Месяцы.Добавить(1);
	Месяцы.Добавить(2);
	Месяцы.Добавить(3);
	Месяцы.Добавить(4);
	Месяцы.Добавить(5);
	Месяцы.Добавить(6);
	Месяцы.Добавить(7);
	Месяцы.Добавить(8);
	Месяцы.Добавить(9);
	Месяцы.Добавить(10);
	Месяцы.Добавить(11);
	Месяцы.Добавить(12);
	
	ДниНедели = Новый Массив;
	ДниНедели.Добавить(1);
	ДниНедели.Добавить(2);
	ДниНедели.Добавить(3);
	ДниНедели.Добавить(4);
	ДниНедели.Добавить(5);
	ДниНедели.Добавить(6);
	ДниНедели.Добавить(7);
	
	Расписание							= Новый РасписаниеРегламентногоЗадания;
	Расписание.ДниНедели				= ДниНедели;
	Расписание.ПериодПовтораВТечениеДня	= 300; // 5 минут
	Расписание.ПериодПовтораДней		= 1; // каждый день
	Расписание.Месяцы					= Месяцы;
	
	Возврат Расписание;
	
КонецФункции

Функция СоздатьЛичноеОбсуждениеПользователей(ИдентификаторПользователь1, ИдентификаторПользователь2)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НовоеОбсуждение = СистемаВзаимодействия.СоздатьОбсуждение();
	НовоеОбсуждение.Групповое = Ложь;
	НовоеОбсуждение.Участники.Добавить(ИдентификаторПользователь1);
	НовоеОбсуждение.Участники.Добавить(ИдентификаторПользователь2);
	НовоеОбсуждение.Записать();
	Возврат НовоеОбсуждение;
	
КонецФункции

Функция ПолучитьСообщениеРекурсивно(ОбсуждениеИдентификатор, СообщениеИдентификатор, СообщениеИдентификаторПосле = Неопределено)
	
	#Если НЕ ВнешнееСоединение Тогда
	Отбор = Новый ОтборСообщенийСистемыВзаимодействия;
	Отбор.Обсуждение = ОбсуждениеИдентификатор;
	Если ЗначениеЗаполнено(СообщениеИдентификаторПосле) Тогда
		Отбор.После = СообщениеИдентификатор;
	КонецЕсли;
	
	Сообщения = СистемаВзаимодействия.ПолучитьСообщения(Отбор);
	
	Если Сообщения.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru='Сообщение не найдено.'");
	КонецЕсли;
	
	Для каждого Сообщение Из Сообщения Цикл
		Если Сообщение.Идентификатор = СообщениеИдентификатор Тогда
			Возврат Сообщение;
		КонецЕсли;
	КонецЦикла;
	
	ПолучитьСообщениеРекурсивно(ОбсуждениеИдентификатор, СообщениеИдентификатор, Сообщения[Сообщения.ВГраница()]);
	#КонецЕсли
	
КонецФункции

Функция ДанныеНеотправленногоСообщения(НеотправленноеСообщениеОбъект)
	
	ДанныеСообщения = НовыйДанныеСообщения();
	Если ЗначениеЗаполнено(НеотправленноеСообщениеОбъект.ОбсуждениеИдентификатор) Тогда
		ДанныеСообщения.Объект = НеотправленноеСообщениеОбъект.ОбсуждениеИдентификатор;
	Иначе
		ДанныеСообщения.Объект = НеотправленноеСообщениеОбъект.ОбсуждениеКонтекст;
	КонецЕсли;
	ДанныеСообщения.Текст = НеотправленноеСообщениеОбъект.Текст;
	ДанныеСообщения.Дата = НеотправленноеСообщениеОбъект.Дата;
	ДанныеСообщения.Автор = НеотправленноеСообщениеОбъект.Автор;
	ДанныеСообщения.Данные = НеотправленноеСообщениеОбъект.Данные;
	ДанныеСообщения.Действия = НеотправленноеСообщениеОбъект.Действия;
	Для каждого Получатель Из НеотправленноеСообщениеОбъект.Получатели Цикл
		ДанныеСообщения.Получатели.Добавить(Получатель.Получатель);
	КонецЦикла;
	ДанныеСообщения.Системный = НеотправленноеСообщениеОбъект.Системный;
	
	Возврат ДанныеСообщения;
	
КонецФункции

Функция НовыйСообщениеСистемыВзаимодействия(ДанныеСообщения, ВызыватьИсключение)
	
	Если ТипЗнч(ДанныеСообщения.Объект) = Тип("Строка") Тогда
		Обсуждение = ПолучитьОбсуждение(ДанныеСообщения.Объект);
		Если Обсуждение = Неопределено Тогда
			// Общее Обсуждение (по ключу) может отсутствовать:
			//  - либо еще не создано прикладной логикой,
			//  - либо ранее было выполнено отключение/подключение обсуждений (происходит отправка старых сообщений).
			// В таком случае сообщение некуда отправить, т.к. обсуждение должна создать ответственная за него прикладная логика.
			Возврат Неопределено;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДанныеСообщения.Объект) = Тип("ИдентификаторОбсужденияСистемыВзаимодействия") Тогда
		Обсуждение = ПолучитьОбсуждение(ДанныеСообщения.Объект);
	ИначеЕсли ТипЗнч(ДанныеСообщения.Объект) = Тип("СправочникСсылка.Пользователи") Тогда
		Обсуждение = ПолучитьОбсуждениеСПользователем(ДанныеСообщения.Объект, ДанныеСообщения.Автор);
	Иначе
		Обсуждение = ПолучитьКонтекстноеОбсуждение(ДанныеСообщения.Объект);
	КонецЕсли;
	
	НовоеСообщение = СистемаВзаимодействия.СоздатьСообщение(Обсуждение.Идентификатор);
	НовоеСообщение.Текст = ДанныеСообщения.Текст;
	Если ДанныеСообщения.Системный Тогда
		ОформитьСистемныйТекст(НовоеСообщение.Текст);
	КонецЕсли;
	НовоеСообщение.Дата = ДанныеСообщения.Дата;
	
	Если ПользовательПодходитДляОтправкиПриемаСообщения(ДанныеСообщения.Автор) Тогда
		ИдентификаторПользователяСВ = ИдентификаторПользователя(ДанныеСообщения.Автор, ВызыватьИсключение);
		Если ИдентификаторПользователяСВ <> Неопределено Тогда
			НовоеСообщение.Автор = ИдентификаторПользователяСВ;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеСообщения.Получатель) И ДанныеСообщения.Получатели.Количество() = 0 Тогда
		ДанныеСообщения.Получатели.Добавить(ДанныеСообщения.Получатель); // Для совместимости.
	КонецЕсли;
	
	Для каждого ПользовательСправочника Из ДанныеСообщения.Получатели Цикл
		Если Не ПользовательПодходитДляОтправкиПриемаСообщения(ПользовательСправочника) Тогда
			Продолжить;
		КонецЕсли;
		ИдентификаторПользователяСВ = ИдентификаторПользователя(ПользовательСправочника, ВызыватьИсключение);
		Если ИдентификаторПользователяСВ <> Неопределено Тогда
			НовоеСообщение.Получатели.Добавить(ИдентификаторПользователяСВ);
		КонецЕсли;
	КонецЦикла;
	
	Если ДанныеСообщения.Данные <> Неопределено Тогда
		НовоеСообщение.Данные = ДанныеСообщения.Данные;
	КонецЕсли;
	
	Если ДанныеСообщения.Действия <> Неопределено И ТипЗнч(ДанныеСообщения.Действия) = Тип("ХранилищеЗначения") Тогда
		ДанныеСообщения.Действия = ДанныеСообщения.Действия.Получить();
	КонецЕсли;
	
	Если ДанныеСообщения.Действия <> Неопределено И ТипЗнч(ДанныеСообщения.Действия) = Тип("СписокЗначений") Тогда
		Для Каждого СтрокаТаблицыИсточник Из ДанныеСообщения.Действия Цикл
			СтрокаПриемника = НовоеСообщение.Действия.Добавить();
			СтрокаПриемника.Значение = СтрокаТаблицыИсточник.Значение;
			СтрокаПриемника.Текст = СтрокаТаблицыИсточник.Представление;
		КонецЦикла;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Сообщение", НовоеСообщение);
	Результат.Вставить("Обсуждение", Обсуждение);
	Возврат Результат;
	
КонецФункции

// Создать и отправить сообщение системы взаимодействия
//
// Параметры:
//  ДанныеСообщения		 - Структура
//  ВызыватьИсключение	 - Булево - Не вызывать исключение, если пользователь системы взаимодействия не существует.
// 
// Возвращаемое значение:
//  Булево - Признак отправлено ли сообщение в систему взаимодействия.
//
Функция СоздатьИОтправитьСообщениеСистемыВзаимодействия(ДанныеСообщения, ВызыватьИсключение = Истина)
	
	СообщениеОтправлено = Ложь;
	СообщениеСистемыВзаимодействия = Неопределено;
	
	Попытка
		Результат = НовыйСообщениеСистемыВзаимодействия(ДанныеСообщения, ВызыватьИсключение);
		Если Результат <> Неопределено Тогда
			СообщениеСистемыВзаимодействия = Результат.Сообщение;
			ОбсуждениеСистемыВзаимодействия = Результат.Обсуждение;
		КонецЕсли;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ЗаписьЖурналаРегистрации(ИмяСобытияЖР(), УровеньЖурналаРегистрации.Ошибка,, ДанныеСообщения.Объект, ТекстОшибки);
	КонецПопытки;
	
	Если СообщениеСистемыВзаимодействия = Неопределено Тогда
		Возврат СообщениеОтправлено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СообщениеСистемыВзаимодействия.Автор) Тогда
		ТекстСообщения = НСтр("ru='Не удалось заполнить Автора сообщения СВ по данным пользователя справочника: ""%1"".'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Строка(ДанныеСообщения.Автор));
		ЗаписьЖурналаРегистрации(ИмяСобытияЖР(), УровеньЖурналаРегистрации.Ошибка,, ДанныеСообщения.Объект, ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		Если ОбсуждениеСистемыВзаимодействия.КонтекстОбсуждения <> Неопределено Тогда
			// Снять режим наблюдения, чтобы пользователь не получал уведомление сам себе.
			БылРежимНаблюдения = СистемаВзаимодействия.ПолучитьРежимНаблюдения(ОбсуждениеСистемыВзаимодействия.Идентификатор);
			Если БылРежимНаблюдения Тогда
				СистемаВзаимодействия.УстановитьРежимНаблюдения(ОбсуждениеСистемыВзаимодействия.Идентификатор, Ложь);
			КонецЕсли;
		КонецЕсли;
		
		СообщениеСистемыВзаимодействия.Записать();
		СообщениеОтправлено = Истина;
		
		Если ОбсуждениеСистемыВзаимодействия.КонтекстОбсуждения <> Неопределено Тогда
			// Вернуть снятый режим наблюдения.
			Если БылРежимНаблюдения Тогда
				СистемаВзаимодействия.УстановитьРежимНаблюдения(ОбсуждениеСистемыВзаимодействия.Идентификатор, Истина);
			КонецЕсли;
		КонецЕсли;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ЗаписьЖурналаРегистрации(ИмяСобытияЖР(), УровеньЖурналаРегистрации.Ошибка,, ДанныеСообщения.Объект, ТекстОшибки);
	КонецПопытки;
	
	Возврат СообщениеОтправлено;
	
КонецФункции

Функция ПользовательПодходитДляОтправкиПриемаСообщения(ПользовательСправочника) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеАвтора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПользовательСправочника, "Недействителен,ПометкаУдаления,ИдентификаторПользователяИБ");
	Если ДанныеАвтора.Недействителен
		Или ДанныеАвтора.ПометкаУдаления
		Или Не ЗначениеЗаполнено(ДанныеАвтора.ИдентификаторПользователяИБ) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция ИмяСобытияЖР()
	
	Возврат НСтр("ru='ОбсужденияУНФ'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

Функция ЭтоСеансАвторизованногоПользователя()
	
	ТекущийПользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
	Возврат ЗначениеЗаполнено(ТекущийПользователь.Имя);
	
КонецФункции

Функция ПроверитьУстановитьПользователяДляОтправкиПриемаСообщений(ПользовательДляОтправкиПриема)
	
	Если ЭтоСеансАвторизованногоПользователя() Тогда
		АвторизованныйПользователь = Пользователи.АвторизованныйПользователь();
		
		Возврат ПользовательПодходитДляОтправкиПриемаСообщения(АвторизованныйПользователь);
	КонецЕсли;
	
	НовыйПользователь = Неопределено;
	
	Если ЗначениеЗаполнено(ПользовательДляОтправкиПриема) Тогда
		Если ПользовательПодходитДляОтправкиПриемаСообщения(ПользовательДляОтправкиПриема) Тогда
			НовыйПользователь = ПользовательДляОтправкиПриема;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НовыйПользователь) Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	НеотправленныеСообщенияСистемыВзаимодействия.Автор КАК Автор,
		|	МАКСИМУМ(НеотправленныеСообщенияСистемыВзаимодействия.Дата) КАК ДатаПоследнегоСообщения
		|ИЗ
		|	Справочник.НеотправленныеСообщенияСистемыВзаимодействия КАК НеотправленныеСообщенияСистемыВзаимодействия
		|ГДЕ
		|	(&БезОтбораПоАвтору
		|			ИЛИ НеотправленныеСообщенияСистемыВзаимодействия.Автор <> &Автор)
		|
		|СГРУППИРОВАТЬ ПО
		|	НеотправленныеСообщенияСистемыВзаимодействия.Автор
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаПоследнегоСообщения УБЫВ");
		Запрос.УстановитьПараметр("БезОтбораПоАвтору", Не ЗначениеЗаполнено(ПользовательДляОтправкиПриема));
		Запрос.УстановитьПараметр("Автор", ПользовательДляОтправкиПриема);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка.Автор)
				И ПользовательПодходитДляОтправкиПриемаСообщения(Выборка.Автор) Тогда
				НовыйПользователь = Выборка.Автор;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НовыйПользователь) Тогда
		Если НовыйПользователь <> ПользовательДляОтправкиПриема Тогда
			Отбор = Новый Структура;
			Отбор.Вставить("Метаданные", МетаданныеЗаданияЗаписиОтложенныхСообщений());
			Отбор.Вставить("Использование", Истина);
			
			СписокЗаданий = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
			Если СписокЗаданий.Количество() > 0 Тогда
				Параметры = Новый Структура;
				Параметры.Вставить("Параметры", Новый Массив);
				Параметры.Параметры.Добавить(НовыйПользователь);
				
				РегламентныеЗаданияСервер.ИзменитьЗадание(СписокЗаданий[0].УникальныйИдентификатор, Параметры);
			КонецЕсли;
		КонецЕсли;
		
		ИдентификаторПользователяИБ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НовыйПользователь, "ИдентификаторПользователяИБ");
		СистемаВзаимодействия.УстановитьТекущегоПользователя(ИдентификаторПользователяИБ);
	КонецЕсли;
	
	Возврат ЗначениеЗаполнено(НовыйПользователь);
	
КонецФункции

Функция МетаданныеЗаданияЗаписиОтложенныхСообщений()
	
	Возврат Метаданные.РегламентныеЗадания.ЗаписьОтложенныхСообщенийВСистемуВзаимодействия;
	
КонецФункции

#КонецОбласти
