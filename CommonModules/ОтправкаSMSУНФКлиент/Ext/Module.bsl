
#Область СлужебныйПрограммныйИнтерфейс

Процедура ОтправитьПоSMS(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	Если ПараметрКоманды.Количество() = 1
		И ЭтоПредметШаблона(ПараметрКоманды[0])
		И ЭлектроннаяПочтаУНФВызовСервера.ЕстьДоступныеШаблоны(Ложь, ПараметрКоманды[0]) Тогда
		ШаблоныСообщенийКлиент.СформироватьСообщение(ПараметрКоманды[0], "СообщениеSMS",,, Новый Структура("ИмяФормыИсточникаСообщения", ПараметрыВыполненияКоманды.Форма.ИмяФормы));
	Иначе
		ДополнительныеПараметрыПечати = Неопределено;
		
		ФормаИсточник = ПараметрыВыполненияКоманды.Форма;
		
		Если ФормаИсточник.ИмяФормы = "ЖурналДокументов.КомиссионнаяТорговля.Форма.ФормаСписка" Тогда
			
			ИмяФормыОбъектаКоманды = Неопределено;
			
			ПолучитьФормуЭлементаЖурнала(ПараметрКоманды, ФормаИсточник, ИмяФормыОбъектаКоманды);
			
			Если ИмяФормыОбъектаКоманды = Неопределено Тогда
				Возврат;
			КонецЕсли;
			
		Иначе
			ИмяФормыОбъектаКоманды = ФормаИсточник.ИмяФормы;
		КонецЕсли;
		
		Если ТипЗнч(ПараметрКоманды) = Тип("Массив") Тогда
			ОбъектыОтправки = Новый ФиксированныйМассив(ПараметрКоманды);
		Иначе
			ОбъектыОтправки = Новый ФиксированныйМассив(Новый Массив);
		КонецЕсли;
		
		ПараметрыОтправкиПФ = Новый Структура("Получатели");
		ЭлектроннаяПочтаУНФВызовСервера.ВыбратьПолучателей(ОбъектыОтправки, ПараметрыОтправкиПФ);
		ОткрытьФорму("ОбщаяФорма.ПодготовкаНовогоSMS", ПараметрыОтправкиПФ);
		
	КонецЕсли;
	
	СтатистикаИспользованияФормКлиент.ПроверитьЗаписатьСтатистикуКоманды(
		"ОтправитьПоSMS",
		ПараметрыВыполненияКоманды.Форма);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПолучитьФормуЭлементаЖурнала(ПараметрКоманды, ФормаИсточник, ИмяФормыОбъектаПечати)
	Если ПараметрКоманды.Количество() Тогда
		Если ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ПереоценкаТоваровНаКомиссии") Тогда
			ИмяФормыОбъектаПечати = "Документ.ПереоценкаТоваровНаКомиссии.Форма.ФормаДокумента";
			ФормаИсточник = ПолучитьФорму("Документ.ПереоценкаТоваровНаКомиссии.Форма.ФормаДокумента")
		ИначеЕсли ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.РасходнаяНакладная") Тогда
			ИмяФормыОбъектаПечати = "Документ.РасходнаяНакладная.Форма.ФормаДокумента";
			ФормаИсточник = ПолучитьФорму("Документ.РасходнаяНакладная.Форма.ФормаДокумента")
		ИначеЕсли ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
			ИмяФормыОбъектаПечати = "Документ.ПриходнаяНакладная.Форма.ФормаДокумента";
			ФормаИсточник = ПолучитьФорму("Документ.ПриходнаяНакладная.Форма.ФормаДокумента")
		ИначеЕсли ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ОтчетКомиссионераОСписании") Тогда
			ИмяФормыОбъектаПечати = "Документ.ОтчетКомиссионераОСписании.Форма.ФормаДокумента";
			ФормаИсточник = ПолучитьФорму("Документ.ОтчетКомиссионераОСписании.Форма.ФормаДокумента")
		ИначеЕсли ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ОтчетКомиссионера") Тогда
			ИмяФормыОбъектаПечати = "Документ.ОтчетКомиссионера.Форма.ФормаДокумента";
			ФормаИсточник = ПолучитьФорму("Документ.ОтчетКомиссионера.Форма.ФормаДокумента")
		ИначеЕсли ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ОтчетОПереработке") Тогда
			ИмяФормыОбъектаПечати = "Документ.ОтчетОПереработке.Форма.ФормаДокумента";
			ФормаИсточник = ПолучитьФорму("Документ.ОтчетОПереработке.Форма.ФормаДокумента") 
		ИначеЕсли ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ОтчетПереработчика") Тогда
			ИмяФормыОбъектаПечати = "Документ.ОтчетПереработчика.Форма.ФормаДокумента";
			ФормаИсточник = ПолучитьФорму("Документ.ОтчетПереработчика.Форма.ФормаДокумента")
		ИначеЕсли ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			ИмяФормыОбъектаПечати = "Документ.ЗаказПоставщику.Форма.ФормаДокумента";
			ФормаИсточник = ПолучитьФорму("Документ.ЗаказПоставщику.Форма.ФормаДокумента")
		ИначеЕсли ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ПриходныйОрдер") Тогда
			ИмяФормыОбъектаПечати = "Документ.ПриходныйОрдер.Форма.ФормаДокумента";
			ФормаИсточник = ПолучитьФорму("Документ.ПриходныйОрдер.Форма.ФормаДокумента");
		ИначеЕсли ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.РасходныйОрдер") Тогда
			ИмяФормыОбъектаПечати = "Документ.РасходныйОрдер.Форма.ФормаДокумента";
			ФормаИсточник = ПолучитьФорму("Документ.РасходныйОрдер.Форма.ФормаДокумента");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ЭтоПредметШаблона(ПараметрКоманды)
	
	ТипыПредметовШаблонов = Новый Массив;
	ТипыПредметовШаблонов.Добавить(Тип("СправочникСсылка.Контрагенты"));
	ТипыПредметовШаблонов.Добавить(Тип("СправочникСсылка.КонтактныеЛица"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.АктВыполненныхРабот"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.ЗаданиеНаРаботу"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.ЗаказНаПроизводство"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.ЗаказПоставщику"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.ПриемИПередачаВРемонт"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.Событие"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.СчетНаОплату"));
	ТипыПредметовШаблонов.Добавить(Тип("ДокументСсылка.РасходнаяНакладная"));
	
	Возврат ТипыПредметовШаблонов.Найти(ТипЗнч(ПараметрКоманды)) <> Неопределено;
	
КонецФункции

#КонецОбласти
