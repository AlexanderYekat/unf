#Область ПрограммныйИнтерфейс

// Возвращает структуру дополнительных данных по штрихкоду
//
// Параметры:
//  Штрихкод - Строка - Штрихкод
//  ТипОбъекта - Тип - тип объекта, для которого определяется принадлежность штрихкода 
// 
// Возвращаемое значение:
//  Структура - Описание:
//   * ЭтоКодМаркировки - Булево - Истина, если штрихкод принадлежит к маркируемой продукции.
//   * ЕстьРазделительGS1 - Булево - Истина, если штрихкод содержит разделитель GS1 
//   * ПредставлениеШтрихкода - Строка - Представление штрихкода
//   * ТекстСообщения - Строка - Сообщение, отображаемое пользователю
//
Функция ДополнительныеДанныеПоШтрихкоду(Штрихкод, ТипОбъекта) Экспорт
	
	ДополнительныеДанныеПоШтрихкоду = ПараметрыПоШтрихкоду();
	ДанныеПоШтрихкоду = МенеджерОборудованияМаркировкаКлиентСервер.РазобратьШтриховойКодТовара(Штрихкод);
	
	Если ОбщегоНазначенияРМККлиентСервер.ЕстьСвойство(ДанныеПоШтрихкоду, "ТипИдентификатораТовара") Тогда
		ТипыШтрихкодовМарок = СформироватьСписокТиповШтрихкодовМарок();
		ТипИдентификатораТовара = ДанныеПоШтрихкоду.ТипИдентификатораТовара;
		ДополнительныеДанныеПоШтрихкоду.ЭтоКодМаркировки = НЕ ТипыШтрихкодовМарок.НайтиПоЗначению(ТипИдентификатораТовара) = Неопределено;
	КонецЕсли;
	
	Если ДополнительныеДанныеПоШтрихкоду.ЭтоКодМаркировки Тогда 
		ДополнительныеДанныеПоШтрихкоду.ТекстСообщения = СформироватьСообщениеПользователю(ТипОбъекта);
	Иначе
		РазделительGS1 = ОбщегоНазначенияБПОКлиентСервер.РазделительGS1();
		
		Если СтрНайти(Штрихкод,РазделительGS1) > 0 Тогда
			ДополнительныеДанныеПоШтрихкоду.ЕстьРазделительGS1 = Истина;
			
			Если ЗначениеЗаполнено(ДанныеПоШтрихкоду.ПредставлениеШтрихкода) Тогда 
				ДополнительныеДанныеПоШтрихкоду.ПредставлениеШтрихкода = ДанныеПоШтрихкоду.ПредставлениеШтрихкода;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДополнительныеДанныеПоШтрихкоду.ПредставлениеШтрихкода) Тогда 
				ДополнительныеДанныеПоШтрихкоду.ПредставлениеШтрихкода = ЗаменитьРазделителиВШтрихкоде(Штрихкод);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДополнительныеДанныеПоШтрихкоду;
	
КонецФункции 

// Возвращает представление штрихкода без разделителя GS1
//
// Параметры:
//  Штрихкод - Строка - Штрихкод
//
// Возвращаемое значение:
//   Строка - Представление штрихкода.
//
Функция ПредставлениеШтрихкодаБезРазделителяGS1(Штрихкод) Экспорт
	
	ПредставлениеШтрихкода = "";
	РазделительGS1 = ОбщегоНазначенияБПОКлиентСервер.РазделительGS1();
	
	Если СтрНайти(Штрихкод,РазделительGS1) > 0 Тогда 
		ДанныеПоШтрихкоду = МенеджерОборудованияМаркировкаКлиентСервер.РазобратьШтриховойКодТовара(Штрихкод);
		Если ЗначениеЗаполнено(ДанныеПоШтрихкоду.ПредставлениеШтрихкода) Тогда 
			ПредставлениеШтрихкода = ДанныеПоШтрихкоду.ПредставлениеШтрихкода; 
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ПредставлениеШтрихкода) Тогда 
			ПредставлениеШтрихкода = ЗаменитьРазделителиВШтрихкоде(Штрихкод);
		КонецЕсли;
	КонецЕсли;

	Возврат ?(ЗначениеЗаполнено(ПредставлениеШтрихкода),ПредставлениеШтрихкода,Штрихкод);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыПоШтрихкоду()
	
	Параметры = Новый Структура;
	Параметры.Вставить("ЭтоКодМаркировки"		, Ложь);
	Параметры.Вставить("ЕстьРазделительGS1"		, Ложь);
	Параметры.Вставить("ПредставлениеШтрихкода"	, "");
	Параметры.Вставить("ТекстСообщения"			, "");
	
	Возврат Параметры;
	
КонецФункции

Функция СформироватьСписокТиповШтрихкодовМарок()
	
	ТипыШтрихкодовМарок = Новый СписокЗначений;
	ИмяМаркировкиМех = "Перечисление.ТипыИдентификаторовТовараККТ.ИзделияИзНатуральногоМеха";
	ИмяМаркировкиGS1 =	"Перечисление.ТипыИдентификаторовТовараККТ.КодТовараВФорматеDataMatrixGS1";
	ИмяМаркировкиВФорматеЕГАИС2 = "Перечисление.ТипыИдентификаторовТовараККТ.КодТовараВФорматеЕГАИС2";
	ИмяМаркировкиВФорматеЕГАИС3 = "Перечисление.ТипыИдентификаторовТовараККТ.КодТовараВФорматеЕГАИС3";
	
	ТипыШтрихкодовМарок.Добавить(ПредопределенноеЗначение(ИмяМаркировкиМех));
	ТипыШтрихкодовМарок.Добавить(ПредопределенноеЗначение(ИмяМаркировкиGS1));
	ТипыШтрихкодовМарок.Добавить(ПредопределенноеЗначение(ИмяМаркировкиВФорматеЕГАИС2));
	ТипыШтрихкодовМарок.Добавить(ПредопределенноеЗначение(ИмяМаркировкиВФорматеЕГАИС3));

	Возврат  ТипыШтрихкодовМарок;
	
КонецФункции

Функция СформироватьСообщениеПользователю(ТипОбъекта)
	
	СформированноеСообщение = НСтр("ru='Считывание кодов маркировки в текущей форме не поддерживается. %1'");
	Если ТипОбъекта = Тип("ДокументСсылка.ПриходнаяНакладная") 
		ИЛИ ТипОбъекта = Тип("ДокументСсылка.РасходнаяНакладная") 
		ИЛИ ТипОбъекта = Тип("ДокументСсылка.ЗаказПокупателя")
		ИЛИ ТипОбъекта = Тип("ДокументСсылка.ОтчетОПереработке") 
		ИЛИ ТипОбъекта = Тип("ДокументСсылка.ОтчетПереработчика")
		ИЛИ ТипОбъекта = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
		Тогда
		СформированноеСообщение = СтрШаблон(СформированноеСообщение,НСтр("ru='Необходимо использовать специальную форму для маркируемой продукции.'"));
	Иначе
		СформированноеСообщение = СтрШаблон(СформированноеСообщение,"");
	КонецЕсли;
	
	Возврат СформированноеСообщение;
	
КонецФункции

Функция ЗаменитьРазделителиВШтрихкоде(Знач Штрихкод)
	
	РазделительGS1 = ОбщегоНазначенияБПОКлиентСервер.РазделительGS1();
	ПредставлениеШтрихкода = Штрихкод;
	
	Если СтрНайти(ПредставлениеШтрихкода,РазделительGS1) > 0 Тогда 
		ЭкранированныйСимволGS1 = ОбщегоНазначенияБПОКлиентСервер.ЭкранированныйСимволGS1();
		ПредставлениеШтрихкода = СтрЗаменить(ПредставлениеШтрихкода, РазделительGS1, ЭкранированныйСимволGS1);
	КонецЕсли;
	
	Возврат ПредставлениеШтрихкода;
	
КонецФункции

#КонецОбласти