
#Область ПрограммныйИнтерфейс

// Возвращает признак упрощенного зачета взносов, подлежащих уплате
// Федеральный закон от 31.07.2023 N 389-ФЗ
//
// Параметры:
//   Организация               - СправочникСсылка.Организации - ИП, который уплачивает фиксированные взносы
//   ПериодСобытия             - Дата - Период, за который оплачиваются фиксированные взносы
//   УчитыватьПереходныйПериод - Булево - Признак, при котором проверяется дата перехода на упрощенный зачет взносов
//
// Возвращаемое значение:
//   Булево
//
Функция УпрощенныйЗачетФиксированныхВзносов(Организация, ПериодСобытия, УчитыватьПереходныйПериод = Истина) Экспорт
	
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Организация)
		Или УчитыватьПереходныйПериод И '20230901' > ПериодСобытия Тогда 	
		Возврат Ложь;
	КонецЕсли;
	
	ПериодДействияЗакона = '20230101';  
	
	СистемаНалогообложенияОрганизации = РегистрыСведений.СистемыНалогообложенияОрганизаций.ОпределитьСистемуНалогообложенияОрганизации(Организация, ПериодСобытия);
	
	ПрименяетсяУСНДоходы = СистемаНалогообложенияОрганизации.ОбъектНалогообложения = Перечисления.ВидыОбъектовНалогообложения.Доходы;
	ПрименяетсяУСНДоходыМинусРасходы = СистемаНалогообложенияОрганизации.ОбъектНалогообложения = Перечисления.ВидыОбъектовНалогообложения.ДоходыМинусРасходы;
	ПлательщикНДФЛ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ИПИспользуетТрудНаемныхРаботников");
	
	Если Не ПлательщикНДФЛ И Не ПрименяетсяУСНДоходыМинусРасходы Тогда
		ПрименяетсяПСН = СистемаНалогообложенияОрганизации.ПрименяетсяПатент;
	Иначе
		// При совмещение с ОСН или УСН доходы минус расходы считаем, что порядок зачета через пополнение ЕНС и резервирование сумм
		ПрименяетсяПСН = Ложь;
	КонецЕсли;
	
	Возврат ПериодСобытия >= ПериодДействияЗакона И (ПрименяетсяУСНДоходы Или ПрименяетсяПСН);
	
КонецФункции 

// Возвращает признак уплаты взноса с доходов свыше предела
// Взнос платится в периоды, когда не действует пониженный тариф фиксированных взносов (ст. 430 НК РФ)
//
// Параметры:
//   Организация   - СправочникСсылка.Организации - ИП, для которого необходимо рассчитать признак уплаты взносов с доходов
//   НачалоПериода - Дата - Дата начала периода
//   КонецПериода  - Дата - Дата окончания периода
//
// Возвращаемое значение:
//   Булево
//
Функция УплачиваетсяВзносСДоходов(Организация, НачалоПериода, КонецПериода) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация)
		Или РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Организация) Тогда
		// Если в периоде платился только НПД, тогда не нужно платить взносы с доходов
		Возврат Ложь;
	КонецЕсли;
	
	// Если учет еще не ведется или передан год до первого года ведения учета,
	// доходы за предыдущие годы заведомо нулевые - взноса не может быть
	ДатаНачалаУчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ДатаРегистрации");
	Если ДатаНачалаУчета = Неопределено Или Год(ДатаНачалаУчета) > Год(НачалоПериода) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Если в периоде нужно было платить взносы не по пониженному тарифу, то тогда необходимо рассчитать взнос с доходов за этот период
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ИСТИНА КАК ПоЕдиномуТарифу
	|ИЗ
	|	РегистрСведений.НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС КАК НастройкиУчетаСтраховыхВзносовИП
	|ГДЕ
	|	НастройкиУчетаСтраховыхВзносовИП.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НастройкиУчетаСтраховыхВзносовИП.Организация = &Организация
	|	И НастройкиУчетаСтраховыхВзносовИП.ПлательщикСтраховыхВзносовПФР_ФФОМС
	|	И НЕ НастройкиУчетаСтраховыхВзносовИП.Тариф В (&Тарифы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМС.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМССрезПоследних
	|ГДЕ
	|	НЕ НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМССрезПоследних.Тариф В (&Тарифы)
	|	И НастройкиУчетаСтраховыхВзносовИП_ПФР_ФФОМССрезПоследних.ПлательщикСтраховыхВзносовПФР_ФФОМС");
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Тарифы", ПониженныеТарифыСтраховыхВзносов());
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПониженныеТарифыСтраховыхВзносов() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить(Перечисления.ИПТарифыФиксированныхВзносов.ВоенныйПенсионер); // п.1.4 ст.430 НК РФ
	Результат.Добавить(Перечисления.ИПТарифыФиксированныхВзносов.УчастникСЭЗнаНТ); // п.1.3 ст.430 НК РФ
	
	Возврат Результат
	
КонецФункции

#КонецОбласти