#Область ПрограммныйИнтерфейс

// Может использоваться для корректировки параметра ПараметрыСканирования.ККТФФД12ИСМП, например,
// в том случае, когда в процессе сканирования уточнена номенклатура, и эта номенклатура должна фискализироваться на 
// устройстве, отличном от установленно ранее в параметре ПараметрыСканирования.ККТФФД12ИСМП.
// 
// Параметры:
//  ДанныеОписания        - Структура - Может быть двих разновидностей:
//                                      1. Данные штрихкода - в случае обработке на сервере.
//                                      2. Данные выбора    - при сканировании неизвестного года и уточннения
//                                                            номенклатуры (при обработке на клиенте).
//                                      В обоих случаях в данных присутствуют поля описания: Номенклатура, Характеристика, Серия.
//  ПараметрыСканирования - (См. ШтрихкодированиеОбщегоНазначенияИСКлиент.ПараметрыСканирования).
Процедура УстановитьККТФФД12ПоДаннымОписанияТовара(ДанныеОписания, ПараметрыСканирования) Экспорт
	
	ИдентификаторУстройства = Неопределено;
	
	ЭтоВызовИзРМК = ПараметрыСканирования.Свойство("ДополнительныеПараметры")
		И ТипЗнч(ПараметрыСканирования.ДополнительныеПараметры) = Тип("Структура")
		И ПараметрыСканирования.ДополнительныеПараметры.Свойство("ПарамтерыПоискаРМК");
		
	Если ЭтоВызовИзРМК Тогда
		ПарамтерыПоискаРМК = ПараметрыСканирования.ДополнительныеПараметры.ПарамтерыПоискаРМК;
		ДанныеШтрихкода = ДанныеОписания;
		
		Если ДанныеШтрихкода.Свойство("ВнешниеДанныеПоШтрихкодам")
			И ДанныеШтрихкода.ВнешниеДанныеПоШтрихкодам.Количество() > 0
			И ДанныеШтрихкода.ВнешниеДанныеПоШтрихкодам[0].ЗначенияПоиска.Количество() > 0 Тогда
			ЗначениеПоиска = ДанныеШтрихкода.ВнешниеДанныеПоШтрихкодам[0].ЗначенияПоиска[0];
			ОрганизацияПродажи = ЗначениеПоиска.СтруктураНайденнойНоменклатуры.Организация;
			
			ИдентификаторУстройства = ПарамтерыПоискаРМК.СоответствияККТОрганизаций.Получить(ОрганизацияПродажи);
			
			Если Не ЗначениеЗаполнено(ИдентификаторУстройства) Тогда
				ИдентификаторУстройства = ПарамтерыПоискаРМК.СоответствияККТОрганизаций.Получить(ПараметрыСканирования.Организация);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИдентификаторУстройства) Тогда
		
		ТипОборудования = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("ККТ");
		
		СписокДоступныхУстройств =  МенеджерОборудованияВызовСервера.ОборудованиеПоПараметрам(ТипОборудования);
		Если СписокДоступныхУстройств.Количество() = 1 Тогда
			ИдентификаторУстройства = СписокДоступныхУстройств[0].Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторУстройства) Тогда
		
		Если МенеджерОборудованияВызовСервера.ФискальноеУстройствоПоддерживаетПроверкуКодовМаркировки(ИдентификаторУстройства) Тогда
			ПараметрыСканирования.ТребуетсяПроверкаСредствамиККТ = Истина;
			ПараметрыСканирования.ККТФФД12ИСМП                   = ИдентификаторУстройства;
			ПараметрыСканирования.НомерФискальногоНакопителя     = ИнтеграцияИСУНФВызовСервера.ЗаводскойНомерФискальногоНакопителя(ИдентификаторУстройства);
		КонецЕсли;
		
		ПараметрыСканирования.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
		
	КонецЕсли;
	
	Если ПараметрыСканирования.ТребуетсяПроверкаСредствамиККТ Тогда
		ПараметрыСканирования.ТребоватьПолныйКодМаркировкиИСМП = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при получении элеметнов проверки средставами ККТ для дозаполнения свойств прикладными данными.
// Используется для заполнения полей, которые будут переданы в методы проверки на ККТ, например, КодЕдиницыИзмерения.
// 
// Параметры:
//  ДанныеДляОбработки - Массив из см. ШтрихкодированиеИСМПКлиентСервер.НовыйЭлементОбработкиУстановкиДополнительныхСвойствПриПроверкеККТ
Процедура ПриУстановкеДополнительныхСвойствЭлеметовПроверкиСредствамиККТ(ДанныеДляОбработки) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Вызывается при необходимости дополнить параметры проверки кодов маркировки средствами ККТ прикладными данными.
// Используется для заполнения полей параметров проверки, например, ПроверятьЗапросыГИСМТ, на основе данных проверки условий
// необходимости подчинения разрешительному режиму.
// 
// Параметры:
//  ПараметрыПроверки - см. ШтрихкодированиеОбщегоНазначенияИСМПКлиент.ПараметрыНачалаПроверкиКодовМаркировкиСредствамиККТ
//  ПараметрыСканирования - см. ШтрихкодированиеОбщегоНазначенияИСКлиент.ПараметрыСканирования
Процедура ПриУстановкеДополнительныхПараметровПроверкиКодовМаркировкиСредствамиККТ(ПараметрыПроверки, ПараметрыСканирования) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти
